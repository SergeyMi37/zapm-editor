ROUTINE %ZAPM.bs.BSpgWEBC
TKWEBC	;PG&A,TK-WEB,1.1,SERVER CONTROL;08OCT97  17:58 [ 12/18/97  10:26 AM ];;;21JUN2000  07:51
	;;Copyright (C) 1997 Patterson, Gray and Associates, Inc.
	;
HTTP	S (port,type)=80,TRACE=1,LOG=1
 I '$D(^%ZAPM.bs.BSpgWEBC("Control")) S ^("Control")="1,,1,,5,,80"
	S port=$P(^("Control"),",",7)
	; Determine OS type, NT or not NT
startup L ^%ZAPM.bs.BSpgWEBC(port):0 E  H
 S OS=^%ZAPM.bs.BSpg("OS") S @$S(OS'="MSM":"nt=0",1:"nt=($ZB($V(0,-4,2),#F,1)=10)")
	Q:OS="M3"
	;
	; Open and initialize the device
CONNECT ;I type=80 
 ;L +CONNECT 
 S CON=$I(^%ZAPM.bs.BSpgWEBC("CONNECT",port)) I $G(^%ZAPM.bs.BSpgWEBC("Stoping",port)) K @$ZR H 2 H
 ;I type=25 L +MCONNECT S CON=$G(^%ZAPM.bs.BSpgWEMM("CONNECT")) L -MCONNECT I CON["." S ^%ZAPM.bs.BSpgWEMM("CONNECT")=1 H 2 H
 ;I type=110 L +MSCONNECT S CON=$G(^%ZAPM.bs.BSpgWEMM1("CONNECT")) L -MSCONNECT I CON["." S ^%ZAPM.bs.BSpgWEMM1("CONNECT")=1 H 2 H
	I type=80 S ST=$G(conTrol,^%ZAPM.bs.BSpgWEBC("Control")) I $G(ST)]"" S port=$S($P(ST,",",7):$P(ST,",",7),1:type),TRACE=$P(ST,",")="ON",LOG=$P(ST,",",3)="ON"
	I OS="MSM" D initMSM
	I OS="M11" D initM11 
	I OS="DSM4" D initDSM4
	G:$S(OS="MSM":'nt,OS="M11":$ZV'["Cache",1:1) unix
CB G:OS="M11" CBM11 i type=80 J accept^%ZAPM.bs.BSpgWEBH($ZH(%xddb),CON,TRACE,LOG):130:0 S jobno=$ZB
 i type=25 J accept^%ZAPM.bs.BSpgWEMM($ZH(%xddb),CON,TRACE,LOG):60:0 S jobno=$ZB
 i type=110 J accept^%ZAPM.bs.BSpgWEMP3($ZH(%xddb),CON,TRACE,LOG):60:0 S jobno=$ZB
	;
	; If there is no partition available, close spawned socket
 I jobno=0 H 1 S RTRY=$G(RTRY)+1 G:RTRY<20 CB S ^%ZAPM.bs.BSpgWEBC("NOJOB",$H,%xddb)=%lddb_"_"_type C 56:(%xddb) K RTRY
	G CONNECT
CBM11 i type=80  J accept^%ZAPM.bs.BSpgWEBH(0,CON,TRACE,LOG):(:4:TCP:TCP)
 i type=25 J accept^%ZAPM.bs.BSpgWEMM(0,CON,TRACE,LOG):(:4:TCP:TCP)
 i type=110 J accept^%ZAPM.bs.BSpgWEMP3(0,CON,TRACE,LOG):(:4:TCP:TCP)
	;
 ; I jobno=0 H 1 S RTRY=$G(RTRY)+1 G:RTRY<20 CB S ^%ZAPM.bs.BSpgWEBC("NOJOB",$H,%xddb)=%lddb_"_"_type C 56:(%xddb) K RTRY
	G CONNECT
	;
unix I OS="MSM" J USTART^%ZAPM.bs.BSpgWEBC(type,TRACE,LOG,OS,nt):60:15 E  S ^%ZAPM.bs.BSpgWEBC("NOJOB",$H,%xddb)=%lddb_"_"_type G CONNECT
 I OS="M11" J USTART^%ZAPM.bs.BSpgWEBC(type,TRACE,LOG,OS,nt)::15 E  S ^%ZAPM.bs.BSpgWEBC("NOJOB",$H)=type G CONNECT
 I OS="DSM4" J USTART^%ZAPM.bs.BSpgWEBC(type,TRACE,LOG,OS,nt)::15 E  S ^%ZAPM.bs.BSpgWEBC("NOJOB",$H)=type G CONNECT
 i type=80 D uaccept^%ZAPM.bs.BSpgWEBH(CON,TRACE,LOG,%xddb,TCP,OS) H
 i type=25 D uaccept^%ZAPM.bs.BSpgWEMM(CON,TRACE,LOG,%xddb,TCP,OS) H
 i type=110 D uaccept^%ZAPM.bs.BSpgWEMP3(CON,TRACE,LOG,%xddb,TCP,OS) H
	G CONNECT
USTART(type,TRACE,LOG,OS,nt) S $ZT="ERROR^%ZAPM.bs.BSpgWEBC" G CONNECT
	;
GMT(X,T)	;($H,timezone)
	N K,Y,D,M S T=$P(X,",",2)+(-T*3600) S:T<0 X=X-1,T=T+86400 S:T>86400 X=X+1,T=T-86400
	S K=X>21608+305+X,Y=4*K+3\1461,D=K*4+3-(1461*Y)+4\4,M=5*D-3\153,D=5*D-3-(153*M)+5\5,M=M+2,Y=M\12+Y-60,M=M#12+1,DT=1900+Y*100+M*100+D
	S D=$P("Mon,Tue,Wed,Thu,Fri,Sat,Sun",",",X+3#7+1)_", "_D_" "_$P("Jan,Feb,Mar,Apr,May,Jun,Jul,Aug,Sep,Oct,Nov,Dec",",",M)_" "_(1900+Y)
	S M=T\60,M=$E(M\60+100,2,3)_":"_$E(M#60+100,2,3)_":"_$E(T#60+100,2,3) Q D_" "_M_" GMT"
	;
	;
JULIAN	S m=+T-3,d=$P(T,"/",2),y=$P(T,"/",3) I (m+d+y)<-2 S T="" Q
	S:y<100 y=y+1900 S o=$S(y<1900:-14917,1:21608),y=y>1999*100+$E(y,3,4) S:m<0 m=m+12,y=y-1 S T=1461*y\4,T=153*m+2\5+T+d+o K m,y,o,d Q
	;
SMTP	S (port,type)=25,TRACE=1,LOG=1,port=$P($G(^%ZAPM.bs.BSpgWEMM("HOST",$ZCONVERT($ZU(110),"U")),"25,110"),",",1)
 G startup
POP3	S (port,type)=110,TRACE=0,LOG=1,port=$P($G(^%ZAPM.bs.BSpgWEMM("HOST",$ZCONVERT($ZU(110),"U")),"25,110"),",",2)
 G startup
HTTPS	S port=443,type=80,TRACE=1,LOG=1 G startup
	;
initMSM	I '$D(TCP) C 56 O 56:(:$S(nt:8,1:0)+0):"TCP" U 56 S %lddb=$KEY,TCP=56
listen	; Process the listen state
	U 56:(%lddb) W /SOCKET("",port) S %xddb=$KEY,lza=$ZA,lzb=$ZB,lzc=$ZC
	I lzc&(lzb=-8) G listen
	I lzc G initMSM
	Q
initM11	I $D(TCP) U TCP W *-2
	I '$D(TCP) S TCP="|TCP|"_port O TCP:(:port:"AS":$C(10)) ; watch $C(10)
	U TCP R *%xddb:10 
	I $G(^%ZAPM.bs.BSpgWEBC("Stoping",port)) K @$ZR H 2 H
	I %xddb=-1 G initM11
	Q
initDSM4 S TCP="80" S $ZT="WAIT^%ZAPM.bs.BSpgWEBC"
	O TCP:TCPCHAN U TCP R %xddb#1 Q
WAIT	H 2 G initDSM4
	;
	;
ERROR S ^%ZAPM.bs.BSpgWEBF("ERROR","TKWEBC",$H)=$ZE Q
	;
PORT() //НОМЕР ПОРТА TCP-SOCKETA
 N p S p=$P($G(conTrol,^%ZAPM.bs.BSpgWEBC("Control")),",",7) I p="" Q ""
  L ^%ZAPM.bs.BSpgWEBC(p):0 I  L -^%ZAPM.bs.BSpgWEBC(p) Q ""
 Q p
DOWN(X) F port=80,25,110 L ^%ZAPM.bs.BSpgWEBC(port):0 E  D STOPS(port)
 L  Q
 Q
STOPS(port)  ;I port=1962 
 S ^%ZAPM.bs.BSpgWEBC("Stoping",port)=1
 Q:$G(^%ZAPM.bs.BSpg("OS"))'="MSM"  D T2 H 1 
 Q
T2	C 56 O 56::"TCP" U 56 S %lddb=$KEY
T3	U 56:(%lddb) W /SOCKET(X,port) I $ZC&($ZB=-8) H 2 G T3
	I $ZC S C=$ZC Q
	W ! C 56 Q
STARTMAIL I $D(^%ZAPM.bs.BSpgWEMM) D  //Загрузка почтовых серверов
 .J SMTP^%ZAPM.bs.BSpgWEBC,POP3^%ZAPM.bs.BSpgWEBC
 Q
STOPMAIL S port=$P($G(^%ZAPM.bs.BSpgWEMM("HOST",$ZCONVERT($ZU(110),"U")),"25,110"),",",1) L ^%ZAPM.bs.BSpgWEBC(port):0 E  D STOPS(25)
 S port=$P($G(^%ZAPM.bs.BSpgWEMM("HOST",$ZCONVERT($ZU(110),"U")),"25,110"),",",2) L ^%ZAPM.bs.BSpgWEBC(port):0 E  D STOPS(110)
 L  Q
 Q
STARTWEB S port=$P($G(conTrol,^%ZAPM.bs.BSpgWEBC("Control")),",",7) 
 L ^%ZAPM.bs.BSpgWEBC(port):0 E  w !,"Уже запущен на порту "_port Q
 L  J HTTP^%ZAPM.bs.BSpgWEBC::0 i  w !,"Запустили" q
 w !,"запуск неудался" 
 Q
STOPWEB  S port=$P($G(conTrol,^%ZAPM.bs.BSpgWEBC("Control")),",",7) L ^%ZAPM.bs.BSpgWEBC(port):0 E  D STOPS(port)
 L  Q
 Q
