ROUTINE %ZAPM.bs.BSCZSTU
%BSCZSTU //ЗАПУСК ЗАДАНИЙ ПРИ СТАРТЕ СИСТЕМЫ
 ;
 ;    Copyright Serge W. Mikhaylenko
 ;    All Rights Reserved
 ;
 ;
 i '$D(^%ZAPM.bs.BSS),$$EXIST^%R("%BSCEXE.INT") D First //импорт массивов при первоначальной установке
 D LOADCFG
 k ^%ZAPM.bs.BSpgWEBL("LOG") //Удаления лога TK-WEB
 D SDemon //ЗАПУСК ДЕМОНА
 D SFTP //ЗАПУСК МОЕГО FTP СЕРВЕРА
 ;D upg^%ZAPM.bs.BSChW(1) //ЗАПУСК РЕКОМПИЛИРОВАНИЯ Б.ДАННЫХ ДЛЯ 5 ВЕРСИИ
 ;D START^%ZMGWSI(0) //PHP итегратор 
 I $$EXIST^%R("%BSpgWEBC.INT") d
 .S A=$$GETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Atkweb") I A'="" J ^%ZAPM.bs.BSpgWEBC d  //основной Веб сервер ИП на порту 1962
 ..d start^%ZAPM.bs.BSpgBS(1966) //дополнительный Веб сервер для агента - напоминателя
 .S A=$$GETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Atkmail") I A'="" D STARTMAIL^%ZAPM.bs.BSpgWEBC //MAIL SERVERS TKWEB
 I $$EXIST^%R("%BSpgTEL.INT")  S A=$$GETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Aterm") d
 .I A'=""  J ^%ZAPM.bs.BSpgTEL //TCP-SOCKET
 //I $$EXIST^%R("%BSCmdapi.INT")  S A=$$GETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Atmdapi") d
 //.I A'="" D start^%ZAPM.bs.BSCmdapi
 I $$EXIST^%R("%BSkevFT.INT")  S A=$$GETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Atmdapi") d
 .I A'="" D start^%ZAPM.bs.BSkevFT
 D TelnetPort
 Q
SDemon L +%BSDemon:0 I  S NewStart="" L -%BSDemon:0 J START^%ZAPM.bs.BSC4demon Q  //Запуск демона
 D GTmp^%ZAPM.bs.BSC4demon      s NewStart=$P($G(@GTmp),"~",1)
 Q
SFTP //  S A="""c:\Program Files\MSW-Tools\FTP-Server\autorun.exe"" c:\Program Files\MSW-Tools\FTP-Server\ftp-server.exe"
 n a,aa,i H 15
 s a=$tr($$GETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Aftp"),"/","\") I a'="" f i=1:1 q:$p(a,"?",i)=""  s aa=$p(a,"?",i) i $ZF(-2,aa)
 Q
TelnetPort //Определить порт TELNET и WEB сервера из файла текущей конфигурации
 N MS,FNINI
 S FNINI=$P($ZU(86),"*")
 d INI^%ZAPM.bs.BSCindx(FNINI,.MS)
 k ^%ZAPM.bs.BS("Telnet") M ^%ZAPM.bs.BS("Telnet")=MS("Telnet")
 k ^%ZAPM.bs.BS("Startup") M ^%ZAPM.bs.BS("Startup")=MS("Startup")
 Q
First N tmp,f,I,R,II s tmp=$zu(86)
 s f=$P(tmp,"\",1,$L(tmp,"\")-1)_"\Mgr\cboot.log"
 o f:"RSU":5 E  Q
 U f R R#3000 C f S I=$P($P(R,"Source directory is ",2),$C(13)) I I="" Q
 I $$ver^%ZAPM.bs.BSC=$P($G(^ROUTINE("%BS",0,1)),"///",2) Q  //СОВПАДАЮТ ВЕРСИИ МАССИВОВ И ПРОГРАММ
 F f="bsc","bscache","bsetup","BScBDPROFILE" I $$%GIF^%ZAPM.bs.BSCEXE(I_"bs-portal\"_f_".gof")
 F II="MAC","INC" i $d(@("^tempBSr"_II)) D  K @("^tempBSr"_II)  //восстановить исходники у программ MAC, INC
 .S I="" F  S I=$O(@("^tempBSr"_II)@(I)) Q:I=""  K f,err m f=@("^tempBSr"_II)@(I,0) D ROUTINE^%R(I_"."_II,.f,.err,"CS",0) M ^mtempBSimport(I)=err
 D INIT^%ZAPM.bs.BSChW
 //восстановить виртуальные каталоги портала
 //...
 q
 //D Tex^%ZAPM.bs.BSMSMF("UpdateVersion",V),SYSMSG^%ZAPM.bs.BSCek("UpdateVersion "_V,"W")
 Q
TK ; TKWEB ;ДИСПЕТЧЕР ПРОГРАММНЫХ ССЫЛОК  
 G TK^%ZAPM.bs.BSpgBS
RELOAD  K ^%ZAPM.bs.BS("Config")
LOADCFG //ЗАГРУЗИТЬ КОНФИГУРАЦИЮ И ИНИЦИАЛИЗИРОВАТЬ НАЧАЛЬНЫЕ АТРИБУТЫ
 N MS,BD,I,II,III,FNINI,DIRbase     S FNINI=$P($zconvert($ZU(86),"L"),"cache.cpf")
 d INI^%ZAPM.bs.BSCindx(FNINI_"B4Y-START.INI",.MS)
 s I=$G(MS("Config","SET")),(DIRbase,II)=$G(MS("Config","Dir"_I)) I $G(II)="" Q
 S III=$G(MS("Config","Name"_I)) I III="" Q
 I III=$$CFG^%ZAPM.bs.BSCp,$ZU(110)=$$CFGSN^%ZAPM.bs.BSCp Q  //ПОКА ТАК, НО ПОТОМ ПРОВЕРЯТЬ ИЗМЕНИЛСЯ ЛИ II_"B4Y.INI"
 K MS,^%ZAPM.bs.BS("Config") d INI^%ZAPM.bs.BSCindx($TR(II_III,"\","/")_".INI",.MS)
 S ^%ZAPM.bs.BS("Config")=III //ДЕЛАЕМ ЕЕ ТЕКУЩАЯ КОНФИГУРАЦИЯ
 S ^%ZAPM.bs.BS("Config","Desc")=$G(MS("Config","Desc")) //КОМЕНТАРИЙ
 S ^%ZAPM.bs.BS("Config","File")=$TR(II_III,"\","/")_".INI"
 S ^%ZAPM.bs.BS("Config","Server")=$ZU(110)
 S BD="^%ZAPM.bs.BSCiP.Switch" K @BD@(III) ;ZW MS
 F I=1:1 Q:'$D(MS("Switch","R"_I))  D
 .S G1=$P(MS("Switch","R"_I),"=")
 .S G2=$P($P(MS("Switch","R"_I),"=",2)," ",1)
 .S @BD@(III,G1,"NEW")=G2 I '$D(@G2) M @G2=@G1
 F I=1:1 Q:'$D(MS("LocalSwitch","R"_I))  D
 .S G1=$P(MS("LocalSwitch","R"_I),"=")
 .S G2=$P($P(MS("LocalSwitch","R"_I),"=",2)," ",1)
 .S @BD@(III,G1,"NEW")=G2 I '$D(@G2) M @G2=@G1
 
 S I=$G(MS("Config","BSETUP")) I I'="" M:'$D(@I) @I=^%ZAPM.bs.BSETUP D SETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Acfg","V",I_"(""CFG"")")
 D CHANGE 
 S BD=$$KBD^%ZAPM.bs.BSF12("^%ZAPM.bs.BSC4(""PROTECT"")")
 I '$D(@BD@("B4I")) D INIT^%ZAPM.bs.BSC4reg
 
 S I=$G(MS("Config","WEBPathB4Y")) I I'="" S I=$$AddBase(DIRbase,I) S ^%ZAPM.bs.BS("Config","WEBPathB4Y")=$P(I," //") D SETTKWEB^%ZAPM.bs.BSCm2(I)
 S I=$G(MS("Config","Frame")) I I'="" S ^%ZAPM.bs.BS("Config","Frame")=$P(I," //")
 
 S i="" F  S i=$O(MS("Config",i)) Q:i=""  I $e(i,1)="v" S I=$G(MS("Config",i)) I I'="" S ^%ZAPM.bs.BS("Config",i)=$P(I," //") //все остальные параметры
 
 S II="" F  S II=$O(MS("Content",II)) Q:II=""   S I=$G(MS("Content",II)) I I'="" D FILE2($$AddBase(DIRbase,I),II)
 
 D WebAlias
 F I="term","tkmail","tkweb","tmdapi","tbsd" S II=$P($G(MS("Ports",I))," ") S:"0"[II II="" D SETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","A"_I,"V",II)
 d Save123^%ZAPM.bs.BSC4cfgP
 Q
AddBase(DIRbase,I) //ДОБАВИТЬ БАЗОВЫЙ ПУТЬ, ЕСЛИ ЗАДАН ПУТЬ НЕ ПОЛНЫЙ
 I $E(I,2)=":" Q $TR(I,"\","/")
 Q $TR(DIRbase_I,"\","/")
WebAlias S I="" F  S I=$O(MS("WebAlias",I)) Q:I=""  I $G(MS("WebAlias",I))'="" D
 .S (^%ZAPM.bs.BSpgWEBC("VirtualDir",$ZCONVERT(I,"U")),^%ZAPM.bs.BSpgWEBC("VirtualDir",I))=$$AddBase(DIRbase,MS("WebAlias",I))
 Q
CHANGE  S I=$$GETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Admin")
 S II=$P($P(I,"@",2),">")
  I II'="",$ZCONVERT(II,"U")'=$ZU(110) S I=$$repl^%ZAPM.bs.BSCp2(I,II,"***"),I=$$repl^%ZAPM.bs.BSCp2(I,"***",$ZU(110)) D SETOPT^%ZAPM.bs.BSC4cfgP("^%ZAPM.bs.BSETUP(""ISS"")","Admin","V",I) W "сменили"
 Q
FILE2(I,F) N II,III D File2Arr^%ZAPM.bs.BSCEXE(I,.III)
 S III="",II="" F  S II=$O(III(II)) Q:II=""  S III=$E(III_III(II),1,32767)
 S ^%ZAPM.bs.BS("Config",F)=III
 Q
