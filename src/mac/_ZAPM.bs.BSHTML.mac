ROUTINE %ZAPM.bs.BSHTML
%BSHTML ; пpогpамма ВЫГРУЗКИ ТАБЛИЦЫ В HTML ; 16:20   07.09.2001
 Q
HTML(TABLE) 
 ;Header of HTML Program
 N XB,YB,XE,YE,X,Y,I,SD,B,YT,XT,YXT,YdT,XdT,ASD,BS,ny,nx,d,d0,d1
 D ADD("<!--%BS VERSION "_$G(^%ZAPM.bs.BS)_"-->")
 D ADD("<HTML>")
 D ADD("<HEAD>")
 D ADD("<TITLE>"_$TR($P(@TABLE,"@"),S1,S2)_"</TITLE>")
 D ADD("</HEAD>")
 D ADD("<BODY>")
 D Wait^%ZAPM.bs.BSXfun("вывод в HTML")
 I $G(OUThtml) D TABCEL
 E  D TABLE
 D End
 Q
TABCEL ;ВЫВОД ТАБЛИЦЫ В HTML В ВИДЕ ТАБЛИЦЫ
 N n1 D ADD("<FONT FACE=""Courier New""><SMALL><SMALL><TABLE BORDER=0>")
 F ny=1:1 Q:'$D(@TABLE@(ny))  K S D  X $G(WA)
 .D ADD("<TR>")
 .F nx=1:1 Q:'$D(@TABLE@(ny,nx))  K S D
 ..D CELL^%ZAPM.bs.BSCi D ADD("<TD>")
 ..S n1="" F  S n1=$O(S(n1)) Q:n1=""  D ADD($$HTm^%ZAPM.bs.BSre($TR(S(n1),S1,S2))_"<BR>")
 ..D ADD("</TD>")
 .D ADD("</TR>")
 D ADD("</TABLE></SMALL></SMALL></FONT>")
 Q
TABLE ;OF HTML
 D ADD("<CENTER><SMALL><SMALL><PRE>")
 F ny=1:1 Q:'$D(@TABLE@(ny))  K S D  X $G(WA)
 .F nx=1:1 Q:'$D(@TABLE@(ny,nx))  D CELL^%ZAPM.bs.BSCi
 .S nx="" F  S nx=$O(S(nx)) Q:nx=""  D ADD($$HTm^%ZAPM.bs.BSre($TR(S(nx),S1,S2)))
 D ADD("</PRE></SMALL></SMALL></CENTER>")
 Q
End ;of HTML Program
 D ADD("</BODY>")
 D ADD("</HTML>")
 Q
ADD(S) U %DEV W S,! U 0
 Q
BS2EXCEL(TABLE,GT) ;BS TO EXCEL ПРИЛОЖЕНИЕ ДЛЯ CACHE'  Bs2Exc.exe
 N TIX,TIY,TIT,XB,YB,XE,YE,X,Y,I,SD,B,YT,XT,YXT,YdT,XdT,ASD,BS,ny,nx,d,d0,d1,LNG,N,NN,NNN,KS,BSR,BST,TIP
 S (S2,S1)="" I $$IFDOS^%ZAPM.bs.BSHTML1($G(@TABLE)) D  ;ПЕРЕКОДИРОВАТЬ
 .K S1,S2 I $$ANSI^%ZAPM.bs.BSre("")
 S LNG=1000,N=0,NN="",KS=0,BSR=$P(TABLE,"("),BST=$QS(TABLE,1),TIP=$P(@TABLE,"@",17)
 D BS2TIT
 F ny=1:1 Q:'$D(@TABLE@(ny))  K S D
 .I TIP=5 S S(1)=$G(@TABLE@(ny)) ;ЭТО ТЕКСТ
 .E  F nx=1:1 Q:'$D(@TABLE@(ny,nx))  D:$P(TIT,",")=ny&($P(TIT,",",2)=nx)  D CELL^%ZAPM.bs.BSCi
 ..S TIX=$L($G(S(1)))+1,TIY=KS
 .S nx="" F  S nx=$O(S(nx)) Q:nx=""  S NNN=$TR(S(nx),S1,S2) D
 ..I ($L(NN)+$L(NNN))>LNG S N=N+1,@GT@(N)=$E(NN,1,$L(NN)-1),NN=""
 ..S NN=NN_NNN_$C(1),KS=KS+1
 S N=N+1,@GT@(N)=$E(NN,1,$L(NN)-1),@GT=KS
 S P4="$"_(TIY-2)_":$"_TIY,P5=TIX+1 ;ПЛЮС ПЕРВЫЙ ПРОБЕЛ В EXCEL
 Q +N
BS2TIT S TIT="1,1"
 I $P(@TABLE,"@",19) S TIT=$P(@TABLE,"@",19)
 E  I $P(@TABLE,"@",50) S TIT=$P(@TABLE,"@",50)
 Q
