ROUTINE %ZAPM.bs.BSpgWEMP3
TKWEBP3	;PG&A,TK-IMAIL,1.1,MAIL-POP3;29JUL97  12:35 [ 12/18/97  10:26 AM ];07OCT2003  10:06;;16DEC2002  23:04
	;;Copyright (C) 1997 Patterson, Gray and Associates, Inc.
	;
	Q
M3 S TRACE=1,LOG=1,$ZT="ERROR^%ZAPM.bs.BSpgWEMP3"
 S TKWEM="^%ZAPM.bs.BSpgWEMM" I $G(^%ZAPM.bs.BSpgWEMM("DATABASE"))]"" X ^("DATABASE")
	L +CONNECT S (CON,@TKWEM@("CONNECT"))=$G(@TKWEM@("CONNECT"))+1 L -CONNECT
	S ADDR=$P($ZDEV($P),"~"),OS="M3",TCP=0 G cont
	;
	;
accept(%ddb,CON,TRACE,LOG)	; Accept connection
	;
 S TKWEM="^%ZAPM.bs.BSpgWEMM",TKWE="^%ZAPM.bs.BSpgWEM" I $G(^%ZAPM.bs.BSpgWEMM("DATABASE"))]"" X ^("DATABASE")
 S $ZT="ERROR^%ZAPM.bs.BSpgWEMP3"
	I %ddb=0 S OS="M11",TCP=0,ADDR="" U TCP:(::"AS":$C(10)) G cont
	V %ddb+46::$J-$V(272,-4,4):2
	S addr=$V(%ddb+16,-3,0),ADDR=$V(addr+13,-3,$V(addr+12,-3,1),9)
	U 56:$ZH(+%ddb) S TCP=56
cont	S CNT=1,CR=$C(13,10),TMO=30,LOGON=0,ACT=0 D INIT,TDATE($H) S SYR=YR,SMT=MT
	;
LOGON	;
	U TCP W "+OK tkweb pop3 server ready at ",DTMA,CR
CMD	W ! I CNT>599 S @TKWEM@("RUNAWAY",CON)=$H G QUIT
	S TT=$H,Y="" R X:TMO S B=$ZB,C=$ZC G:X="" ERRD:C<0,ERRT:'$T,ERRE:X="" F  Q:$TR(X,$C(13),$C(10))[$C(10)  Q:C<0  S T=$P(TT,",",2)-$P($H,",",2)+TMO Q:T<0  R Y:1 S C=$ZC,X=X_Y
	I X[$C(31) S X=$P(X,$C(31),2,99)
	I $E(X,1,4)'="PASS" S X=$TR(X,LC_$C(13,10,1,0),UP)
	S @TKWEM@("OUT",CON,CNT)=X,CNT=CNT+1
	G:C<0 ERRD S CMD=$P(X," ") I LOGON<2 G USER:CMD="USER",QUIT:CMD="QUIT",PASS:CMD="PASS" W "-ERR must logon",CR G CMD ; APOP not yet implemented
	G STAT:CMD="STAT",LIST:CMD="LIST",RETR:CMD="RETR",DELE:CMD="DELE",NOOP:CMD="NOOP",RSET:CMD="RSET",UPDATE:CMD="QUIT",TOP:CMD="TOP",UIDL:CMD="UIDL" W "-ERR command not recognized",CR G CMD
	;
USER	S I=$P(X," ",2) I I]"" D  I $D(@TKWEM@("USER",I)) S USER=^(I),LOGON=1 W "+OK ",I," is a valid mailbox",$S(I'=USER:" (alias for "_USER_")",1:""),CR G CMD
	.I I["@" S I=$P(I,"@",1)
	W "-ERR never heard of mailbox -"_I,CR Q  ;S LOGON=0 G CMD
PASS	I LOGON=0 W "-ERR must enter USER name",CR G CMD    ;W $$CHECKPASS^%ZAPM.bs.BSpgBS("Styx","gf3hjv7")
	S PASS=$TR($P(X," ",2,99),$C(13,10)),LOGON=1 G:PASS="" PSE G:'$$CHECKPASS^%ZAPM.bs.BSpgBS(USER,PASS) PSE S LOGON=2
	S USER=$TR(USER,LC,UP)
	L +@TKWEM@(USER):2 E  W "-ERR mailbox already in use",CR S LOGON=0 G CMD
	D COUNT W "+OK ",$P(COUNT," ")," messages for ",USER," (",$P(COUNT," ",2)," Octets)",CR G CMD
PSE	W "-ERR invalid password ",CR G CMD
QUIT	W "+OK tk-web POP3 server signing off",CR G END
	;
STAT	W "+OK ",COUNT,CR G CMD
LIST	I 'COUNT W "-ERR no messages",CR G CMD
	S A=$P(X," ",2) G:A LIST1
	W "+OK ",$P(COUNT," ")," messages (",$P(COUNT," ",2)," octets)",CR
	S A="" F C=1:0 S A=$O(@CGLB@(A)) Q:A=""  S B=@CGLB@(A) I $G(@TKWEM@("MB",USER,B)) W C," ",+^(B),CR S C=C+1
	W ".",CR G CMD
LIST1	D GETM G:B="" CMD W "+OK ",C," ",+^(B),CR G CMD
	;
RETR	D GETM G CMD:B="" W "+OK ",+^(B)," octets",CR
	S A="",ACT=1 F  S A=$O(@TKWEM@("MB",USER,B,A)) Q:A=""  W:^(A)="." "." W ^(A),CR
	W ".",CR G CMD
DELE	D GETM G:B="" CMD K @CGLB@(A) S $P(COUNT," ",2)=$P(COUNT," ",2)-$G(@TKWEM@("MB",USER,B)),$P(COUNT," ")=$P(COUNT," ")-1,^(B)="`"_^(B),ACT=1 W "+OK messaged deleted",CR G CMD
NOOP	W "+OK",CR G CMD
RSET	S A="" F  S A=$O(@TKWEM@("MB",USER,A)) Q:A=""  I $D(^(A))#2,'^(A) S ^(A)=$E(^(A),2,255)
	D COUNT W "+OK ",COUNT,CR G CMD
UPDATE	S A="" F  S A=$O(@TKWEM@("MB",USER,A)) Q:A=""  I $D(^(A))#2,'^(A) K ^(A)
	W "+OK TK-WEB POP3 server signing off, ",+COUNT," messages (",$P(COUNT," ",2)," octets)",CR
END	W ! I 'TRACE,'ACT K @TKWEM@("OUT",CON)
	Q
TOP	D GETM G:B="" CMD W "+OK",CR
	S A="" F  S A=$O(@TKWEM@("MB",USER,B,0,A)) Q:A=""  W:^(A)="." "." W ^(A),CR
	S A="" F I=1:1:$P(CMD," ",3) S A=$O(@TKWEM@("MB",USER,B,A)) Q:A=""  W:^(A)="." "." W ^(A),CR
	W ".",CR G CMD
UIDL	I $P(X," ",2)="" W $S(COUNT:"+OK",1:"-ERR"),CR G:'COUNT CMD S A="",C=0 F  S A=$O(@TKWEM@("MB",USER,A)) W:A="" ".",CR G:A="" CMD I $G(^(A)) S C=C+1 W C," ",A,CR
	D GETM I B]"" W "+OK ",A," ",B,CR
	G CMD
	;
GETM	S A=$P(X," ",2),B="" S B=$G(@CGLB@(A))
	I B="" W "-ERR нет сообщений, только ",+COUNT," сообщений",CR Q
	I $D(@TKWEM@("MB",USER,B))#2,'^(B) W $S(CMD="DELE":"-ERR сообщение "_A_" уже удалено",1:"-ERR сообщение удалено"),CR S B="" Q
	Q
COUNT K COUNT,^%ZAPM.bs.BSpgWEMCT($J) S (A,B,C)=0,CGLB="COUNT" F  S A=$O(@TKWEM@("MB",USER,A)) Q:A=""  I $G(^(A)) S B=B+^(A),C=C+1,@CGLB@(C)=A I C=100 S CGLB="^%ZAPM.bs.BSpgWEMCT($J)" M ^%ZAPM.bs.BSpgWEMCT($J)=COUNT
	S COUNT=C_" "_B Q
	;
INIT	S LC="abcdefghijklmnopqrstuvwxyz",UP="ABCDEFGHIJKLMNOPQRSTUVWXYZ" Q
	;
ERROR	S:$ZE'["DSCON" @TKWEM@("ERR",$H)=$ZE_","_$G(CON)_","_$G(CNT)_","_$G(ER) Q
	;
ERRC	S ER="CMND" G ERR
ERRT	S ER="TIMEOUT" G ERR
ERRD	S ER="DISCON"_","_$ZA_","_$ZB_","_$ZC G ERR
ERRL	S ER="LENGTH" G ERR
ERRE	S ER="EMPTY" H 1 G ERR
ERR	S @TKWEM@("OUT",CON,CNT)=ER_","_$G(X),CNT=CNT+1,ACT=1
	U TCP G CMD:ER'["DSCON" Q
	;
TDATE(X)	D DATE,TIME S DTM=$P(DT,"/",1,2)_" "_TM
	S DTMA=$P("Thursday,Friday,Saturday,Sunday,Monday,Tuesday,Wednesday",",",X#7+1)_"  "_$P("January,February,March,April,May,June,July,August,September,October,November,December",",",MT)_" "_+$P(DT,"/",2)_", "_$P(DT,"/",3)_"  "_TMA Q
TIME	S TM=$P(X,",",2),TM=$E(TM\3600+100,2,3)_":"_$E(TM#3600\60+100,2,3)_":"_$E(TM#60+100,2,3),TMA=$S(TM>12:TM-12,1:+TM)_":"_$P(TM,":",2)_$S(TM>11:" PM",1:" AM") Q
	;         
DATE	S K=X>21608+305+X,YR=4*K+3\1461,D=K*4+3-(1461*YR)+4\4,MT=5*D-3\153,D=5*D-3-(153*MT)+5\5,MT=MT+2,YR=MT\12+YR+1840,MT=MT#12+1
	S DT=YR*100+MT*100+D,DT=$E(DT,5,6)_"/"_$E(DT,7,8)_"/"_$E(DT,1,4)
	K K,D Q
	;
