ROUTINE %ZAPM.bs.BSpgWEBH
TKWEBH	;PG&A,TK-WEB,1.1,HTML CONTROL;08OCT97  18:08 [ 10/15/97   8:12 AM ];11FEB2004  09:14;;07JAN2000  22:33
	;;Copyright (C) 1997 Patterson, Gray and Associates, Inc.
	;
M3 S TRACE=1,LOG=1 I '$D(^%ZAPM.bs.BSpgWEBC("Control")) S ^("Control")="1,,1,,5,,80"
 L +CONNECT S (CON,^%ZAPM.bs.BSpgWEBC("CONNECT"))=$G(^%ZAPM.bs.BSpgWEBC("CONNECT"))+1 L -CONNECT
	S ADDR=$P($ZDEV($P),"~"),OS="M3",TCP=0 G cont
	;
accept(%ddb,CON,TRACE,LOG)	
	I %ddb=0 S OS="M11",TCP=0,ADDR="" U TCP:(::"AS":$C(10)) D  G cont
	.Set ADDR=$A($ZU(111,0),1)_"."_$A($ZU(111,0),2)_"."_$A($ZU(111,0),3)_"."_$A($ZU(111,0),4) ;_":"_($A($ZU(111,0),5)*256+$A($ZU(111,0),6))
	S TCP=56,OS="MSM"
	V %ddb+46::$J-$V(272,-4,4):2 S addr=$V(%ddb+16,-3,0),ADDR=$V(addr+13,-3,$V(addr+12,-3,1),9) U 56:$ZH(+%ddb) G cont
	;
uaccept(CON,TRACE,LOG,ADDR,TCP,OS)	
cont S:'$D(TEST) $ZT="ERROR^%ZAPM.bs.BSpgWEBH2"
	S CNT=1,CR=$C(13,10),TMO=$G(^%ZAPM.bs.BSpgWEBC("TimeOut"),56) D INIT,TDATE($H) ; S RLC=$E(UP,CON#26+1)_$E(UP,CON\26#26+1)_"_"_$E(UP,CON\676#26+1)_$E(UP,CON\17576#26+1)
    S SMT=MT,TZ=$G(^%ZAPM.bs.BSpgWEBC("TimeZone")),STL=$S($D(^%ZAPM.bs.BSpgWEBC("MAXSTRING")):^("MAXSTRING"),1:255) I '$D(THANDLE) G PROCESS
    I OS="M3" L +CONNECT S (CON,^%ZAPM.bs.BSpgWEBC("CONNECT"))=$G(^%ZAPM.bs.BSpgWEBC("CONNECT"))+1 L -CONNECT
	S (Y,LM,MM,L,EXP)="",(PC,FL)=0,TM=$H,%("ADDRESS")=ADDR G P1
	;
PROCESS	;
	K LL,%,ACS,TYPE S (Y,LM,MM,L,EXP,X)="",(PC,FL)=0,TM=$H,%("ADDRESS")=ADDR
    I ADDR'="",$D(^%ZAPM.bs.BSpgNOWEB(ADDR)) H
	U TCP R X:TMO I OS="DSM4",ADDR'="" S X=ADDR_X,ADDR=""
	X $S(OS="M3":"S C=$ZC",OS="MSM":"S C=$ZC X:C=1 ""R X:TMO S C=$ZC""",OS="M11":"S C=$ZA#8>3,X=X_$ZB",1:"S C=0")
P1	G:X="" ERRD:C,ERRT:'$T,ERRE:X="" F  Q:$TR(X,$C(13))[$C(10,10)  Q:C  S T=$P(TM,",",2)-$P($H,",",2)+TMO Q:T<0  R Y:1 S X=X_Y X $S("MSM,M3"[OS:"S C=$ZC",OS="M11":"S C=$ZA#8>3,X=X_$ZB",1:"S C=0")
	G:C ERRD
	I X["/scripts/mgwms32.dll" S X=$$TR^%ZAPM.bs.BSsFUNM(X,"/scripts/mgwms32.dll","/?BSTKWEB%20") ;ЗАМЕНА WEBLINK
	S W=X,X=$TR(X,$C(13)) G:X'[$C(10,10) ERRL
	I X["admin.dll"!(X["system32") S ^VIRUS=$G(^VIRUS)+1 H
	D PARSE I $D(%("CONTENT-LENGTH:")) D GETBODY
	I $TR($G(%("CONNECTION:")),UP,LC)["keep-alive" S PC=1
	S COOKID=$P($G(%("COOKIE:")),"=",2)
 G GET:CMD="GET",GET:CMD="HEAD",POST:CMD="POST",PUT^%ZAPM.bs.BSpgWEBH1:CMD="PUT",OPTIONS:CMD="OPTIONS",PROPFIND:CMD="PROPFIND" G ERRC
	;
GETBODY	I W[$C(13,10,13,10) S Y=$P(W,$C(13,10,13,10),2,999),X=$P(W,$C(13,10,13,10))
	E  S Y=$P(W,$C(10,10),2,999),X=$P(W,$C(10,10))
    ;G GETB1^%ZAPM.bs.BSpgWEBPUT //MSW
    G:%("CONTENT-LENGTH:")>10000 GETB1^%ZAPM.bs.BSpgWEBPUT
	F  Q:$L(Y)'<%("CONTENT-LENGTH:")  S T=$P(TM,",",2)-$P($H,",",2)+TMO Q:T<0  R y:1 S Y=Y_y X $S("MSM,M3"[OS:"S C=$ZC",OS="M11":"S C=$ZA#8>3,Y=Y_$ZB",1:"S C=0") Q:C
	S %("BODY")=$E(Y,1,STL) Q
	;
GET S ST=200 D FILE I "M3CONTROL,SCONTROL,CONFIG"[DIR G CONTROL^%ZAPM.bs.BSpgWEBH1:DIR'=""
 I FLN'[".H",URI_FLN["?" G PARSEY^%ZAPM.bs.BSpgWEBH1
 I FLN?1A1":".E G FILE^%ZAPM.bs.BSpgWEBH1
 I FLN?1.AN1"^"1A.E G GLOB^%ZAPM.bs.BSpgWEBL
 I FLN[".H",$P(FLN,".",2)["C",'$D(%("COOKIE:")) S COOKIE="CID="_CON_"; expires="_$$GMT($H+365,TZ)_"; path=/",^%ZAPM.bs.BSpgWEBL("COOKIE",CON)=DIR_","_FLN_","_ADDR
 I FLN[".HTP",'$D(%("AUTHORIZATION:")) S ST=401 G GET1
 I URI'["?" D HTML^%ZAPM.bs.BSpgBS(URI,FLN,DIR) G GET1 ////////////////////////////////////////////АНАЛОГ ISS
GET4 I $D(@GLB)=0 D 404^%ZAPM.bs.BSpgWEBH2 G GET1
 S XX=@GLB,FL=$P(XX,"`",3) I FL["M",URI'["?NOD" D MODIFY^%ZAPM.bs.BSpgWEBL G GET2
 I $G(%("CACHE-CONTROL:"))["max-stale=0" G GET2
 S A=$G(%("IF-MODIFIED-SINCE:")) I A]"" D CMP I  S ST=304,FL=0 D WRITE G AGAIN
GET2	S LM=$P(XX,"`",4)
GET1	D WRITE
AGAIN	W ! I PC=2 S CNT=CNT+1,TMO=15 G PROCESS
	Q
	;
POST D FILE G PARSEY^%ZAPM.bs.BSpgWEBH1
	;
WRITE D:CMD="HEAD" HEAD^%ZAPM.bs.BSpgWEBH2
	S L=$L($G(LL))+FL I $D(LL)>1 S A="" F  S A=$O(LL(A)) Q:A=""  S L=L+$L(LL(A))
	D HTML W:$L($G(LL)) LL I $D(LL)>1 S A="" F  S A=$O(LL(A)) Q:A=""  W LL(A)
	I FL,$D(SKP) S A=$O(@GLB@(0)),A=0 F  S A=$O(^(A)) Q:A=""  W $S($D(SKP(A)):SKP(A),1:^(A)) I $D(SKP(A,0)) X SKP(A,0) I $D(@GLB@(0))
    
	I FL,'$D(SKP),GLB["^" D  S A=$O(@GLB@(0)),A=0  F  S A=$O(^(A)) Q:A=""  W $G(^(A)) 
	.M @GLB@(-1)=MM
	
	I FL,'$D(SKP),GLB'["^" S A=$O(@GLB@(0)),A=0 F  S A=$O(@GLB@(A)) Q:A=""  W $G(@GLB@(A))
	I $D(DEVD) X DEVR F   U DEV R A#253 S @ZC U TCP W A,$S($L(A)'=253:$C(13,10),1:"") Q:Z
	W ! I $D(DEVD) X DEVC K DEVD
	K SKP,LL 
	;I FL K:$G(GLB)["TKTWEB" @GLB
	D TRACE Q
	;
PROPFIND	D FILE S ST=207,TYPE="text/xml" S:'$D(%("AUTHORIZATION:")) ST=401 G GET1
OPTIONS	S ST=200 D FILE S OTHERMM="OPTIONS, TRACE, GET, HEAD, POST, PUT, DELETE, PROPFIND, COPY, MOVE, MKCOL, PROPPATCH",OTHERMM="Allow: "_OTHERMM_CR_"Public: "_OTHERMM_CR_"DAV: 1"_CR G GET1
	;
HTML	U TCP S ST=$$STATUS(+ST),MM="HTTP/1.1 "_ST_CR S xFLN=FLN,FLN=$ZCONVERT(FLN,"U")
	I +ST=401 S MM=MM_"WWW-Authenticate: Basic Realm="""_$S($G(AUT)]"":AUT,1:"tkweb")_"""" I URI?1"/".E1"/" S ST=404 G HTML
	S TYPE=$S($G(TYPE)]"":TYPE,$D(LL):"text/html",FLN[".HQX":"application/mac-binhex40",FLN[".H":"text/html",FLN[".J":"image/jpeg",FLN[".G":"image/gif",FLN[".B":"image/bmp",FLN["GIF":"image/gif",1:"")
	S:TYPE="" TYPE=$S(FLN[".BIN":"application/x-macbinary",FLN[".MP3":"audio/x-mpeg",FLN[".HLP"!(FLN[".CHM"):"application/mshelp",FLN[".PDF":"application/pdf",FLN[".RA":"audio/x-pn-realaudio",FLN[".D":"model/vnd.dwf",FLN[".XSL":"text/xml",1:"")
	I TYPE="" S TYPE=$S(FLN[".ICO":"image/x-icon",FLN[".XL":"application/vnd.ms-excel",1:"")
	S:TYPE="" TYPE=$S(FLN[".RM":"application/vnd.rn-realmedia",FLN[".M3":"application/m3",FLN[".ZIP":"application/zip",FLN[".HTA":"application/hta",1:"text/html")
	S MM=MM_"Content-Type: "_TYPE_" charset=windows-1251"_CR S FLN=xFLN
	S MM=MM_"Server: "_$$SN()_CR_"Date: "_$$GMT($H,TZ)_CR_"Content-Length: "_L_CR
	S MM=MM_$G(OTHERMM) I $D(COOKIE) S MM=MM_"Set-Cookie: "_COOKIE_CR
	S:LM]"" MM=MM_"Last-Modified: "_LM_CR S:$G(EXP)]"" MM=MM_"Expires: "_EXP_CR S:PC=1 MM=MM_"Connection: Keep-Alive"_CR,PC=2
	W MM,CR
	Q
SN() Q "PG&A,TKWEB/1.2 (%BS-Portal)"
	;
TRACE G:'TRACE T1 S ^mtempBSpgWEBT(-CON,CNT)=$E(X,1,STL) M ^mtempBSpgWEBT(-CON,CNT)=% I CON_" "["000 " J CLN^%ZAPM.bs.BSpgWEBH2
 F AA="ST","LL","PC","GLB","L","FL","FLN","DIR","URI","COOKIE","LM","EXP","TYPE" I $G(@AA)'="" S ^mtempBSpgWEBT(-CON,CNT,AA)=@AA
T1	Q:'LOG  S D="" F AA="CON","CNT","ADDRESS","CMD","FLN","ST","L","CONNECTION:","USER-AGENT:","REFERER:","DTM","RLC","DIR","FROM:" S D=D_"`"_$S($D(%(AA)):$E(%(AA),1,80),AA'[":":$G(@AA),1:"")
 I "JG"'[$E($P($G(FLN),".",2)) S ^%ZAPM.bs.BSpgWEBL($S(ADDR="":"LOG",$D(^%ZAPM.bs.BSpgWEBC("LOCAL",ADDR)):"LOGL",1:"LOG"),-(CON*100+CNT))=$E(D,2,510)_"`"_($P($H,",",2)-$G(LTS))
 I $G(%("FROM:"))]"" S ^%ZAPM.bs.BSpgWEBL("FROM:",%("FROM:"))=$G(DIR)
	Q:'$D(ST)  Q:ST>399
 I $G(ADDR)]"" S $P(^%ZAPM.bs.BSpgWEBL("STATS",YR,"ADDR",ADDR),"+",SMT)=$P($G(^%ZAPM.bs.BSpgWEBL("STATS",YR,"ADDR",ADDR)),"+",SMT)+1 I '$D(^%ZAPM.bs.BSpgWEBL("ADDR",ADDR)) S ^(ADDR)="" J RLOG^%ZAPM.bs.BSpgWEBN(ADDR)
 S AA=$G(%("USER-AGENT:")) I AA]"" S $P(^%ZAPM.bs.BSpgWEBL("STATS",YR,"BROWSER",AA),"+",SMT)=$P($G(^%ZAPM.bs.BSpgWEBL("STATS",YR,"BROWSER",AA)),"+",SMT)+1
 Q:$G(ACS)=""  S $P(^%ZAPM.bs.BSpgWEBL("STATS",YR,"FILE",DIR_"/"_FLN),"+",SMT)=$P($G(^%ZAPM.bs.BSpgWEBL("STATS",YR,"FILE",DIR_"/"_FLN)),"+",SMT)+1
 S $P(^%ZAPM.bs.BSpgWEBL("STATS",YR,"TOTAL",DIR_" #FILES"),"+",SMT)=$P($G(^%ZAPM.bs.BSpgWEBL("STATS",YR,"TOTAL",DIR_" #FILES")),"+",SMT)+1
 S $P(^%ZAPM.bs.BSpgWEBL("STATS",YR,"TOTAL",DIR_" #BYTES"),"+",SMT)=$P($G(^%ZAPM.bs.BSpgWEBL("STATS",YR,"TOTAL",DIR_" #BYTES")),"+",SMT)+$L(MM)+L+1
	Q
	;
PARSE ;;;S ^mtempZ(0)=X
	S X=$$DECODING^%ZAPM.bs.BSpgBS(X)
	S %("COMMAND")=$P(X,$C(10))
	S CMD=%("COMMAND") 
	;S CMD=$TR(%("COMMAND"),LC,UP) //````
	;;;S ^mtempZ(1)=CMD
	 F J=2:1:$L(X,$C(10)) S B=$P(X,$C(10),J) Q:B=""  S C=$P(B," ") S:C]"" %($TR(C,LC,UP))=$P(B," ",2,999)
	I $L(Y) S %("BODY")=$E(Y,1,STL)
	D
	.F J=2:1:$L(CMD," ") I $P(CMD," ",J)'="" S URI=$P(CMD," ",J) Q
	.F J=J+1:1:$L(CMD," ") I $P(CMD," ",J)'="" S HTTP=$P(CMD," ",J) Q
	;D  //MSW
	;.S URI=$P(CMD," ",2,$L(CMD," ")-1)
	;.S HTTP=$P(CMD," ",$L(CMD," "))
	;;;S ^mtempZ(2)=URI S ^mtempZ(3)=HTTP
	S CMD=$P(CMD," ") Q
	; 
FILE	I URI="/" D DEF
	I $E(URI)="/" S DIR=$P(URI,"/",2),FLN=$P(URI,"/",3,9)
	E  S DIR=$P(URI,"/",4),FLN=$P(URI,"/",5,9)
    I FLN="" S DIR=$S(DIR="":"",$D(^%ZAPM.bs.BSpgWEBF("PAGES",DIR)):DIR,$D(^%ZAPM.bs.BSpgWEBF("ALIAS",DIR)):^(DIR),1:DIR),FLN=$S(DIR="":$S(OS="M3":"?M3",1:"?")_"INTRO",$P($G(^%ZAPM.bs.BSpgWEBF("PAGES",DIR)),"\",5)]"":$P(^(DIR),"\",5),1:DIR)
	I DIR=FLN,DIR'["?" Q:DIR="M3CONTROL"  D DEF S DIR=$P(URI,"/",2)
    G GBUILD^%ZAPM.bs.BSpgWEBGIF:FLN["GIF(" S:DIR["^" FLN=DIR I FLN?.AN1"^"1.A.E S GLB=FLN Q
	I FLN["?" S:FLN[".HTM?" RLC=$P(FLN,"?",2,9),FLN=$P(FLN,"?")
	I FLN["/" S RLC=$P(FLN,"/",1,$L(FLN,"/")-1),FLN=$P(FLN,"/",$L(FLN,"/"))
 I $P($G(^%ZAPM.bs.BSpgWEBF("PAGES",DIR)),"\",6)="ON" G DEAC^%ZAPM.bs.BSpgWEBH2
 S GLB="^%ZAPM.bs.BSpgWEBF(""PAGES"","""_DIR_"""",A=FLN,ACS=$G(^%ZAPM.bs.BSpgWEBF(98,DIR))
 S:A'["." A=A_".HTM" S A=A_$S($D(^%ZAPM.bs.BSpgWEBF("PAGES",DIR,A)):"",$D(^%ZAPM.bs.BSpgWEBF("PAGES",DIR,A_"L")):"L",1:"") I $P(A,".",2)="HTM",$G(%("USER-AGENT:"))["MS",$D(^%ZAPM.bs.BSpgWEBF("PAGES",DIR,A_"I")) S A=A_"I"
	S GLB=GLB_","""_A_""")" Q
	;
DEF S URI=$TR($G(%("HOST:")),LC,UP),URI=$S(URI="":$G(^%ZAPM.bs.BSpgWEBF("DEFAULT","HOME")),$D(^%ZAPM.bs.BSpgWEBF("HOME",URI)):^(URI),1:$G(^%ZAPM.bs.BSpgWEBF("DEFAULT","HOME")))
	Q
CMP	I $P(XX,"`",3)]"",$P(A,";")=$P(XX,"`",3) Q
	I $P(XX,"`",5) S A=$$DTEX($P(A,";")) I $P(XX,"`",5)']A
	Q
DTEX(A)	S A=$TR(A,LC,UP)
	I A["-" S A=$P(A,",",2) Q $P(A,"-",3)+1900*100+$$MON($P(A,"-",2))*100+$P(A," ",2)_" "_$P(A," ",3)
	I A["," S A=$P(A,",",2) Q $P(A," ",4)*100+$$MON($P(A," ",3))*100+$P(A," ",2)_" "_$P(A," ",5)
	S:$E(A)=" " A=$E(A,2,99) Q $P(A," ",6)*100+$$MON($P(A," ",2))*100+$P(A," ",4)_" "_$P(A," ",5)
	;
MON(X)	Q $F("JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC",X)\3
	;
GMT(X,T)	;($H,timezone)
	N K,Y,D,M S T=$P(X,",",2)+(-T*3600) S:T<0 X=X-1,T=T+86400  S:T>86400 X=X+1,T=T-86400
	S K=X>21608+305+X,Y=4*K+3\1461,D=K*4+3-(1461*Y)+4\4,M=5*D-3\153,D=5*D-3-(153*M)+5\5,M=M+2,Y=M\12+Y-60,M=M#12+1,DT=1900+Y*100+M*100+D
	S D=$P("Mon,Tue,Wed,Thu,Fri,Sat,Sun",",",X+3#7+1)_", "_D_" "_$P("Jan,Feb,Mar,Apr,May,Jun,Jul,Aug,Sep,Oct,Nov,Dec",",",M)_" "_(1900+Y)
	S M=T\60,M=$E(M\60+100,2,3)_":"_$E(M#60+100,2,3)_":"_$E(T#60+100,2,3) Q D_" "_M_" GMT"
	;
	;
INIT S LC="abcdefghijklmnopqrstuvwxyz" ;йцукенгшщзхъфывапролджэячсмитьбю"
     S UP="ABCDEFGHIJKLMNOPQRSTUVWXYZ" ;ЙЦУКЕНГШЩЗХЪФЫВАПРОЛДЖЭЯЧСМИТЬБЮ" 
     Q
	;
STATUS(ST)	Q ST_$P($T(@ST),";",2)
200	; OK
201	; Created
202	; Accepted
204	; No Content
207	; Multi-Status
301	; Moved Permanently
302	; Moved Temporarily
304	; Not Modified
400	; Bad Request
401	; Unauthorized
403	; Forbidden
404	; Not Found
500	; Internal Server Error
501	; Not Implemented
502	; Bad Gateway
503	; Service Unavailable
	;
ERRC	S ER="CMND" G ERR
ERRT	Q:CNT>1  S ER="TIMEOUT" G ERR
ERRD	K:$G(GLB)["TKTWEB" @GLB Q
ERRL	S ER="LENGTH" G ERR
ERRE	S ER="EMPTY" G ERR
ERR	D PARSE S %("ST")="ERROR-"_ER D TRACE Q
	;
TDATE(X)	D DATE,TIME S DTM=$P(DT,"/",1,2)_" "_LTM
	S DTMA=$P("Thursday,Friday,Saturday,Sunday,Monday,Tuesday,Wednesday",",",X#7+1)_"  "_$P("January,February,March,April,May,June,July,August,September,October,November,December",",",MT)
	S DTMA=DTMA_" "_+$P(DT,"/",2)_$S($P(DT,"/",3)<30:", 20",1:", 19")_$P(DT,"/",3)_"  "_TMA Q
TIME	S LTS=$P(X,",",2),LTM=$E(LTS\3600+100,2,3)_":"_$E(LTS#3600\60+100,2,3)_":"_$E(LTS#60+100,2,3),TMA=$S(LTM>12:LTM-12,1:+LTM)_":"_$P(LTM,":",2)_$S(LTM>11:" PM",1:" AM") Q
	;         
DATE	S K=X>21608+305+X,YR=4*K+3\1461,D=K*4+3-(1461*YR)+4\4,MT=5*D-3\153,D=5*D-3-(153*MT)+5\5,MT=MT+2,YR=MT\12+YR+1840,MT=MT#12+1
	S DT=YR+100*100+MT*100+D,DT=$E(DT,5,6)_"/"_$E(DT,7,8)_"/"_$E(DT,3,4) K K,D Q
	;
TD(X)	D TDATE(X) Q DTMA
	;
last D INIT,TDATE($H) S X="^mtempBSpgWEBT",X=$Q(@X) Q:X=""  S X=@X
 S (Y,LM,MM,L,EXP,ADDR,CR)="",(LOG,TRACE,TCP,PC,FL,TZ)=0,TM=$H,%("ADDRESS")=ADDR,OS=$G(^%ZAPM.bs.BSpg("OS"))
	G P1+3
