ROUTINE %ZAPM.bs.BSpgWEBGI1
TKWEBGIF ;PG&A,TK-WEB,1.1, GIF FUNCTIONS;15OCT97  08:03;;;22AUG97  09:09 [ 10/15/97   8:12 AM ]
 ;;Copyright (C) 1997 Patterson, Gray and Associates, Inc. ;#7280
 ;
 Q
BUILD S DIR="PGA",DOC="TEST.GIF",GG="^%ZAPM.bs.BSpgWEBF(""PAGES"",DIR,DOC)" K @GG
 ; S NROW=24,WIDTH=80*8,HEIGHT=NROW*10+1,TIME=0,IMAGES=1
 ; IMAGE FORMAT - FORE;BACK;TIME;TYPE;
 S NROW=1,WIDTH=20*8 F IMAGES=1:1:5 S IMAGE(IMAGES)="00FF00;FFFFFF;40;C",IMAGE(IMAGES,1)=$P("HELLO,GOOD AFTERNOON,HOW ARE,YOU,DOING?",",",IMAGES)
 S TM=$H,HEIGHT=NROW*10+1 D GIF
 W $P($H,",",2)-$P(TM,",",2)," SECONDS"
 Q
 O 51:("C:\MSM43\TEST.GIF":"W") U 51 S A="" F  S A=$O(@GG@(A)) Q:A=""  W ^(A)
 C 51
 Q
GBUILD N (FLN,GLB,GIF) S (GG,GLB)="GIF",T=^%ZAPM.bs.BSpgWEBGIF(+$P(FLN,"(",2),+$P(FLN,",",2)),TIME=0,LNG=0
 F IMAGES=1:1 S A=$P(T,";",IMAGES-1*5+1,IMAGES-1*5+5) Q:A=""  S IMAGE(IMAGES)=$S($P(A,";")="":"000000",1:$P(A,";"))_";"_$S($P(A,";",2)="":"FFFFFF",1:$P(A,";",2))_";"_$P(A,";",3,4),IMAGE(IMAGES,1)=$P(A,";",5) S:$L($P(A,";",5))>LNG LNG=$L($P(A,";",5))
 S IMAGES=IMAGES-1,NROW=1,WIDTH=LNG*8,HEIGHT=11 D GIF
 S SZ=0 S A="" F  S A=$O(GIF(A)) Q:A=""  S SZ=SZ+$L(GIF(A))
 S GIF="TEMP`G`"_SZ_"`"
 Q
 ;
GIF K STR
 S STR="GIF89a",CC=0,TIME=$O(IMAGE(1)) I 'TIME S TIME=+$P(IMAGE(1),";",3)
 ; LOG SCR DESCR
 S STR=STR_$C(WIDTH#256,WIDTH\256,HEIGHT#256,HEIGHT\256,128+32+2,0,0)
 ; GLOBAL COLOR TABLE
 K OTB,STB S OTB=0 F J=1:1:IMAGES F K=1,2 S F=$$COLR($P(IMAGE(J),";",K)) S:'$D(STB(F)) OTB(OTB)=F,STB(F)=$C(OTB),OTB=OTB+1
 F J=0:1:7 S:'$D(OTB(J)) OTB(J)=$C(0,0,0) S STR=STR_OTB(J)
 ; S STR=STR_$C(255,255,255)_$C(0,0,0)_$C(255,0,0)_$C(0,255,0)_$C(0,0,255)_$C(100,100,100)_$C(200,200,200)_$C(50,100,150)
 I TIME S STR=STR_$C(33,255,11,78,69,84,83,67,65,80,69,50,46,48,3,1,231,3,0)
 F IMAGE=1:1:IMAGES D IMAGE
 I TIME S STR=$C(33,255,11,71,73,70,67,79,78,110,98,49,46,48,2,11,0,0)
 S STR=STR_$C(59),CC=CC+1,@GG@(CC)=STR
 Q:GG'["^%ZAPM.bs.BSpgWEBF"
 S D=$$GMT^%ZAPM.bs.BSpgWEBH($H,+$G(^%ZAPM.bs.BSpgWEBC("TimeZone"))),SZ=0 S A="" F  S A=$O(^%ZAPM.bs.BSpgWEBF("PAGES",DIR,DOC,A)) Q:A=""  S SZ=SZ+$L(^(A))
 S ^%ZAPM.bs.BSpgWEBF("PAGES",DIR,DOC)="TEMP`G`"_SZ_"`"
 Q
COLR(X) N A,K S A="" F K=1,3,5 S A=A_$C($ZH($E(X,K,K+1)))
 Q A
IMAGE ; IMAGE DESCRIPTOR
 S A=IMAGE(IMAGE),TIME=$P(A,";",3),COL=STB($$COLR($P(IMAGE(IMAGE),";",1)))_STB($$COLR($P(IMAGE(IMAGE),";",2)))
 S TRY=1234567890,TIMEX=TIME
 G IMAGE0:'TIME,IMAGE0:$P(A,";",4)'["R" F TRY=TRY,"23456789","3456789","456789","56789","6789","789","89","9" D IMAGE0 S TIMEX=20
 Q
IMAGE0 ; GRAPHIC CONTROL EXTENSION
 S STR=STR_$C(33,249,4,0,TIMEX#256,TIMEX\256,0,0)
 S CC=CC+1,@GG@(CC)=STR,STR=""
 S STR=STR_$C(44,0,0,0,0,WIDTH#256,WIDTH\256,HEIGHT#256,HEIGHT\256,0)
 S CC=CC+1,@GG@(CC)=STR_$C(7),STR="" ;7=CODE SIZE
 D RESET I '$D(AL) S AL="" F I=32:2:254 S AL=AL_$C(I,I+1)
 S NTX="",NT=$TR($J("",WIDTH\8),AL,^%ZAPM.bs.BSpgFAXF(10,.7,1)) D CONV
 F ROW=1:1:NROW S TXT=IMAGE(IMAGE,ROW),TXT=$E(TXT_$J("",WIDTH\8),1,WIDTH\8) F TRX=1:1:10 S TR=$E(TRY,TRX) S:'TR TR=1,TXT=$J("",WIDTH\8) S NT=$TR(TXT,AL,^%ZAPM.bs.BSpgFAXF(10,.7,TR)) D CONV
 D CONV3 Q
RESET K TB F JJ=0:1:7 S TB($C(JJ))=$C(JJ)
 S NX=130,STR=STR_$C(128) D:$L(STR)>254 CONVS Q
CONV S NTY="",C=$D(^%ZAPM.bs.BSpgFAXC(0,4,0)) F C=1:1:WIDTH\8 S NTY=NTY_^($A(NT,C))
 W:0 "ROW-",ROW," ",TR,!
 S NTX=NTX_$TR(NTY,10,COL)
 S J=1 F  Q:J'<$L(NTX)  F J=2:1:$L(NTX) I '$D(TB($E(NTX,1,J))) S STR=STR_TB($E(NTX,1,J-1)),TB($E(NTX,1,J))=$C(NX),NX=NX+1,NTX=$E(NTX,J,999) D:$L(STR)>254 CONVS D:NX=256 RESET Q
 Q
CONV3 I $L(NTX) S NTX=NTX_" " F J=2:1:$L(NTX) S A=$E(NTX,1,J) I '$D(TB(A)) D CONV2 Q
 S STR=STR_$C(129),CC=CC+1,@GG@(CC)=$C($L(STR))_STR_$C(0),STR="" Q
CONV2 S STR=STR_TB($E(NTX,1,J-1)),NTX=$E(NTX,J,999),TB(A)=$C(NX),NX=NX+1 D:$L(STR)>254 CONVS
 I NX=256 D RESET
 Q
CONVS S CC=CC+1,@GG@(CC)=$C(254)_$E(STR,1,254),STR=$E(STR,255,999) Q
 ;
 ;
STORE O 51:("C:\WEBTEST\TEST.GIF":"W"::::"") S A="" F  S A=$O(^%ZAPM.bs.BSpgGIF("TEST",A)) Q:A=""  U 51 W ^(A) U 0 W "."
 C 51 U 0
 Q
