ROUTINE %ZAPM.bs.BSzvUB
%BSzvUB ;Уточнение "стандартной" боковины  (А.Тимофеев) ; 10:37   27.09.2000
 ;Загрузка "стандартной" боковины
 N i
 S A=$$LOAD^%ZAPM.bs.BSscrF("^%ZAPM.bs.BSZ(SVb3)",1,1,"","1^%ZAPM.bs.BSzvUB") Q:A=""
 Q
1 ;Копирование данных из "стандартной" боковины
 S i=0,i1=2 F  S i=$O(@mas2@(bok,i)) Q:i=""  D  S i1=i1+1
 .I i1=2 S $P(@mas4@(2,1),"@",15)=i Q
 .S @mas4@(i1,1)=@mas4@(2,1)
 .S $P(@mas4@(i1,1),"@")=i1+1
 .S $P(@mas4@(i1,1),"@",15)=i
 .S @mas4@("FTR",i1,1)=@mas4@("FTR",2,1)
 .S @mas4@("FTR",i1,1,1)=@mas4@("FTR",2,1,1) Q
 S $P(@mas4@(1,1),"@",15)=bok
 S $P(@mas4,"@",28)=i1
 Q
 
B ;Сброс в буфер
 N i,i1,i2,i3,i4,met,mtbd,LABEL
 K @mas3@("bk"),@mas3@("bok"),@mas3@("bokE") S (@mas3@("bk"),@mas3@("bok"))="Элементы боковины",@mas3@("bokE")=$NA(@mas2@(bok))
 I $D(@mas5) D  D B1(1) Q                                           ;"Уточненная"
 .S i=0,i1=1 F  S i=$O(@mas5@(i)) Q:i=""  D  S i1=i1+1
 ..S i2=$G(@mas5@(i,1))
 ..S @mas3@("bk",i1)=$$STR(i2,1),@mas3@("bok",i1)=$G(@mas2@(bok,i2,"2,1"))
 ..I $G(@mas2@(bok,i2,"2,5"))="Y" S @mas3@("bok")=$G(@mas3@("bok"))_","_i1
 ..I $G(@mas2@(bok,i2,"2,6"))'="" S @mas3@("bokE",i1)=$G(@mas2@(bok,i2,"2,6"))
 S i=0,i1=1 F  S i=$O(@mas2@(bok,i)) Q:i=""  D   S i1=i1+1      ;"Стандартная"
 .S @mas3@("bk",i1)=$$STR(i),@mas3@("bok",i1)=$G(@mas2@(bok,i,"2,1"))
 .I $G(@mas2@(bok,i,"2,5"))="Y" S @mas3@("bok")=$G(@mas3@("bok"))_","_i1
 .I $G(@mas2@(bok,i,"2,6"))'="" S @mas3@("bokE",i1)=$G(@mas2@(bok,i,"2,6"))
 D B1(0)
 Q
B1(FLAG) S i="" F  S i=$O(@mas3@("bk",i)) Q:i=""  D  S @mas3@("bk",i)=str
 .S str=@mas3@("bk",i),met=$P($P(str," G:$T(",2),")") I met="" Q
 .I $D(LABEL(met)) S str=$P(str," G:$T(")_" G "_met Q
 .S str=$P(str," G:$T(")_$S(FLAG:"",1:" Q")
 Q
 
STR(%1,%2) ;Трансформация условия на строку
 S %0="" N Tmp
 S met=$G(@mas2@(bok,%1,"2,4")),Str=$G(^("2,2")),LABEL(Str)=""
 S Tmp=$G(@mas2@(bok,%1,"2,3"))
 S %0=Str_" "_$S(Tmp'="":" I "_$$TR^%ZAPM.bs.BSsFUNM(Tmp,"CurIndx",((i1-1)*C)),1:"")_" S BUF("_((i1-1)*C)_"+C,Z)=$G(BUF("_((i1-1)*C)_"+C,Z))+$S($D(D(Z,"_Str_")):D(Z,"_Str_"),1:D(Z)) "
 S %0=%0_$S(met="Q":met,1:" G:$T("_met_")'="""" "_met_$S($G(%2)="":" Q",1:""))
 I %0["^(" S %0=$$AddGet^%ZAPM.bs.BSzmOL(%0,"^(","$G(@masR@(")
 Q %0
