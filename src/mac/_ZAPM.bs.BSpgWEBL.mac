ROUTINE %ZAPM.bs.BSpgWEBL
TKWEBL	;PG&A,TK-WEB,1.1,LANGUAGE SUPPORT;12AUG97  13:49 [ 10/15/97   8:12 AM ];12JUN2003  16:32;;23OCT2000  09:15
	;;Copyright (C) 1997 Patterson, Gray and Associates, Inc.
	;
	Q
	;
MODIFY Q:URI["/NOMOD/"  S $ZT="ERROR^%ZAPM.bs.BSpgWEBL"
	F J=0,.1 I $D(@GLB@(0,J)) S L="" F  S L=$O(@GLB@(0,J,L)) Q:L=""  S A=L\1,B=^(L),E=$P(B,"`",2),C=$S($D(SKP(A)):SKP(A),1:$G(@GLB@(A))) D @$P(B,"`")
	S EXP=-1,$P(XX,"`",4)="" Q
M1	S FL=FL-$L(C),D=$P(B,"`",3),C=$P(C,E)_$S($D(%(D))#2:%(D),$D(%("FORM",D))#2:%("FORM",D),D?.E1P.E&(D'?1"^"1A.E):"",1:$G(@D))_$P(C,E,2,99),SKP(A)=C,FL=FL+$L(C) Q
M2	D MX S D="F=$$XTR(""$$"_$P(B,"`",3)_$S($E(F)="(":F,F="":"",1:"(F)")_""")",@D,D=C,C=$P(C,E)_F_$P($P(C,E,2,99),">",2,99),SKP(A)=C,FL=FL+$L(C)-$L(D) U TCP Q
M3	D MX S eC=A,D="F=$$OUT("""_$P(B,"`",3)_""")",@D,D=C
 I eC S D=$L($P(C,E)_$P($P(C,E,2,999),">",2,99))-$L(C),FL=FL+$L(F)+D+eC+6,F=$P(C,E)_F,SKP(A)=F,D="</pre>"_$P($P(C,E,2,999),">",2,99),^mtempBSpgTWEB($J,A,$O(^mtempBSpgTWEB($J,A,""),-1)+1)=D
	I 'eC S C=$P(C,E)_F_$P($P(C,E,2,999),">",2,99),SKP(A)=C,FL=FL+$L(C)-$L(D)
	U TCP K eC Q
MX	S F=$P(C,E,2),D=A I F'[">" F  S D=$O(@GLB@(D)) Q:'D  S SKP(D)=$S($D(SKP(D)):SKP(D),1:$G(@GLB@(D))),F=F_$P(SKP(D),">"),SKP(D)=$P(SKP(D),">",2,99) I SKP(D)'="" Q
	S F=$E($P(F,">"),2,9999) Q
	;
MON(X)	Q $F("JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC",X)\3
	;
GMT(X,T)	;($H,timezone)
	N K,Y,D,M S T=$P(X,",",2)+(-T*3600) S:T<0 X=X-1,T=T+86400 S:T>86400 X=X+1,T=T-86400
	S K=X>21608+305+X,Y=4*K+3\1461,D=K*4+3-(1461*Y)+4\4,M=5*D-3\153,D=5*D-3-(153*M)+5\5,M=M+2,Y=M\12+Y-60,M=M#12+1,DT=1900+Y*100+M*100+D
	S D=$P("Mon,Tue,Wed,Thu,Fri,Sat,Sun",",",X+3#7+1)_", "_D_" "_$P("Jan,Feb,Mar,Apr,May,Jun,Jul,Aug,Sep,Oct,Nov,Dec",",",M)_" "_(1900+Y)
	S M=T\60,M=$E(M\60+100,2,3)_":"_$E(M#60+100,2,3)_":"_$E(T#60+100,2,3) Q D_" "_M_" GMT"
	;
	;
INIT D INIT^%ZAPM.bs.BSpgWEBH Q
	;
GIF(T) S ^%ZAPM.bs.BSpgWEBGIF(CON,A)=T Q "GIF("_CON_","_A_")"
	;
TOFF(T) N (T) S T=-T+$G(^%ZAPM.bs.BSpgWEBC("TimeZone")),T=$P($H,",",2)+(-T*3600),X=+$H S:T<0 X=X-1,T=T+86400 S:T>86400 X=X+1,T=T-86400 S X=+X_","_T D TDATE(X) Q DTMA
AUTH(T)	I '$D(%("AUTHORIZATION:")) Q "(missing)"
 Q $P($$UUDEC^%ZAPM.bs.BSpgWEBU($P(%("AUTHORIZATION:"),"Basic ",2)),$C(15))
	;
LASTV(T) N A,B,C,D,J,K S A=$G(%("ADDRESS")) Q:A="" ""  S (B,C,D,J)="" F K=1:1:100 S C=$O(^mtempBSpgWEBT(C)) Q:C=""  Q:J>4  F  S D=$O(^mtempBSpgWEBT(C,D)) Q:D=""  Q:J>4  I $G(^(D,"ADDRESS"))=A S B=B_"<TD>"_$G(^("COMMAND"))_"<TR>",J=J+1
	Q B
URLDATA()	Q $P($G(URI),"?",2)
	;
XTR(T) M AA=% S AA=A N (T,F,CON,AA,TBL,FLN,URI,DTMA) S $ZE="XERROR^%ZAPM.bs.BSpgWEBL" M %=AA S A=AA,T="T="_T,@T Q T
XERROR	Q $TR($ZE,"<>","||")_" "_T
OUT(T) M AA=% S AA=A N (AA,F,T,OS,CON,SKP,eC) S Z=0,$ZT="FERROR^%ZAPM.bs.BSpgWEBL",A=AA M %=AA D SETUP^%ZAPM.bs.BSpgWEBU1
	W "<PRE>" D @T D OUTX X DEVC Q T
OUTX S $ZT="RERROR^%ZAPM.bs.BSpgWEBL",T=""
	X DEVR U DEV F J=2:1 R A#253 S @ZC S T=T_A_$S($L(A)<253:$C(13,10),1:"") Q:Z  I $L(T)>500 G OUTX2
	S T=T_"<\PRE>",eC=0 Q
OUTX2 S SKP(eC,0)="N A S A="""" F  S A=$O(^mtempBSpgTWEB($J,"_eC_",A)) Q:A=""""  W ^(A),!" K ^mtempBSpgTWEB($J,eC) S B=0
 F J=2:1 R A#253 S @ZC S A=A_$S($L(A)<253:$C(13,10),1:""),B=B+$L(A),^mtempBSpgTWEB($J,eC,J)=A Q:Z
	S eC=B Q
RTN N (v,%,URI,FLN,GLB,FL,SKP,OS,RLC,LL,PAGE,CON,CNT,ST,AUT,TYPE,Y,DTMA,DEVD,DEVR,DEVW,DEVC,DEV,FIL,ZC,PASS) S Z=0,$ZT="RTERROR^%ZAPM.bs.BSpgWEBL"
	I $P(FLN,":",2)>0 ZN $P(FLN,":",2) S FLN=$P(FLN,":")
	S A="A=$T("_FLN_")",@A I A'["tkweb"&(A'["TKWEB") S LL="<H3>Privileged Reference</H3>Entry reference not enabled for TK-WEB" Q
	I URI?.E1"."1A.E,"ERT"[$E($P(URI,".",2)),URI'[".RTF" D @FLN Q
 D SETUP^%ZAPM.bs.BSpgWEBU1 D @FLN I $D(PAGE)!$D(NOFILE) X DEVC K DEVD Q
RTN1 S $ZT="RERROR^%ZAPM.bs.BSpgWEBL",T="" I $D(DEVD) X DEVD K NOPAGE Q
	X DEVR U DEV S FL=0 F J=2:1 R A#253 S @ZC S (A,@GLB@(J))=A_$S($L(A)<253:$C(13,10),1:""),FL=FL+$L(A) Q:Z
	X DEVC Q
RTERROR	S LL="<h1><font color=""#bb0000"">TkWeb Error</h1></FONT><P><P>ERROR: "_$TR($ZE,"<>","||") K PAGE Q
	;
FERROR	Q "<P>"_$TR($ZE,"<>","||")_"<P>"_T
ERROR S T=$TR($ZE,"<>","||")_" "_$G(D),^%ZAPM.bs.BSpgWEBF("ERR","TKWEBL",CON)=$ZE_";"_$G(FLN) K PAGE Q
RERROR	S ZT=$ZE
	G:ZT'["END" ERROR X DEVC Q
	;
GLOB S $ZT="ERROR^%ZAPM.bs.BSpgWEBL" I FLN?1.AN1"^"1.AN.E S FLN=$TR($P($P(FLN,"?"),"."),LC,UP),EXP=-1 D RTN G GLOB1:'$D(PAGE) S URI=$TR(PAGE,LC,UP) K PAGE G GET^%ZAPM.bs.BSpgWEBH
 S A=$E($P(FLN,"("),2,99) I '$D(^%ZAPM.bs.BSpgWEBC("DBACCESS","^"_A)) S LL(1)="Privileged Reference<p>Global not enabled for TK-WEB" G GLOB1
	S GLB=$P(FLN,".")
 I $D(@GLB)=0 D 404^%ZAPM.bs.BSpgWEBH2 G GLOB1
	I $D(@GLB)=1 S LL(1)=@GLB G GLOB1
	I $G(@GLB)?1.N S FL=@GLB G GLOB1
	S FL="",A="" F  S A=$O(@GLB@(A)) Q:A=""  S FL=FL+$L($G(@GLB@(A)))
GLOB1 G GET1^%ZAPM.bs.BSpgWEBH
	;
	; ;WIDTH;COLOR;FONTFACE;SIZE;ALIGN;COL;ROW;OTHER;VALIGN  *=LINK
TH(T)	S T=$TR(T,$C(13,10)),TBL($P(T,";")_" ")=T Q ""
TD(T)	S T=$TR(T,$C(13,10)) I '$D(TBL($P(T,";")_" ")) Q T
	N A,Y,I,B,C,N S A=TBL($P(T,";")_" "),Y="<TR>" F I=2:1 Q:$P(A,";",I,99)=""  S C=$P(A,";",I) D TDT S Y=Y_"</TD>"
	Q Y
TDT	F N=1:1:9 S B(N)=$S($P(C,":",N)="":$G(B(N)),1:$P(C,":",N))
	S Y=Y_"<TD" S:B(1)]"" Y=Y_" WIDTH="_B(1)
	S:B(2)]"" Y=Y_" BGCOLOR="_B(2)
	S:B(5)]"" Y=Y_" ALIGN="_$P(",LEFT,RIGHT,CENTER",",",$F("LRC",B(5)))
	I B(9)]"" S Y=Y_" VALIGN="_$P(",TOP,BOTTOM,CENTER",",",$F("TBC",B(9)))
	S:B(6)]"" Y=Y_" COLSPAN="_B(6)
	S Y=Y_">" I B(3)_B(4)'=""!(B(8)["W") S Y=Y_"<FONT" S:B(3)]"" Y=Y_" FACE="""_B(3)_"""" S:B(4)]"" Y=Y_" SIZE="_B(4) S:B(8)["W" Y=Y_" COLOR=""#FFFFFF""" S Y=Y_">"
	S B(8)=$TR(B(8),"W") S:B(8)'="" Y=Y_"<"_B(8)_">" I $P(T,";",I)'["`" S Y=Y_$P(T,";",I) Q
	S Y=Y_"<a href="""_$P($P(T,";",I),"`",2)_""">"_$P($P(T,";",I),"`")_"</a>" Q
	;
TDATE(X)	D DATE,TIME S DTM=$P(DT,"/",1,2)_" "_LTM
	S DTMA=$P("Thursday,Friday,Saturday,Sunday,Monday,Tuesday,Wednesday",",",X#7+1)_"  "_$P("January,February,March,April,May,June,July,August,September,October,November,December",",",MT)
	S DTMA=DTMA_" "_+$P(DT,"/",2)_$S($P(DT,"/",3)<30:", 20",1:", 19")_$P(DT,"/",3)_"  "_TMA Q
TIME	S LTM=$P(X,",",2),LTM=$E(LTM\3600+100,2,3)_":"_$E(LTM#3600\60+100,2,3)_":"_$E(LTM#60+100,2,3),TMA=$S(LTM>12:LTM-12_":"_$P(LTM,":",2)_" PM",1:$P(LTM,":",1,2)_" AM") Q
	;         
DATE	S K=X>21608+305+X,YR=4*K+3\1461,D=K*4+3-(1461*YR)+4\4,MT=5*D-3\153,D=5*D-3-(153*MT)+5\5,MT=MT+2,YR=MT\12+YR+1840,MT=MT#12+1
	S DT=YR*100+MT*100+D,DT=$E(DT,5,6)_"/"_$E(DT,7,8)_"/"_$E(DT,3,4) K K,D Q
	;
MOD	; EXAMPLE OF MODIFY LOGIC FOR TKWEBH
