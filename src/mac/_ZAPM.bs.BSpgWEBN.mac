ROUTINE %ZAPM.bs.BSpgWEBN
TKWEBN	;PG&A,TK-WEB,1.1,NAME UTIL;;;;09FEB98  17:11
	;;Copyright (C) 1997 Patterson, Gray and Associates, Inc.
	;
	; NS LOOKUP - CURRENTLY ONLY WORKS FOR TYPES 1 & 15
	;           - RECURSIVE MODE
	;
	;
NAME(X,Y) K MX S:$G(Y)<1 Y=1 S:'$G(CON) CON=1 N:1 (CON,X,Y,MX,NSTR) S NS(1)=$G(^%ZAPM.bs.BSpgWEBC("DNS")) Q:NS(1)="" "" Q $$NAMX(X,Y,CON)
	;
NAMX(X,Y,ID)	;NAME RESOLVER
	S OS=$S($ZV["M3":"M3",1:"MSM")
NAMB	S I=$O(NS("")) Q:I="" -4 S NS=NS(I) K NS(I) I NS'?1.N1"."1.N1"."1.N1"."1.N S CON=CON+1000,NS=$$NAME(NS,1) G:NS'>0 NAMB
	G:$D(BNS(NS)) NAMB
	S X=$$LITL^%ZAPM.bs.BSsFUNM(X)
	S B="" F I=1:1:$L(X,".") S C=$P(X,".",I),B=B_$C($L(C))_C
	S NAME=$C(ID\256,ID#256,1,0,0,1,0,0,0,0,0,0)_B_$C(0,0,Y,0,1)
	I $D(NSTR) U 0 W !,"looking for - ",X," on ",NS," type ",Y
	S E=-6 I OS="MSM" D  C 56:(%dns) E  G NAMT
	. S nt=($ZB($V(0,-4,2),#F,1)=10)
	. O 56:(:$S(nt:8,1:0)+0):"TCP" U 56 S %dns=$KEY
	. U 56:(%dns) W /SOCKET(NS,53) S %xddb=$KEY,lza=$ZA,lzb=$ZB,lzc=$ZC
	. W *$L(NAME)\256,*$L(NAME)#256,NAME
	. S E=-5 R *A:10 I $T R *B:10 I $T,A+B>0 R C#(A*256+B):10 I $T
	I OS="M3" O "TCP":(NS_"/"_53) U "TCP" D  C "TCP" E  G NAMT
	. W *$L(NAME)\256,*$L(NAME)#256,NAME,!
	. S E=-5 R *A:10 I $T R *B:10 I $T,A+B>0 R C#(A*256+B):10 I $T
	;
	I $D(NSTR) W !,"RECEIVED - ",$L(C)," chara ",! F JJ=1:1:$L(C) W $A(C,JJ)," " I JJ=$L(C) W ! F JJ=1:1:$L(C) W $S($A(C,JJ)<31!($A(C,JJ)>126):".",1:$E(C,JJ))
	I $L(C)<$L(NAME) S E=-1 G NAMA
	I $A(C,1)*256+$A(C,2)'=ID S E=-2 G NAMA
	I $A(C,4)#16 S E=-100-($A(C,4)#16) G NAMA
	S I=13,J=0 F KK=6,8,10,12 S KI(KK)=$A(C,KK) S J=J+1,KK(KK)=J F J=J:1:KI(KK)+J-1 S RK(J)=KK D NAME1 Q:$A(C,I)<1
	I $D(NSTR) W !,"Records - " F I=1:1 Q:'$D(RT(I))  W ?15,RT(I),?19,$P("QD,AN,NS,AR",",",RK(I)\2-2),?24,TTL(I)\86400,"d ",TTL(I)#86400\3600,"h ",TTL(I)#3600\60,"m ",TTL(I)#60,"s ",?41 W:",1,2,5,15,"[(","_RT(I)_",") RD(I) W !
	I $A(C,8)=0 G:$A(C,10) NAM5 S E=-3 G NAMA
	G NAMM:Y=15 S IP="" I $G(KK(8)) F J=KK(8):1 Q:'$D(RT(J))  I RT(J)=Y S IP=$G(RD(J)) Q
	I IP="" S E="" G NAMA
	Q IP
NAMM	S KK=KK(12) S:KI(12)=KI(10) KK=100 F J=KK(8):1 Q:$D(RT(J))=0  I RT(J)=15 D:$G(RD(KK))="" NAMMX S I=RD(J),MX($P(I," ")_"."_KK)=$P(RD(J)," ",2,9)_" "_$G(RD(KK)),KK=KK+1
	Q $D(MX)
NAMI(y)	N J S x="" F J=1:1:4 S x=x_"."_$A(y,J)
	Q $E(x,2,99)
NAMT	;
NAMA	S BNS(NS)="" I $O(NS(""))="" Q E
	S ID=ID+1000 G NAMB
NAMMX	S X=$P(RD(J)," ",2,9) N MX S RD(KK)=$$NAME(X,1) Q
NAM5	F K=1:1 I '$D(NS(K)) Q
	S KK=KK(12) F J=KK(10):1 Q:$G(RK(J))'=10  I $G(RT(J))=2 S F=$S($D(RD(KK)):RD(KK),1:RD(J)),KK=KK+1 S:F]"" NS(K)=F,K=K+1
	K C,RT S CON=CON+1000 G NAMB
	;
NAME1	S RN(J)=$$NAME2(C,I),I=LI,RT(J)=$A(C,I+1)*256+$A(C,I+2),RC(J)=$A(C,I+3)*256+$A(C,I+4) I KK=6 S I=I+5,RD(J)="",TTL(J)=0 Q
	S TTL(J)=$A(C,I+5)*256+$A(C,I+6)*256+$A(C,I+7)*256+$A(C,I+8),DL(J)=$A(C,I+9)*256+$A(C,I+10),RD(J)=$E(C,I+11,I+10+DL(J))
	S:RT(J)=1 RD(J)=$$NAMI(RD(J)) S:RT(J)=2!(RT(J)=5) RD(J)=$$NAME2(C,I+11) S:RT(J)=15 RD(J)=$A(RD(J))*256+$A(RD(J),2)_" "_$$NAME2(C,I+13)
	S:RT(J)=12 RD(J)=$$NAME2(C,I+11)
	S I=I+DL(J)+11 Q
NAME2(C,I)	S F="" F I=I:0 S D=$A(C,I) G:D=192 NAME3 S:D>0 F=F_"."_$E(C,I+1,I+D),I=I+D+1 I D<1 Q
	S LI=I Q $E(F,2,99)
NAME3	S D=F_"."_$$NAME2(C,$A(C,I+1)+1),LI=I+1 Q $E(D,2,99)
	;
TN	W $$NAME("pattersongray.com") Q
	Q
NSLOOKUP R ">>",X,!,"TYPE (1-255) ",TYPE,! Q:X=""  S NSTR=1 W $$NAME^%ZAPM.bs.BSpgWEBN(X,TYPE),!! X:$D(MX) "N (MX) W" K CON G NSLOOKUP
	;
REVERSEK	R ">>",X,! Q:X=""  I X'?1.N1"."1.N1"."1.N1"."1.N W "Need dotted quad",! G REVERSEK
	S Y=X,X="" F J=1:1:4 S $P(X,".",J)=$P(Y,".",4-J+1)
 S NSTR=1,TYPE=12,X=X_".in-addr.arpa"  W $$NAME^%ZAPM.bs.BSpgWEBN(X,TYPE),!! X:$D(MX) "N (MX) W" K CON G REVERSEK
	;
REVERS(X)	S Y=X,X="" F J=1:1:4 S $P(X,".",J)=$P(Y,".",4-J+1)
 S X=X_".in-addr.arpa"  Q $$NAME^%ZAPM.bs.BSpgWEBN(X,12)
	;
AA S AA="" F  S AA=$O(^%ZAPM.bs.BSpgWEBL("ADDR",AA)) Q:AA=""  I $G(^(AA))="" W !,AA,?20 S B=$$REVERS(AA) W B I B'<0 S ^%ZAPM.bs.BSpgWEBL("ADDR",AA)=B
	Q
RLOG(X) Q:X=""  S B=$$REVERS(X) I B'<0 S ^%ZAPM.bs.BSpgWEBL("ADDR",X)=B Q
