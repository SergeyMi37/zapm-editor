ROUTINE %ZAPM.ed.BSC4cfg
%BSC4cfg ;БИБЛИОТЕКА ФУНКЦИЙ РАБОТЫ С ОПЦИЯМИ ; 21:45   11.01.2001
 ;
 ;    Copyright (C) 2002 Serge W. Mikhaylenko
 ;    All Rights Reserved
 ;
 ;КОРРЕКТНО РАБОТАЮТ ТОЛЬКО НОВЫЙ ФОРМАТ ТАБЛИЦ ОПЦИИ: OPT=1 !!!!!!!!!!
 Q
VAR ;НАЧАЛО
 S Bs=$$CurT^%ZAPM.ed.BSChT($$%^%ZAPM.ed.BSCh(BSNSP),$$%^%ZAPM.ed.BSCh(BSPART),$$%^%ZAPM.ed.BSCh(BSTABL))
 S PART=$P(Bs,"("),TABL=$QS(Bs,1)
 S O=Bs
 Q
BSVAR ;ПЕРЕМЕННЫЕ ДЛЯ ТАБЛИЦЫ
 w "<input type=""hidden"" name=""BSTABL"" value="""_%KEY("BSTABL")_""">",$G(BK) 
 w "<input type=""hidden"" name=""BSPART"" value="""_%KEY("BSPART")_""">",$G(BK) 
 ;w "<input type=""hidden"" name=""BSNSPACE"" value="""_%KEY("BSNSPACE")_""">",$G(BK)     
 Q
ENTER ;основной вход в таблицу опций
 N O D VAR
 D BEG1^%ZAPM.ed.BSC4
 W "<TITLE> <<<<<<<<<<<< "_$$TITLE(O)_"</TITLE></HEAD>",BK
 W "<BODY  onkeyPress=""javascript: if (27 == event.keyCode) window.close();"" "_$$0^%ZAPM.ed.BSCh(3)_">",BK
 W "<CENTER>"_AF_O_EF
 I $G(@O@("OPT"))'=1,'$P($G(@O@("OPT")),",",2) W $$LNG^%ZAPM.ed.BSC4("В таблице опций",158)_" '"_O_"' "_$$LNG^%ZAPM.ed.BSC4("неправильный параметр",159)_BK D BACK^%ZAPM.ed.BSCh Q
 D OPT(O)
 D BSVAR
 w "<BR><input type=""hidden"" name=""BSNSP"" value="""_BSNSP_""">"
 w "<BR><input type=""hidden"" name=""BSLABEL"" value=""SAVE^~BSC4cfg"">"
 D BU2
 W "</form></CENTER>"
ENTE D END2^%ZAPM.ed.BSCh
 Q
BU2      w "<input type=""SUBMIT"" name=""SubMit1""  value=""Запомнить""> "
 w "<input type=""RESET"" name=""Reset1""  ONCLICK=""window.close();""  value=""Отменить"">"
 Q       
SAVE ;СОХРАНИТЬ НОВЫЕ ПАРАМЕТРЫ
 N O
 D VAR
 D SAVEOPT^%ZAPM.ed.BSC4cfg(O)
 D END2^%ZAPM.ed.BSCh
 q
TITLE(TBL) ;вывести заголовок
 I $G(@TBL@("OPT"))=1 Q $P($G(@TBL@(2,4)),"@",15)
 E  Q $P($G(@TBL),"@",1)
 Q
OPT(TBL,NOFORM) ;ФОРМА С ПАРАМЕТРАМИ
 W "<form action="""_$$ADDLIB^%ZAPM.ed.BSC4()_""" method=""POST"">"
 N BSLABEL,BSTOP,BSNSP1
 D ADDBSPOST^%ZAPM.ed.BSC4reg
 D BU2
 I '$D(@TBL@("OPT")) W $$LNG^%ZAPM.ed.BSC4("В таблице опций отсутствуют базовые параметры",160)_BK D BACK^%ZAPM.ed.BSCh G ENTE
 ;w "<input type=""hidden"" name=""BSROU"" value="""_$G(MainRef,"MAIN^~BSCh1")_""">"
 N I,II,P1,P2,P3,P4,P5,X,Y,S
 w BK,"<table border=0>",BK
 I $G(@TBL@("OPT"))'=1 S S=@$ZR F I=$S($P(S,",",3):$P(S,",",2),1:1):1 Q:'$D(@TBL@(I))  D  W BK
 .Q:'$P(S,",",2)  Q:'$P(S,",")
 .S P4=$P($G(@TBL@(I,+$P(S,",",1))),"@",15),P1=$P($G(@TBL@(I,+$P(S,",",2))),"@",15)
 .S P3=" SIZE=50 ",P5=$P($P($G(@TBL@(I,$P(S,",",2))),"@",18),"#")
 .I P4'="",$P($G(@TBL@(I,$P(S,",",2))),"@",5)="" D text1(P1,P3,P5)
 I $G(@TBL@("OPT"))=1 F I=3:1 Q:$P($G(@TBL@(I,2)),"@",15)=""  D  W BK
 .S P1=$P(@TBL@(I,1),"@",15),P2=$P(@TBL@(I,2),"@",15),P3=$P(@TBL@(I,3),"@",15)
 .S P4=$P(@TBL@(I,4),"@",15),P5=$P(@TBL@(I,5),"@",15)
 .D TAB
 I $$GETOPT(TBL,"NO")
 w "</table>"
 Q
TAB ;ТЕКУЩАЯ СТРОКА
 I P2="textarea" D  Q
 .W "<tr><td align=""right""><textarea name="""_$$name(P5)_""" "_P3_" >"_P1_"</textarea></td><td>",$$TEGS^%ZAPM.ed.BSre(P4),"</td></tr>"
 I P2="text" D text(P1,P3,P5) Q
 I P2="checkbox" D  Q
 .W "<tr><td align=""right""><input type=""checkbox"" name="""_$$name(P5)_""" value="""_P1_""" "_P3_$$ch(P1)_"></td><td>",$$TEGS^%ZAPM.ed.BSre(P4),"</td></tr>"
 I P2="radio" w "<p>",BK D  Q
 .F II=1:1:$L(P4,"~") W "<tr><td align=""right""><input type=""radio"" name="""_$$name(P5)_""" value="_II_" "_P3_$$ch(P1=II)_"></td><td>",$$TEGS^%ZAPM.ed.BSre($P(P4,"~",II)),"</td></tr>",BK
 .w "</p>"
 Q       
text(P1,P3,P5) W "<tr><td align=""right""><input type=""text"" name="_$$name(P5)_" "_P3_" value='"_P1_"'></td><td>",$$TEGS^%ZAPM.ed.BSre(P4),"</td></tr>"
 Q
text1(P1,P3,P5) W "<tr><td align=""right"">",$$TEGS^%ZAPM.ed.BSre(P4),"</td><td><input type=""text"" name="_$$name(P5)_" "_P3_" value='"_P1_"'></td></tr>"
 Q
name(P) ;ИМЯ КОМПОНЕНТЫ
 I P="" Q $QS(TBL,1)_I
 Q P
ch(P) I P q " checked "
 q ""
SAVEOPT(TBL) ;ПРИСВОЕНИЕ
 N I,C,N,S
 ;W "<BODY >",BK
 W "<BODY onload='window.close();'>",BK
 W "<CENTER>"
 W "<H3>"_$$LNG^%ZAPM.ed.BSC4("Опции запомнены",161)_"</H3>"
 
 W "<HR></CENTER>"
 I $G(@TBL@("OPT"))'=1 S S=@$ZR F I=$S($P(S,",",3):$P(S,",",2),1:1):1 Q:'$D(@TBL@(I))  D
 .Q:'$P(S,",",2)  Q:'$P(S,",")  Q:$P($G(@TBL@(I,$P(S,",",2))),"@",5)'=""
 .;S P4=$P($G(@TBL@(I,+$P(S,",",1))),"@",15),P1=$P($G(@TBL@(I,+$P(S,",",2))),"@",15)
 .S N=$P($P($G(@TBL@(I,$P(S,",",2))),"@",18),"#") i N="" s N=$QS(TBL,1)_I
 .I $TR($G(%KEY(N)),"@","")=$G(%KEY(N)) S $P(@TBL@(I,$P(S,",",2)),"@",15)=$G(%KEY(N)) Q
 .S $P(@TBL@(I,$P(S,",",2)),"@",15)=$TR($G(%KEY(N)),"@","") D PROT(N)
 
 I $G(@TBL@("OPT"))=1 F I=3:1 Q:$P($G(@TBL@(I,2)),"@",15)=""  D
 .S C=$P($G(@TBL@(I,2)),"@",15)
 .I $P($G(@TBL@(I,5)),"@",15)'="" S N=$P($G(@TBL@(I,5)),"@",15)
 .E  S N=$QS(TBL,1)_I
 .I C="checkbox" D  Q
 ..I $D(%KEY(N)) S $P(@TBL@(I,1),"@",15)=1
 ..E  S $P(@TBL@(I,1),"@",15)=0
 .I $TR($G(%KEY(N)),"@","")=$G(%KEY(N)) S $P(@TBL@(I,1),"@",15)=$G(%KEY(N)) Q
 .S $P(@TBL@(I,1),"@",15)=$TR($G(%KEY(N)),"@","") D PROT(N)
 Q
PROT(N) ;ЗАПИСЬ ЗАПРЕЩЕННОГО
 W "<br>"_$$LNG^%ZAPM.ed.BSC4("Параметр",162)_" '"_N_"' "_$$LNG^%ZAPM.ed.BSC4("имеет запрещенный символ",163)_" '@': "_RED_$G(%KEY(N))_EF_"<br>"
 W "<br>"_$$LNG^%ZAPM.ed.BSC4("Запомнили",164)_" : "_GREEN_$TR($G(%KEY(N)),"@","")_EF_"<br>"
 Q
GETOPT(TBL,NO,NK) ;ВОЗВРАТИТЬ ЗНАЧЕНИЕ ОПЦИИ NO
 ;NO - ИМЯ ПОКАЗАТЕЛЯ
 ;NK - НОМЕР КОЛОНКИ В ТАБЛИЦЕ 
 N N,I,Q S Q="?"
 try { ;в TBL может быть ссылка на несуществующую область
	 I $G(@TBL@("LST"),1)'=$G(@TBL@("OPT","LST")) D GREATINDX
 } catch e {}
 I $G(@TBL@("OPT","LST",NO))'="" S I=$G(@TBL@("OPT","LST",NO)) Q $P($G(@TBL@(I,$G(NK,1))),"@",15)
 F I=3:1 Q:$P($G(@TBL@(I,2)),"@",15)=""  D
 .I $P($G(@TBL@(I,5)),"@",15)'="" S N=$P($G(@TBL@(I,5)),"@",15)
 .E  S N=$QS(TBL,1)_I
 .I N=NO S Q=$P($G(@TBL@(I,$G(NK,1))),"@",15) Q
 Q Q
SETOPT(TBL,NO,Q,NK) ;ПРИСВОИТЬ ЗНАЧЕНИЕ ОПЦИИ NO=Q
 N N,I
 I $G(@TBL@("LST"),1)'=$G(@TBL@("OPT","LST")) D GREATINDX
 I $G(@TBL@("OPT","LST",NO))'="" S I=$G(@TBL@("OPT","LST",NO)),$P(@TBL@(I,$G(NK,1)),"@",15)=Q Q
 F I=3:1 Q:$P($G(@TBL@(I,2)),"@",15)=""  D
 .I $P($G(@TBL@(I,5)),"@",15)'="" S N=$P($G(@TBL@(I,5)),"@",15)
 .E  S N=$QS(TBL,1)_I
 .I N=NO S $P(@TBL@(I,$G(NK,1)),"@",15)=Q Q
 Q
GREATINDX //СОЗДАТЬ ИНДЕКС ПОКАЗАТЕЛЕЙ ОПЦИЙ
 S $ZT="ERRGREAT^%ZAPM.ed.BSC4cfg"
 K @TBL@("OPT","LST") S @TBL@("OPT","LST")=$G(@TBL@("LST"),1)
 F I=3:1 Q:$P($G(@TBL@(I,2)),"@",15)=""  D
 .I $P($G(@TBL@(I,5)),"@",15)'="" S N=$P($G(@TBL@(I,5)),"@",15)
 .E  S N=$QS(TBL,1)_I
 .S @TBL@("OPT","LST",N)=I
 Q
ERRGREAT Q
