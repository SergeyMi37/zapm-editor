ROUTINE %ZAPM.ed.BSDOS
%BSDOS ;ДОС-ИНТЕРФЕЙС ; 15:29   15.04.2003
 D 0 G D
0 I %BS(12)'="" G PROT^%ZAPM.ed.BSMSM:'$$SizeFile^%ZAPM.ed.BSDOS2(1)
 S OO=31,OOO="^%ZAPM.ed.BS" K %JB D 24^%ZAPM.ed.BSTM,^%ZAPM.ed.BSB,ZA^%ZAPM.ed.BSF Q:R1=27  I %B=3 S ls=$$ZOS4() D O^%ZAPM.ed.BSF7 W $$r^%ZAPM.ed.BS3 G 0
 I %B=2 D DRV D:$D(%DRV) DSPACE G 0
 I %B=1 D DOS
 G 0
D D TIT^%ZAPM.ed.BST,W^%ZAPM.ed.BST Q
EXE(EXE,O,S) ;ВЫЗОВ НА ВЫПОЛНЕНИЕ ДОС-ПРОЦЕССА
 I $ZV["Cach"||($ZV["IRIS") D EXE^%ZAPM.ed.BSCEXE(EXE) Q
 I $P($ZV,"Version ",2)<4 Q
 I '$G(O),$V($V(44)+392) Q:$G(S)=""  D ErrMsg^%ZAPM.ed.BSXfun(S) Q
 I $E(%BS(1),6) W $$bel^%ZAPM.ed.BS3 Q  ;```D ErrMsg^%ZAPM.ed.BSXfun("Для М-Сервера запрещено ! "_$G(S)) Q
 S EXE=$$JOB^%HOSTCMD(EXE)
 Q
ZOS1(D) ;Set Current Drive
 I $ZV["MSM" Q $ZOS(1,D)
 Q $$SCDrive^%ZAPM.ed.BSCEXE(D)
ZOS2(D) ;Delete a File
 I $ZV["MSM" Q $ZOS(2,D)
 I $$DelFile^%ZAPM.ed.BSCEXE(D) Q ""
 Q 1
ZOS3(S,D) ;Rename a File
 I $ZV["MSM" Q $ZOS(3,S,D)
 Q $$RenFile^%ZAPM.ed.BSCEXE(S,D)
ZOS4() ;Get MS-DOS Version Number
 I $ZV["MSM" Q "MS-DOS "_$ZOS(4)
 Q $$Ver^%ZAPM.ed.BSCEXE()
ZOS5(F) ;Set File Attributes
 I $ZV["MSM" Q $ZOS(5,F)
 Q $$AtrFile^%ZAPM.ed.BSCEXE(F)
ZOS6(D) ;Create a New Directory
 I $ZV["MSM" Q $ZOS(6,D)
 Q $$CNDir^%ZAPM.ed.BSCEXE(D)
ZOS7(D) ;Remove an Existing Directory
 I $ZV["MSM" Q $ZOS(7,D)
 Q $$DelDir^%ZAPM.ed.BSCEXE(D)
ZOS8(D) ;Change Current Directory
 I $ZV["MSM" Q $ZOS(8,D)
 Q $$CCDir^%ZAPM.ed.BSCEXE(D)
ZOS9(D) ;Get Drive Parameters
 I $ZV["MSM" Q $ZOS(9,D)
 Q $$GetDPar^%ZAPM.ed.BSCEXE(D)
ZOS10(D) ;Get File Attributes
 I $ZV["MSM" Q $ZOS(10,D)
 Q $$ExsFile^%ZAPM.ed.BSCEXE(D)
ZOS11(D) ;Get Current Directory for Drive
 I $ZV["MSM" Q $ZOS(11,D)
 Q $$GCDir^%ZAPM.ed.BSCEXE(D)
ZOS12(D,V) ;Begin Directory Search
 I $ZV["MSM" Q $ZOS(12,D,V)
 Q $$BegDir^%ZAPM.ed.BSCEXE(D,V)
ZOS13(D) ;Continue Directory Search
 I $ZV["MSM" Q $ZOS(13,D)
 Q $$ContDir^%ZAPM.ed.BSCEXE(D)
ZOS14() ;Get Current Drive
 I $ZV["MSM" Q $ZOS(14)
 Q $$CurDisk^%ZAPM.ed.BSCEXE()
BaseDir() ;БАЗОВАЯ ДИРЕКТОИЯ
 N D S D=$P($G(^%ZAPM.ed.BSETUP("PATH",12,4)),"@",15)
 Q D
CurDisk() ;ТЕКУЩИЙ ДИСК
 N D S D=$P($P($G(^%ZAPM.ed.BSETUP("PATH",12,4)),"@",15),":")
 I $ZV["MSM" Q $S(D'="":D,1:$ZOS(14))
 Q $S(D'="":D,1:$$CurDisk^%ZAPM.ed.BSCEXE()) ;Cach
CurDir(Disk) ;ТЕКУЩАЯ ПОДДИРЕКТОРИЯ
 N D S D=$P($P($G(^%ZAPM.ed.BSETUP("PATH",12,4)),"@",15),":",2,999)
 I $ZV["MSM" Q $S(D'="":D,1:$ZOS(11,Disk))
 Q $S(D'="":D,1:$$GCDir^%ZAPM.ed.BSCEXE(Disk)) ;Cach
DRV N %B K M,DR S M=" A: B: ",i="CDEFGHIJKLMNOPQRSTUVWXYZ",T=$$ZOS14() F ii=1:1:$L(i) S %ER=$$ZOS1($E(i,ii)) I %ER>0 S %ER=$$ZOS11($E(i,ii)) I '(%ER<0) S M=M_$E(i,ii)_": "
 I $D(%BS)["0" D CLr^%ZAPM.ed.BSF4
 S M(1,1)=M_"@6@ @0",OO=1,OOO="M",%B=$L($P(M,T)," ")-1,%ER=$$ZOS1(T) D 24^%ZAPM.ed.BSTM,^%ZAPM.ed.BSB,ZA^%ZAPM.ed.BSF G DR:R1=27 S %DRV=$P($P(M," ",%B+1),":")
 K M,EXITout Q
DR W $$bel^%ZAPM.ed.BS3 K %DRV,EXITout Q
DSPACE D SPACE^%ZAPM.ed.BSERV
e Q
DOS I $ZV["Cach"||($ZV["IRIS") D DirDDR^%ZAPM.ed.BSCmDDR Q  ;```
 D DRV G:'$D(%DRV) DIREXIT
 D SI^%ZAPM.ed.BSN N (%BS,vl,%zT,%KAT,%File,%DRV) D SI^%ZAPM.ed.BSN,SA^%ZAPM.ed.BSsBUF,DIR,RE^%ZAPM.ed.BSsBUF Q
DIR ;ДИРЕКТОРИЯ
 S %D=%DRV,%ER=$$ZOS11(%D),%D=$S(%ER<0:"^",1:%D_":"_%ER_$S(%ER="\":"",1:"\")_"*.*")
 S BL="ABCDEFGHIJKLMNOPQRSTUVWXYZАБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧЬЪШЩЭЮЯ"
 S LL="abcdefghijklmnopqrstuvwxyzабвгдеежзийклмнопpстуфхцчьъшщэюя"
DIRNEW S ls="Сканиpование "_%D D WAITS^%ZAPM.ed.BSF2
 S %X=%D,%A=$L(%X,"\"),%B=$P(%X,"\",%A) I %B'="",%B'["*",%B'["." I $ZB(+$$ZOS10(%X),16,1) S %X=%X_"\*.*"
 I $E(%X,$L(%X))=":"!($E(%X,$L(%X))="\") S %X=%X_"*.*"
 S %ER=$$ZOS12(%X,32+16+4+1) I %ER<0 D ^%ZAPM.ed.BSDOS1 G DIREXIT
 S MRMR="W /CUP(23,1),/EL(0),$$clr^%ZAPM.ed.BS3(4),"" <Enter>-ВХОД <F3>-СМОТР.<F4>-РЕДАКТ <F6>-AKT <F8>-УДАЛИТЬ <F10>-ВЫБОР И ВЫХОД """
DIRO S BSR="^%ZAPM.ed.BSbufB",BST=%X_$G(%BS(3),$P)_$J_"u"_%BS(15) I $D(@(BSR_"(BST,""STA"")")) D STA^%ZAPM.ed.BST G DIRE
 S i=1,(kod,kof,kb)=0,STR="@@1@55@@@@1,1@1@"_$P(%BS,"!",5)_"@@@@@",%Y=%ER,^%ZAPM.ed.BSbufB(BST,"FTR",1,1)="I d'["" <DIR> "" S d1=$TR(d,BL,LL)"
DI S %I=$P(%Y,"^"),%J=$P(%Y,"^",2,999)
 S %K=$A(%J,22)
 I $ZB(%K,16,1) G:$E(%I)="." DIREC S ST=" "_$P(%I,".")_$J("",8-$L($P(%I,".")))_$S(%I[".":".",1:" ")_$J($P(%I,".",2),3)_" <DIR> ",kod=kod+1
 E  S %A=$A(%J,27),%B=$A(%J,28),%C=$A(%J,29),%D=$A(%J,30) S ST=" "_$P(%I,".")_$J("",8-$L($P(%I,".")))_$S(%I[".":".",1:" ")_$J($P(%I,".",2),3)_" "_$J("-"_(%A+(%B*256)+(%C*256*256)+(%D*256*256*256))_"-",11) D  ;
 .S kof=kof+1,kb=kb+(%A+(%B*256)+(%C*256*256)+(%D*256*256*256))
 S ST=ST_$J("",23-$L(ST)) S %A=$A(%J,25),%B=$A(%J,26),%C=256*%B+%A,%D=%C#32,%C=%C\32,%E=%C#16,%C=%C\16,%C=%C+1980
 S ST=ST_"  "_$E("  ",1,2-$L(%E))_%E_"-"_$E("00",1,2-$L(%D))_%D_"-"_%C_"  " ;ДАТА
 S %A=$A(%J,23),%B=$A(%J,24),%C=%B*256+%A,%D=%C#32*2,%C=%C\32,%E=%C#64,%C=%C\64,%A=%C\12,%B=%C#12 S:%B=0 %B=12
 S ST=ST_$E("  ",1,2-$L(%B))_%B_":"_$E("00",1,2-$L(%E))_%E_$S(%A:"p",1:"a") ;ВРЕМЯ
 S i=i+1,^%ZAPM.ed.BSbufB(BST,i,1)=STR_ST
DIREC S %Y=$$ZOS13(%Y) G:%Y'<0&($P(%Y,"^")'="") DI S ^%ZAPM.ed.BSbufB(BST,1,1)="@@1@55@@@@@1@"_$P(%BS,"!",4)_"@@@@@ .. "_$P(%X,"*")_"  "_$FN(kb,",")_" bytes  files-"_kof_" dir-"_kod
 S ^%ZAPM.ed.BSbufB(BST)="@@@@@@@1@1@22@80@@X MRMR@@%FK^%ZAPM.ed.BSDOS2@1@1@@@@@@@@1@@@@@@@@@@@@@@@@DelF8^%ZAPM.ed.BSDOS",se=i,ke=1,$P(@$ZR,"@",36)="2,1" D TAB^%ZAPM.ed.BSF1,^%ZAPM.ed.BST
DIRE I $D(EXITout) G DIREXIT
 D Kdf^%ZAPM.ed.BSDOS2
 I R1=27!(ny=1) D Kdf^%ZAPM.ed.BSDOS2 K @(BSR_"(BST)") S R1=27,%D=$P(%X,"\",1,$L(%X,"\")-2)_"\" D DIF G DIRD:%D="\",DIRNEW
 I d[" <DIR> " D Kdf^%ZAPM.ed.BSDOS2 S %D=$P(%X,"*")_$TR($E(d,2,13)," ","")_"\" D DIF G DIRO:%D["\\",DIRNEW
 D DIF I %File[".%BS" D VIEW^%ZAPM.ed.BSDOS2("View*.*") G DIRO ;???ЗАЧЕМ Я НЕ ПОМНЮ
 I R1=13 S EXITout=1,R1=13 D DIA,DIF,GLKL^%ZAPM.ed.BSDOS2 G DIREXIT
 G DIRO
DIRD D DRV G:$G(R1)=27 DIREXIT I '$D(%DRV) G DIRD
 G DIR
DIA N FF,F Q:'$P(@BSR@(BST),"@",3)  F FF=1:1 Q:'$D(@BSR@(BST,FF))  I $D(@BSR@(BST,FF,1)),$P(@BSR@(BST,FF,1),"@",12),$P(@BSR@(BST,FF,1),"@",15)'[" <DIR>" S F=$$FULLNAME($P(@BSR@(BST,FF,1),"@",15)) I F'="" S %BS("Tmp","DosFiles",F)=$P($P(@BSR@(BST,FF,1),"@",15),"-",2)
 Q
DelF8 I $E(%BS(1),17),R1=127 S R1=27,R2=79,R3=87 Q
 Q
DIREXIT K kod,kof,kb Q
DIF S %File=$$FULLNAME(d) Q
FULLNAME(d) Q $P(%X,"*")_$S($E(d,2,3)'="..":$TR($E(d,2,13)," ",""),1:"")
FILE(%X,M) ;M=1 - РАЗМЕР ФАЙЛА
         ;M=2 - ДАТА
         ;M=3 - ВРЕМЯ
 I $ZV["Cach"||($ZV["IRIS") Q $$FileAtr^%ZAPM.ed.BSCEXE(%X,M)
 N %ER,%J,%I,%A,%B,%C,%D,%Y,%K
 S %ER=$$ZOS12(%X,32+16+4+1) I %ER<0 Q ""
 S %Y=%ER,%I=$P(%Y,"^"),%J=$P(%Y,"^",2,999),%K=$A(%J,22)
 I $ZB(%K,16,1) Q:$E(%I)="." ""  I M=1 Q "<DIR>"
 E  S %A=$A(%J,27),%B=$A(%J,28),%C=$A(%J,29),%D=$A(%J,30) I M=1 Q (%A+(%B*256)+(%C*256*256)+(%D*256*256*256))
 S %A=$A(%J,25),%B=$A(%J,26),%C=256*%B+%A,%D=%C#32,%C=%C\32,%E=%C#16,%C=%C\16,%C=%C+1980
 I M=2 Q %E_"-"_$E("00",1,2-$L(%D))_%D_"-"_%C
 S %A=$A(%J,23),%B=$A(%J,24),%C=%B*256+%A,%D=%C#32*2,%C=%C\32,%E=%C#64,%C=%C\64,%A=%C\12,%B=%C#12 S:%B=0 %B=12
 Q %B_":"_$E("00",1,2-$L(%E))_%E_$S(%A:"p",1:"a") ;ВРЕМЯ
