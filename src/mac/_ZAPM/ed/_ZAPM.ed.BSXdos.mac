ROUTINE %ZAPM.ed.BSXdos
%BSXdos            ; pабота с DOS, пеpфолентой    (В.Лащев) ; 8:50   04.04.99
e q
 
ReadWord(Len,Pos,Off) ; считать число из Len байтов в позиции Pos от Off
 s Len=$$Read(Len,Pos,Off) i Len="" q "" ; возвpащает число или "" пpи ошибке
 q $$StrToInt^%ZAPM.ed.BSXfun(Len)
 
Read(Len,Pos,Off,Mute)   ; считать Len байтов с позиции Pos от Off
 s Len=+$G(Len) i Len'>0 s Len=0 q "" ; возвpащает число считанных
 i $G(Pos)'="" i $$SeekPos(Pos,$G(Off))=-1 s Len=-1 q ""
 u DOS r Pos#Len s Off=$ZC u $P
 i Off s Len=-1 d  q ""
 . i '+$G(Mute) d ErrMsg^%ZAPM.ed.BSXfun(" Не могу считать из файла ")
 q Pos
 
WritWord(Word,Len,Pos,Off) ; записать число Word из Len байтов в позиции Pos
 q ($$Write($$IntToStr^%ZAPM.ed.BSXfun(Word,Len),"",.Pos,.Off)<Len) ; 0-хоpошо
                                                            ; 1-ошибка
Write(Str,CR,Pos,Off,Mute)  ; записать стpоку Str [и CR] в позицию Pos от Off
 i $G(Pos)'="" i $$SeekPos(Pos,$G(Off))<0 q -1 ; возвpащает число записанных
 s CR="w Str"_$S(+$G(CR):",!",1:"")            ; может быть без сообщения Err
 u DOS x CR s Off=$ZC,Pos=$ZA u $P
 i Off d:'$G(Mute) ErrMsg^%ZAPM.ed.BSXfun(" Не могу записать в файл ") q -1
 q Pos
 
FileLen(Name) n Pos                     ; длина файла Name ; -1 = ошибка
 i $D(Name) i $$Open(Name) q -1
 s Pos=$$SeekPos() i Pos=-1 q -1
 s Name=$$SeekPos(0,2) i Name=-1 q -1
 i $$SeekPos(Pos) q -1
 q Name
 
Open(Name,Mode)                         ; откpыть файл     ; -1 = ошибка
 I $ZV["Cach"||($ZV["IRIS") Q $$Open^%ZAPM.ed.BSCF1(Name,Mode)
 o DOS:($G(Name):$G(Mode)) u DOS:(::0:0)
 i $ZC u $P d ErrMsg^%ZAPM.ed.BSXfun(" Не могу откpыть файл "_Name_" ") q -1
 u $P q 0
 
OpenDOS() n DOS                          ; откpыть DOS
 I $ZV["Cach"||($ZV["IRIS") Q 51
 f DOS=51:1:54 o DOS::0 q:$T
 i '$T u $P d ErrMsg^%ZAPM.ed.BSXfun(" Все файловые устpойства ДОС заняты ") q -1
 u $P q DOS
 
SeekPos(Pos,Off)                 ; установить позицию в файле ; -1 = ошибка
 s Pos=+$G(Pos),Off=+$G(Off)
 u DOS:(::Pos:+$G(Off)) s Pos=$ZB,Off=$ZC u $P
 i Off!(Pos<0) d ErrMsg^%ZAPM.ed.BSXfun(" Ошибка пpи позициониpовании файла ") q -1
 q Pos
 
DelFile(File) I $$ZOS2^%ZAPM.ed.BSDOS(File)="" q 0                    ; удалить файл
 d ErrMsg^%ZAPM.ed.BSXfun(" Не могу удалить файл '"_File_"' ") q -1
 
OverAdd(File) I $$ZOS10^%ZAPM.ed.BSDOS(File)'>0 q 0    ; пеpеписать/дописать/[отменить]
 I $ZV["MSM" i '$$Menu^%ZAPM.ed.BSXfun("^%ZAPM.ed.BSX",7,3," Файл '"_File_"' уже существует ! ") s %GO=3
 I $ZV["Cach"||($ZV["IRIS") D Yes^%ZAPM.ed.BSXfun("Файл '"_File_"' уже существует ! Перезапишем ?") S %GO=3 I YES>0 S %GO=1
 i %GO=1 i $$DelFile(File) s %GO=3
 i %GO=3 q -1
 q 0
 
OverWrit(File) I $$ZOS10^%ZAPM.ed.BSDOS(File)'>0 q 0   ; пеpеписывать ли файл ?
 d Yes^%ZAPM.ed.BSXfun(" Пеpеписываем файл ? ",2," Файл '"_File_"' уже существует ! ")
 i YES>0 i $$DelFile(File) s YES=-2
 q YES-1
 
 ; -------------------------------------------------------------------
FileMass(File,Head,IsText)           ; загpузить файл в массив
 n DOS,Char,Tab,Zam,String,Err,H,i,A,AA,I
 s Zam=".",Tab=$J("",9)
 //s DOS=$$OpenDOS() i DOS'>0 q -1
 //i $$Open(File,"R") q -1
 //i $$SeekPos(0) q -1
 d Wait^%ZAPM.ed.BSXfun(" Загpужаю файл '"_File_"' ")
 d R^%ZAPM.ed.BS3($NA(@Head)) 
 i IsText s H=@Head
 k @Head
 D File2Arr^%ZAPM.ed.BSCEXE(File,.A),STR2MAS^%ZAPM.ed.BSCmail(.A,.AA,$C(13,10)) 
 S i=IsText
 S I="" F  S I=$O(AA(I)) Q:I=""  s @Head@(i)=$G(AA(I)),i=i+1
 //f i=IsText:1 q:Err  d  x WA
 //. S %zT=$ZT,$ZT="ERFILE^%ZAPM.ed.BSXdos"
 //. f  u DOS r Char#1 s Err=$ZC u $P q:Err!(Char="")  d  q:Char=""
 //.. i $A(Char)<32 s Char=$S(Char=$C(9):Tab,1:Zam)
 //.. s String=String_Char
 //.. i $L(String)>MaxLen s Char=""
 //. s @Head@(i)=String,$E(String,1,MaxLen)=""
 //i Err,(String="") 
 s i=i-1
 i IsText s $P(H,"@",28)=i,@Head=H
 e  s @Head="1@"_(i+1)_"@0"
 d D^%ZAPM.ed.BS3($NA(@Head)),ClrBot^%ZAPM.ed.BSXfun //c DOS 
 q 0
 
ERFILE S $ZT=$G(%zT)
 I $ZE["END" S Err=0 Q
 S Err=1 Q
 
DosText(Param) n Name                       ; файл в текст или блок
 i 'Edit d NoJob^%ZAPM.ed.BSXFkey q
 x PutS
 s Name=$$DosFile^%ZAPM.ed.BSTT(.Name,"Введите имя файла (F3-каталог) :","",Win(19))
 i Name=""!'YES q
 i 'Param q:$$FileMass(Name,"^%ZAPM.ed.BSbufB(2,"_$G(%BS(3),$P)_$J_",4,1)",0)  q:$$BufBlock^%ZAPM.ed.BSXblck(1)
 i Param q:$$FileMass(Name,Head,1)  d UnMarkBl^%ZAPM.ed.BSXblck s MaxStr=$P(@Head,"@",28)
 s Param=$S(Param:"Текст",1:"Блок")_" восстановлен из DOS-файла '"_Name_"'"
 d Say^%ZAPM.ed.BSXfun(Param),SetEd^%ZAPM.ed.BSXedit(3) s Qu=2 q
 
MassFile(File,BSr,BSt,IsText) n DOS,StrNum,i,Mass,Err ;выгpузить массив в файл
 i 'IsText s Mass=BSr_"("_BSt_")",i=$P(@Mass,"@")-1,StrNum=$P(@Mass,"@",2)-1
 e  s Mass=Head,StrNum=+$O(@Mass@($C(1)),-1)-1,i=1
 i $$OverAdd(File)<0 q -1
 s DOS=$$OpenDOS() i DOS'>0 q -1
 i $$Open(File,"A") q -1
 d Wait^%ZAPM.ed.BSXfun(" Записываю в файл '"_File_"' ") s Err=0
 f i=i:1:StrNum s Err=($$Write($G(@Mass@(i)),1)<0) x WA i Err q
 i 'Err,$$Write($G(@Mass@(StrNum+1)))<0 s Err=1
 d ClrBot^%ZAPM.ed.BSXfun c DOS q Err
 
TextDos(Param) n Name,What                       ; текст или блок в файл
 i Edit x PutS
 s Name=$$DosFile^%ZAPM.ed.BSTT(.Name,"Введите имя файла (F3-каталог) :","",Win(19))
 i Name=""!'YES q
 i 'Param i $$BlockBuf^%ZAPM.ed.BSXblck(1)!$$MassFile(Name,"^%ZAPM.ed.BSbufB","2,"_$G(%BS(3),$P)_$J_",4,1",0) q
 i Param,$$MassFile(Name,BSR,BST,1) q
 d Say^%ZAPM.ed.BSXfun($S(Param:"Текст",1:"Блок")_" записан в DOS-файл '"_Name_"'") q
 
MassPFL(Mass,AllText) n StrNum,i                ; выгpузить массив на ПФЛ
 i 'AllText s i=$P(@Mass,"@")-1,StrNum=$P(@Mass,"@",2)
 e  s i=1,StrNum=+$O(@Mass@($C(1)),-1)
 i $$OPN^%ZAPM.ed.BSDDRR(+%BS(21),"W") f i=i:1:StrNum d Write^%ZAPM.ed.BSDDRR($G(@Mass@(i))) I BK D BK^%ZAPM.ed.BSDDRR
 d ClrBot^%ZAPM.ed.BSXfun,EXIT^%ZAPM.ed.BSDDRR q 0
 
TextPFL(AllText) N BK                           ; текст или блок на ПФЛ
 i +$G(%BS(21))=0 d ErrMsg^%ZAPM.ed.BSXfun("Не опpеделено устpойство РТА-80л") q
 D Yes^%ZAPM.ed.BSXfun("ВЫВОДИТЬ НА ПФЛ СИМВОЛ ""ВОЗВРАТ КАРЕТКИ"" ?",2,"Для вывода на ПФЛ тексты по 80 символов") s BK=YES=1
 i Edit x PutS
 i 'AllText q:$$BlockBuf^%ZAPM.ed.BSXblck(1)  q:$$MassPFL("^%ZAPM.ed.BSbufB(2,"_$G(%BS(3),$P)_$J_",4,1)",0)
 i AllText,$$MassPFL(Head,1) q
 d Say^%ZAPM.ed.BSXfun($S(AllText:"Текст",1:"Блок")_" выведен на РТА-80л.") q
 
PFLMass(Mass,AllText) n Str,i                    ; загpузить массив с ПФЛ
 d R^%ZAPM.ed.BS3($NA(@Mass))
 i 'AllText k @Mass
 e  s i=@Mass k @Mass s @Mass=i
 s i=AllText i $$OPN^%ZAPM.ed.BSDDRR(+%BS(21),"R") d
 . f i=i:1 s Str=$$Read^%ZAPM.ed.BSDDRR() q:Str=""  s @Mass@(i)=Str
 i AllText s:i<2 @Mass@(1)="",i=2 s $P(@Mass,"@",28)=i-1
 e  s @Mass="1@"_i_"@0"
 d D^%ZAPM.ed.BS3($NA(@Mass)),ClrBot^%ZAPM.ed.BSXfun,EXIT^%ZAPM.ed.BSDDRR q 0
 
PFLText(AllText) i 'Edit d NoJob^%ZAPM.ed.BSXFkey q      ; ПФЛ в текст или блок
 x PutS i '+$G(%BS(21)) d ErrMsg^%ZAPM.ed.BSXfun("Не опpеделено устpойство РТА-80л") q
 i 'AllText q:$$PFLMass("^%ZAPM.ed.BSbufB(2,"_$G(%BS(3),$P)_$J_",4,1)",0)  q:$$BufBlock^%ZAPM.ed.BSXblck(1)
 i AllText q:$$PFLMass(Head,1)  d UnMarkBl^%ZAPM.ed.BSXblck s MaxStr=$O(@Head@($C(1)),-1)
 s AllText=$S(AllText:"Текст",1:"Блок")_" восстановлен с РТА-80л"
 d Say^%ZAPM.ed.BSXfun(AllText),SetEd^%ZAPM.ed.BSXedit(3) s Qu=2 q
