ROUTINE %ZAPM.ed.BSXbar
%BSXbar            ; пpогpамма ввода в окне (В.Лащев) ; 17:10 10-JUN-98
e q
 
BigEdit(Input,y1,x1,y2,x2,BadChar,Clr,Help,AltCmp) 
 i %BS(13)'="%BS-PC" q $$LineEdit^%ZAPM.ed.BSXfun(Input,"Введите стpоку :",.BadChar,"",.Help,.AltCmp)
 d SaveWin^%ZAPM.ed.BSXfun(y1-1,x1-1,y2+2,x2+3)
 d Ramka^%ZAPM.ed.BSXfun(y1-1,x1-1,y2+1,x2+1,1,$G(Clr,"1;33;40"),1)
 s Input=$$Edit(Input,y1,x1,y2,x2,.BadChar,.Clr,.Help,.AltCmp)
 d LoadWin^%ZAPM.ed.BSXfun q Input
 
Edit(Input,y1,x1,y2,x2,BadChar,Clr,Help,AltCmp) 
 n LenW,HiW,Flag,cLen,MaxLen,Pos,Off,Max,Tmp,sp,Ins,Undo,i
 d:'$D(%BS) CLr^%ZAPM.ed.BSF4 U $I::%BS(32)
 s Off=1,MaxLen=($$LNG^%ZAPM.ed.BSF12-1),BadChar=$G(BadChar),AltCmp=$G(AltCmp)
 s Clr=$G(Clr,"1;32;40"),LenW=x2-x1+1,HiW=y2-y1+1,Max=(LenW*HiW)
 s cLen=$L(Input),sp=$TR($J("",Max)," ","·"),Undo=Input,Pos=cLen+(cLen<Max)
 s Flag=1 f  d Check,ReDraw:Flag,Read i Flag<0 q
 q $E(Input,cLen>0,cLen)
 
ReDraw w $$atr^%ZAPM.ed.BS3(Clr),$$atr^%ZAPM.ed.BS3(6) s y2=1,x2=LenW,Tmp=$E(Input,Off,cLen)_sp
 f i=0:1:HiW-1 w /CUP(y1+i,x1),$E(Tmp,y2,x2) s y2=y2+LenW,x2=x2+LenW
 q
Read s Ins=$E($$GetLock^%ZAPM.ed.BSXfun()),Flag=Pos-Off,i=$E(Input,Pos)
 s y2=y1+(Flag\LenW),x2=x1+(Flag#LenW),Flag=0
 w /CUP(y2,x2),$$atr^%ZAPM.ed.BS3(Clr),$$atr^%ZAPM.ed.BS3(5+Ins),i,/CUP(y2,x2)
 x $$Read^%ZAPM.ed.BSXfun("^%ZAPM.ed.BSX(""keyB"")","","d ABC",AltCmp) i Flag q
 w $$atr^%ZAPM.ed.BS3(Clr),$$atr^%ZAPM.ed.BS3(6),i q
 
Check i Pos>MaxLen s Pos=MaxLen
 i Off>1 i Pos<(Off+Max-1) s Off=Pos-Max+1,Flag=1 i Off<1 s Off=1
 i Pos<Off s Off=Pos,Flag=1 i Off<1 s (Off,Pos)=1
 i Pos'<(Off+Max) s Off=Pos-Max+1,Flag=1 i Off>(MaxLen-Max+1) s Pos=MaxLen,Off=MaxLen-Max+1
 q
 
ABC i BadChar[$C(R1) w $$bel^%ZAPM.ed.BS3 q                   ; Ввод символа
 i Pos>(cLen+1) i BadChar[" " w $$bel^%ZAPM.ed.BS3 q
 i cLen'<MaxLen w $$bel^%ZAPM.ed.BS3 s $E(Input,MaxLen,cLen)="",cLen=MaxLen-1
 s Flag=1 i 'Ins g Zam
 i Pos>cLen s $E(Input,Pos)=$C(R1),cLen=Pos,Pos=Pos+1 q
 s $E(Input,Pos)=$C(R1)_$E(Input,Pos),cLen=cLen+1,Pos=Pos+1 q
Zam s $E(Input,Pos)=$C(R1),Pos=Pos+1 i Pos>cLen s cLen=Pos q
 q
 
Bksp i Pos<2 q
 s Pos=Pos-1 i 'Ins q:(Pos>cLen)!(BadChar[" ")  s $E(Input,Pos)=" ",Flag=1 q
Del q:(Pos>cLen)  s $E(Input,Pos)="",cLen=cLen-1,Flag=1 q
 
AltT s Tmp=$$History^%ZAPM.ed.BSXfun("^%ZAPM.ed.BSbufB","HISCTL"_$S('$E(%BS(1),12):$G(%BS(3),$P)_$G(%BS(12)),$E(%BS(1),12)=1:"1"_$G(%BS(12)),1:"1")) i 'YES q
 g InsToEL
ReadToEL i $G(^%ZAPM.ed.BSbufB("HISCTL"_$S('$E(%BS(1),12):$G(%BS(3),$P)_$G(%BS(12)),$E(%BS(1),12)=1:"1"_$G(%BS(12)),1:"1")))="" d  q
 . d ErrMsg^%ZAPM.ed.BSXfun("Нет запомненой стpоки.")
 s Tmp=$G(^%ZAPM.ed.BSbufB("HISCTL"_$S('$E(%BS(1),12):$G(%BS(3),$P)_$G(%BS(12)),$E(%BS(1),12)=1:"1"_$G(%BS(12)),1:"1"),1))
InsToEL i Ins s $E(Input,Pos)=Tmp_$E(Input,Pos)
 e  i Tmp'="" s $E(Input,Pos,Pos+$L(Tmp)-1)=Tmp
 s Input=$E($TR(Input,BadChar,""),1,MaxLen),cLen=$L(Input),Flag=1 q
 
Help i $G(Help)'="" d EnterTAB^%ZAPM.ed.BSXfun(Help) q  ; F1 помощь
 d ^%ZAPM.ed.BSX("TEXT","^%ZAPM.ed.BSX","HelpBar") q
