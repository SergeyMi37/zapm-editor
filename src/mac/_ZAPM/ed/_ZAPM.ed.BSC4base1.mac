ROUTINE %ZAPM.ed.BSC4base1
%BSC4base1 ; пpогpамма управления БД ; 15:21   10.09.2002
 ;
 ;    Copyright (C) 2002 Serge W. Mikhaylenko
 ;    All Rights Reserved
 ;
ROU ;ПРОГРАММЫ
 D BEG1^%ZAPM.ed.BSC4
 D pin^%ZAPM.ed.BSC4base
 I $G(BSPRO)="" W $$LNG^%ZAPM.ed.BSC4("Сначало определите ПРОЕКТ",74) G PRO^%ZAPM.ed.BSC4base
 W "<form action="""_$$ADDLIB^%ZAPM.ed.BSC4()_""" method=""POST"">"
 S BSLABEL="ActRou^~BSC4base1"
 D ADDBSPOST^%ZAPM.ed.BSC4reg
 W $$SERVIS^%ZAPM.ed.BSC4base("ROU^~BSC4base1","CfgRou","NWO1")
 W "<table border=0 width=100%>"
 W "<tr><td width=10% align='right' class=s1 ><strong>"_$$LNG^%ZAPM.ed.BSC4("ПРОГРАММЫ",124)_"</strong></td><td width=60% class=s1 >"_$$LNG^%ZAPM.ed.BSC4("текущего проекта ",76)_RED_BSPRO_EF_"</td><td class=s1 >"_$$LNG^%ZAPM.ed.BSC4("Групповое действие над всеми",77)_"</td></tr>"
 N I,N,BD,Sort
 S Sort=+$$GETOPT^%ZAPM.ed.BSC4cfg("^"_pi_"(""CfgRou"")","Sort1") I 'Sort S Sort=-1
 W "<tr><td class=s3 >"_$$LNG^%ZAPM.ed.BSC4("ИМЯ",51)_"</td><td class=s3 >"_$$LNG^%ZAPM.ed.BSC4("ОПИСАНИЕ ПРОГРАММНОГО МОДУЛЯ",125)_"</td><td class=s3 >" D ACT0^%ZAPM.ed.BSC4base W $$Che^%ZAPM.ed.BSC4base(0)_"<input type=""submit"" name='ActOK0' title='"_$$LNG^%ZAPM.ed.BSC4("Групповое действие",79)_"' value='&gt;&gt;'></td></tr>"
 S N="",I=0 I '$D(@("^"_pin)) W $$LNG^%ZAPM.ed.BSC4("Нет раздела ! Обратитесь к Администратору",80) Q
 W "<tr><td class=s1 ><input type=""text"" size=13 maxlength=13 name='Rou' value='"_$$NewRoutin("NewRoutin")_"'></td><td class=s1 > "
 D OR(),TLIST() W "</td><td class=s1 >"
 W "<input type=""submit"" name=""newOK"" value='&gt;&gt;"_$$LNG^%ZAPM.ed.BSC4("Создать программу",126)_"'></td></tr>",BK
 S Co=0
 F  S N=$O(@("^"_pin_".ROU")@(N),Sort) Q:N=""  D
 .S Co=Co+1
 .W "<input type=""hidden"" name='Rou"_Co_"' value="""_N_""">" s sta=$G(@("^"_pin_".ROU")@(N,"STA"))
 .W "<tr><td width=20% class=s1 ><a title='"_$s($e(sta,1,2)'="OK":$$LNG^%ZAPM.ed.BSC4("не компилирована",316),1:$$LNG^%ZAPM.ed.BSC4("компилирована")_$P(sta,"OK",2))_"'>"_$$RedStar^%ZAPM.ed.BSC4rou($e(sta,1,2)'="OK")_N_"</a>"
 .w "</td><td class=s1 >"
 .i $d(@("^"_pin_".ROU")@(N,"HTM"))
 .s Tmp=$ZR i sta["OK" W $$NEWIN("<IMG SRC='"_BSDOMAIN_"/img/ie.gif'>",Tmp,$$LNG^%ZAPM.ed.BSC4("загрузить предлагаемый HTML код в новом окне",139),,$S(sta["OK":" ",1:"disabled"))
 .w AF_$G(@("^"_pin_".ROU")@(N,"REM"))_EF_"</td><td class=s1 >"
 .W "<input type=""submit""  title='"_$$LNG^%ZAPM.ed.BSC4("Действие",127)_"' name='ActOK"_Co_"' value='&gt;&gt;'>" D ACT^%ZAPM.ed.BSC4base W $$Che^%ZAPM.ed.BSC4base(Co,N)_"</td></tr>",BK
 W "<input type=""hidden"" name='RouCount' value="""_Co_""">"
 W " </table>"
 W "</form>"
 D END^%ZAPM.ed.BSC4
 Q
NewRoutin(N) N NN ;НОВОЕ ИМЯ ПРОГРАММЫ
 F NN=1:1 I '$D(@("^"_pin_".ROU")@(N_NN)) Q
 Q N_NN
EditRou(Rou) ;РЕДАКТИРОВАТЬ ПРОГРАММУ
 D BEG1^%ZAPM.ed.BSC4
 W $$SERVIS^%ZAPM.ed.BSC4base("ROU^~BSC4base1","CfgRou")
 N Y,STA,ht,ht1,oFFe
 S STA=$G(@("^"_pin_".ROU")@(Rou,"STA"))
 S CheckCode=$P($G(@("^"_pin_".ROU")@(Rou,"FLG")),"@",1)
 S ProtCode=$P($G(@("^"_pin_".ROU")@(Rou,"FLG")),"@",2)
 S Answer=$G(@("^"_pin_".ROU")@(Rou,"ROU")) I Answer="" S Answer=$$LNG^%ZAPM.ed.BSC4("Контроль не производился",128)
 W "<form action="""_$$ADDLIB^%ZAPM.ed.BSC4()_""" method=""POST"">"
 S BSLABEL="SaveRou^~BSC4base1"
 D ADDBSPOST^%ZAPM.ed.BSC4reg
 W "<table border=0 width=100%>"
    W "<input type=""hidden"" name='Rou' value="""_Rou_""">"
 W "<tr><td align='CENTER' width=40% class=s1 >"_$$LNG^%ZAPM.ed.BSC4("РЕДАКТИРОВАНИЕ ПРОГРАММЫ",129)_"</td><td  width=60% class=s1 >"_RED_BSPRO_"."_EF_Rou_"</td></tr>",BK
 W "<tr><td width=30% class=s3 >"_$$LNG^%ZAPM.ed.BSC4("ОБЩИЕ ПАРАМЕТРЫ",96)_"</td><td  width=70% class=s3 >"_$$LNG^%ZAPM.ed.BSC4("ЗНАЧЕНИЯ",97)_"</td></tr>",BK
  W "<tr><td class=s1 >"_$$LNG^%ZAPM.ed.BSC4("Описание",98)_"</td><td class=s1 ><input type=""text"" size=45 maxlength=145 name='Desc' value='"_$G(@("^"_pin_".ROU")@(Rou,"REM"))_"'></td></tr>",BK
   W "<tr><td class=s1 > <input type=""checkbox"" name=""CheckCode"" value="""_CheckCode_""" "_$$ch^%ZAPM.ed.BSC4cfg(CheckCode)_">"_$$LNG^%ZAPM.ed.BSC4("Контроль кода Администратором",130)_"</td><td class=s1 ><input type=""text"" size=25 maxlength=45 name='Answer' value='"_Answer_"'>"
    W " <input type=""checkbox"" name=""ProtCode"" title='"_$$LNG^%ZAPM.ed.BSC4("Запретить ремастеринг во избежания уничтожения пользовательской редакции кода")_"' value="""_ProtCode_""" "_$$ch^%ZAPM.ed.BSC4cfg(ProtCode)_">"_$$LNG^%ZAPM.ed.BSC4("Запретить ремастеринг")_"</td></tr>",BK
     W "<tr><td class=s1 >"_$$LNG^%ZAPM.ed.BSC4("Статус программы",131)_" Cache'ObjectScript</td><td class=s1 ><input type=""text"" size=45 maxlength=145 name='StatRou' value='"_STA_"'></td></tr>",BK
 W "</table>"
 S oFFe=$$GETOPT^%ZAPM.ed.BSC4cfg(MainOpt,"WebEditRou")
 D BUTT
 s ht=$G(@("^"_pin_".ROU")@(Rou,"COS")),Tmp=$ZR
 W " ",$$NEWIN("<IMG SRC='"_BSDOMAIN_"/img/cos.gif'> COS",Tmp,$$LNG^%ZAPM.ed.BSC4("загрузить предлагаемый COS код в новом окне браузера",134),"NumADD^~BSC4r","BUT")
 s ht1=$G(@("^"_pin_".ROU")@(Rou,"HTM")),Tmp=$ZR
 W $$NEWIN("<IMG SRC='"_BSDOMAIN_"/img/ie"_$S(STA["OK":"",1:"off")_".gif'> HTML",Tmp,$$LNG^%ZAPM.ed.BSC4("загрузить предлагаемый HTML код в новом окне",139),,$S(STA["OK":" ",1:"disabled"))
 W "<table border=0 width=100%>"
 W "<tr><td width=* class=s1 ><IMG SRC='"_BSDOMAIN_"/img/cos.gif'>"_$$LNG^%ZAPM.ed.BSC4("Серверная часть (выполняет Cache' сервер) ",132)_$$HelpCache^%ZAPM.ed.BSC4_" "
 I $G(@BDSES@(BSSES,"TEXTARIA","COS"))="" w "<A HREF='"_$$GETH^%ZAPM.ed.BSC4r2("COS","B")_"'><IMG SRC='"_BSDOMAIN_"/img/b.gif' ALT='"_$$LNG^%ZAPM.ed.BSC4("увеличить окно редактирования",138)_"' border=0></A>"_BK
 I $G(@BDSES@(BSSES,"TEXTARIA","COS"))'="" w "<A HREF='"_$$GETH^%ZAPM.ed.BSC4r2("COS","L")_"'><IMG SRC='"_BSDOMAIN_"/img/l.gif' ALT='"_$$LNG^%ZAPM.ed.BSC4("уменьшить окно редактирования",133)_"' border=0></A>"_BK
 W $s(STA'["OK":RED_$$LNG^%ZAPM.ed.BSC4("необходима компиляция")_EF,1:"")_"</td></tr>",BK
 W "<tr><td class=s1 >"
 I oFFe W "<textarea "_$$GETTA^%ZAPM.ed.BSC4r2("COS")_" name='ServR'>"_$$TR^%ZAPM.ed.BSsFUNM($$TR^%ZAPM.ed.BSsFUNM(ht,"<","&lt;"),">","&gt;")_"</textarea>"
 i 'oFFe W "<CENTER>"_RED_$$LNG^%ZAPM.ed.BSC4("РЕДАКТОР ВЫКЛЮЧЕН")_EF_"</CENTER>"
 W "</td></tr>",BK
 W "<tr><td width=* class=s3 ><IMG SRC='"_BSDOMAIN_"/img/ie.gif'>"_$$LNG^%ZAPM.ed.BSC4("Клиентская часть (выполняется в WEB браузере) ",135)_$$HelpDhtml^%ZAPM.ed.BSC4_", "_$$HelpJS^%ZAPM.ed.BSC4_" "
 I $G(@BDSES@(BSSES,"TEXTARIA","HTM"))="" w "<A HREF='"_$$GETH^%ZAPM.ed.BSC4r2("HTM","B")_"'><IMG SRC='"_BSDOMAIN_"/img/b.gif' ALT='"_$$LNG^%ZAPM.ed.BSC4("увеличить окно редактирования",138)_"' border=0></A>"_BK
 I $G(@BDSES@(BSSES,"TEXTARIA","HTM"))'="" w "<A HREF='"_$$GETH^%ZAPM.ed.BSC4r2("HTM","L")_"'><IMG SRC='"_BSDOMAIN_"/img/l.gif' ALT='"_$$LNG^%ZAPM.ed.BSC4("уменьшить окно редактирования",133)_"' border=0></A>"_BK
 W "</td></tr>",BK
 W "<tr><td class=s3 >"_$s(STA'["OK":RED_$$LNG^%ZAPM.ed.BSC4("после компиляции",136)_EF,1:"")
 w $$LNG^%ZAPM.ed.BSC4(" возьмите в буфер обмена и вставте в ваш WEB сайт",137)
 W "</td></tr>",BK
 w "<tr><td class=s3 ><textarea "_$$GETTA^%ZAPM.ed.BSC4r2("HTM")_" name='ClienR'>"_$$TR^%ZAPM.ed.BSsFUNM($$TR^%ZAPM.ed.BSsFUNM(ht1,"<","&lt;"),">","&gt;")_"</textarea></td></tr>",BK
 W "</table>"
 W "</form>"
 D InfoSess^%ZAPM.ed.BSC4rout,END^%ZAPM.ed.BSC4
 Q
NEWIND I BSTMP["|" S BSTMP=$TR(BSTMP,"|",$C(34))
 w $g(@BSTMP," ") ;новое окно
NEWIN(t,ZR,tit,CodeM,BUT) ;новое окно
 n BSLABEL,BSLOGIN,BSPRO,BSRoutineId,BSSES,BSTMP,BSTOP,BSGRANT s BSLABEL=$g(CodeM,"NEWIND^~BSC4base1"),BSTMP=ZR
 S R=$$ADDBSGET^%ZAPM.ed.BSC4() I R[$C(34) S R=$TR(R,$C(34),"|")
 I $D(BUT) Q "<BUTTON "_BUT_" title='"_tit_"' onclick=""open('"_R_"','','toolbar=yes,menubar=no,scrollbars=yes,"_$$GETOPT^%ZAPM.ed.BSC4cfg(MainOpt,"NWT1")_",status=1,resizable=1');"">"_t_"</BUTTON>"
 Q "<a title='"_tit_"' class=u1 onclick=""open('"_R_"','Tmp"_$TR($H,",","")_"','toolbar=yes,menubar=no,scrollbars=yes,"_$$GETOPT^%ZAPM.ed.BSC4cfg(MainOpt,"NWT1")_",status=1,resizable=1');"">"_t_"</a>"
CheckRou(ServR) ;        трансляция и синтаксический контроль кода
 I ServR="" W RED_Rou_" "_$$LNG^%ZAPM.ed.BSC4("Программа пустая!",140)_EF_BBK Q 0
 I '$$Security^%ZAPM.ed.BSC4r(ServR) W RED_Rou_" "_$$LNG^%ZAPM.ed.BSC4("Контроль на безопасность не пройден!",141),EF," ",$$NEWIN($$LNG^%ZAPM.ed.BSC4("Запрещенные команды",142),"1",$$LNG^%ZAPM.ed.BSC4("загрузить запрещенный COS код в новом окне браузера",143),"Protect^~BSC4r"),BBK Q 0
 S ServR=" #include %BSC4inc"_BK_ServR
 s K=$L(ServR,BK)
 F I=1:1:K S ServR(I)=$P(ServR,BK,I) ;W ServR(I),BK
 S ServR="",ServR(0)=K,out=""
 D ROUTINE^%R(pin_"."_Rou_".MAC",.ServR,.err,"CS",0)
 I $G(err)'="" W "<PRE>",RED,$$FMTERR^%R(.err,.ServR),EF,"</PRE>",BK Q 0
 ;D ROUTINE^%R(pin_"."_Rou_".INT",.ServR,.err,"D",0)
 ;I $G(err)'="" W "<PRE>",RED,$$FMTERR^%R(.err,.ServR),EF,"</PRE>",BK Q 0
 ;D DEL1^%ZAPM.ed.BSC4r4(pin_"."_Rou_".MAC") ;,filedate,nsp)
 Q 1
CompRou ;КОМПИЛИРОВАТЬ ПРОГРАММУ
 N N1,R1,I,K,out,err
 s N1=$ZU(5),R1=$zcrc(N1_"^"_pin_"."_Rou,7)
 I '$$CheckRou(ServR) D  Q  ;ИЛИ СИНТАКСИС НЕ ПРОШЕЛ
 .S @("^"_pin_".ROU")@(Rou,"STA")="Error "_$$TD^%ZAPM.ed.BSC4base
 .D DEL1^%ZAPM.ed.BSC4r4(pin_"."_Rou_".*") K @BDREF@(R1)
 .S @("^"_pin_".ROU")@(Rou,"ROU")=" "
 ;контроль прошел
 S @BDREF@(R1,"Name")=pin_"."_Rou,@BDREF@(R1,"Namespace")=N1 ;,@BDREF@(R1,"pi")=pi
 I $G(ClienR)'="" S @("^"_pin_".ROU")@(Rou,"HTM")=$$TR^%ZAPM.ed.BSsFUNM(ClienR,"????????",R1)
 ;I $G(ServR)'="" S @("^"_pin_".ROU")@(Rou,"COS")=$$TR^%ZAPM.ed.BSsFUNM(ServR,"????????",R1) !!!!!! НЕЛЬЗЯ
 S @("^"_pin_".ROU")@(Rou,"STA")="OK "_$$TD^%ZAPM.ed.BSC4base,@("^"_pin_".ROU")@(Rou,"ROU")=" BSRoutineId="_R1
 Q
ReMST(newTP,EnablT) K @("^"_pin_".ROU")@(Rou)
 I newTP["^" D @(newTP_"(pin,Rou,EnablT)") I 1
 E  D @(newTP_"^%ZAPM.ed.BSC4rou(pin,Rou,EnablT)")
 D EditRou(Rou) Q
BUTT W "<nobr><input type=""submit"" name=""SaveRou"" "_$S('$G(oFFe):"disabled",1:"")_" title='"_$$LNG^%ZAPM.ed.BSC4("Только сохранить изменения",108)_"' value='"_$$LNG^%ZAPM.ed.BSC4("Сохранить",109)_"'>"
 w "<input type=""submit"" name=""CompRou"" "_$S('$G(oFFe):"disabled",1:"")_" title='"_$$LNG^%ZAPM.ed.BSC4("Компилировать серверную часть модуля",144)_"' value='"_$$LNG^%ZAPM.ed.BSC4("Компилировать",111)_"'>"
 w "<input type='RESET' "_$S('$G(oFFe):"disabled",1:"")_" title='"_$$LNG^%ZAPM.ed.BSC4("Отменить текущие изменения",112)_"' name='Reset1' value='"_$$LNG^%ZAPM.ed.BSC4("Отменить",73)_"'>"
 N MST S MST=$G(@("^"_pin_".ROU")@(Rou,"MST"))
 I MST'="" w "<input type=""submit"" name=""ReMST"" "_$S('$G(oFFe):"disabled",1:"")_" "_$S($G(ProtCode):"disabled",1:"")_" title='"_$$LNG^%ZAPM.ed.BSC4("Создать заново программу, используя Мастер `",145)_$P($P(MST,","),"^")_$$LNG^%ZAPM.ed.BSC4("` для таблицы `",146)_$P(MST,",",2)_"`' value='"_$$LNG^%ZAPM.ed.BSC4("РеМастеринг",147)_"'>"
 W "</nobr>" Q
SaveRou ;СОХРАНИТЬ ИЗМЕНЕНИЯ
 D pin^%ZAPM.ed.BSC4base
 I $D(ReMST) S ReMST=@("^"_pin_".ROU")@(Rou,"MST") D ReMST($P(ReMST,",",1),$P(ReMST,",",2)) Q
 I $G(Desc)'="" S @("^"_pin_".ROU")@(Rou,"REM")=Desc
 I $G(Suff)'="" S @("^"_pin_".ROU")@(Rou,"ROU")=Suff
 S $P(@("^"_pin_".ROU")@(Rou,"FLG"),"@",1)=$S($D(CheckCode):1,1:0)
 S $P(@("^"_pin_".ROU")@(Rou,"FLG"),"@",2)=$S($D(ProtCode):1,1:0)
 I $D(CheckCode),'$$TARIF^%ZAPM.ed.BSC4rout("TIP") W RED,$$LNG^%ZAPM.ed.BSC4("Контроль кода Адмигистратором",148)," ",$$LNG^%ZAPM.ed.BSC4("возможно только на старших тарифных планах",149),EF,BBK,BBK
 ;ZW %KEY("ServR"),BK
 I $G(ServR)'="" S @("^"_pin_".ROU")@(Rou,"COS")=ServR
 I $G(ClienR)'="" S @("^"_pin_".ROU")@(Rou,"HTM")=ClienR
 S @("^"_pin_".ROU")@(Rou,"STA")="Save "_$$TD^%ZAPM.ed.BSC4base
 ;S @("^"_pin_".ROU")@(Rou,"ROU")=" "
 I $D(CompRou) D CompRou
 D EditRou(Rou) Q
ActRou ;ДЕЙСТВИЯ НАД ПРООГРАММОЙ
 N I D pin^%ZAPM.ed.BSC4base
 I $D(newOK) G CreatRou
 I $D(ActOK0) D  G ROU ;ГРУППОВАЯ
 .I $D(Check0) F I=1:1:$G(RouCount) D Ac($G(Act0),@("Rou"_I)) ;ВСЕ
 .E  F I=1:1:$G(RouCount) I $D(@("Check"_I)) D Ac($G(Act0),@("Rou"_I))
 F I=1:1:$G(RouCount) I $D(@("ActOK"_I)) Q
 I $D(@("Rou"_I)) D
 .I $G(@("Act"_I))=2 D KillRou(@("Rou"_I)) G ROU
 .I $G(@("Act"_I))=1 D EditRou(@("Rou"_I)) Q
 .I $G(@("Act"_I))=3 D R2B(@("Rou"_I)) G ROU
 Q
Ac(A,R) ;ГРУППОВОЕ ДЕЙСТВИЕ
 I A=1 S Rou=R,ServR=$G(@("^"_pin_".ROU")@(Rou,"COS")),ClienR=$G(@("^"_pin_".ROU")@(Rou,"HTM")) D CompRou Q
 I A=2 D KillRou(R) Q
 Q
ALLROU(PART) //КОМПИЛИРОВАТЬ ВСЕ ПРОГРАММЫ B4Y
 N Rou,PP,pin
 D MAINVAR^%ZAPM.ed.BSC4
 S pin=$P(PART,"^",2,99)
 S PP=$G(@PART@("ROU","KEY")) Q:PP=""
 S Rou="" F  S Rou=$O(@PP@(Rou)) Q:Rou=""  D Ac(1,Rou)
 Q
R2B(ROU,DEST) ;ПРОГРАММУ В БУФЕР ОБМЕНА
 I '$D(DEST) S DEST=$NA(@GTmp@(BSLOGIN,"ROU"))
 N ID K @DEST
 S ID=$P($G(@("^"_pin_".ROU")@(ROU,"ROU"))," BSRoutineId=",2)
 M @DEST=@("^"_pin_".ROU")@(ROU)
 I ID'="" D
 .S @DEST@("HTM")=$$TR^%ZAPM.ed.BSsFUNM(@("^"_pin_".ROU")@(ROU,"HTM"),ID,"????????")
 .S @DEST@("COS")=$$TR^%ZAPM.ed.BSsFUNM(@("^"_pin_".ROU")@(ROU,"COS"),ID,"????????")
 .K @DEST@("ROU"),@DEST@("STA")
 Q
KillRou(Rou) ;УДАЛИТЬ ПРОГРАММУ
 N ID
 D DEL1^%ZAPM.ed.BSC4r4(pin_"."_Rou_".*") S ID=$P($G(@("^"_pin_".ROU")@(Rou,"ROU"))," BSRoutineId=",2)
 K @("^"_pin_".ROU")@(Rou) I ID'="" K @BDREF@(ID)
 W $$LNG^%ZAPM.ed.BSC4("ПРОГРАММА",150)," '",RED,Rou,EF,"' ",$$LNG^%ZAPM.ed.BSC4("УДАЛЕНА",117),BBK,BBK
 Q
CreatRou ;СОЗДАТЬ ПРОГРАММУ
 I $D(@("^"_pin_".ROU")@(Rou)) W $$LNG^%ZAPM.ed.BSC4("Программа с именем",151)," '",RED,Rou,EF,"' ",$$LNG^%ZAPM.ed.BSC4("уже существует",61),BK G ROU
 I newTP[$P(GTmp,")") D Copy^%ZAPM.ed.BSF8(newTP,$NA(@("^"_pin_".ROU")@(Rou))) G ROU
 I newTP="" S @("^"_pin_".ROU")@(Rou,"REM")="" I 1
 E  D
 .I newTP["*" W $$LNG^%ZAPM.ed.BSC4("Мастер создания программ '",152)_$E(newTP,2,99)_$$LNG^%ZAPM.ed.BSC4("' доступен только на старших тарифных планах",153) I 1
 .E  D @(newTP_"^%ZAPM.ed.BSC4rou(pin,Rou,EnablT)")
 G ROU
OR() N B,I,P,T ;ВСЕ ПРОГРАММЫ ДОСТУПНЫЕ КАК ШАБЛОН
 W "<select name=""newTP"" size=1>"
 I $D(@GTmp@(BSLOGIN,"ROU")) W "<option value='"_$ZR_"'>"_$$LNG^%ZAPM.ed.BSC4("Копировать из буфера",56)_"</option>"
 ;S P="^%ZAPM.ed.BSC4E",T=""
 ;S B=$$KBD^%ZAPM.ed.BSF12($NA(@P@("ROU")))
 W "<option value=''>"_$$LNG^%ZAPM.ed.BSC4("применить мастер..",154)_"</option>"
 F T="RunCode","InputForm","Populate","DynamicSQL","Find","Navigator"  W "<option value='"_T_"'>"_T_"</option>"
 W "</select>"
 Q
TLIST() N N ;ДОСТУПНЫЕ ТАБЛИЦЫ
 S N=""
 W "<select name=""EnablT"" size=1>"
 W "<option " W " value=''>"_$$LNG^%ZAPM.ed.BSC4("к таблице..",155)_"</option>"
 F  S N=$O(@("^"_pin)@(N)) Q:N=""  I $P($G(@("^"_pin)@(N)),"@",17)=2 D
 .I N="ROU" Q
 .I $G(@("^"_pin)@(N,"Compile"))'["OK" q  ;не компилирована
 .W "<option " W " value='"_N_"'>"_N_"</option>"
 W "</select>"
 Q
