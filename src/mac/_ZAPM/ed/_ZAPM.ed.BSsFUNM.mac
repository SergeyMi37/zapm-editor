ROUTINE %ZAPM.ed.BSsFUNM
%BSsFUNM ;ПОЛЬЗОВАТЕЛЬСКИЕ ФУНКЦИИ ; 16:36   16.10.2000
SELECT(%1,%2,%3,%4) ;ГЛОБАЛЬНЫЙ $SELECT
 ;%1-НОМЕР ПЕРЕМЕННОЙ
 ;%2-ИМЯ ТАБЛИЦЫ,РАЗДЕЛА ОПИСАНИЯ БАЗЫ ЛОГ. УСЛОВИЙ (DFLT="LOGIC,%BSZ")
 ;%3-ИМЯ УЗЛА В БАЗЕ ЛОГ. УСЛОВИЙ
 ;%4-ВЫЗОВ ТАБЛИЦЫ\ТЕКСТА ПО <F1>
 N TEST,ZrE,Dt,li,iX
 S ZrE=$ZR,Dt=$T ;SAVETEST
SELE I $D(cHOSe("S",%1)) S $ZT="ERRSEL^%ZAPM.ed.BSsFUNM" D  Q $G(li)
 .K li S iX="" F  S iX=$O(cHOSe("S",%1,iX)) Q:iX=""  X "I "_cHOSe("S",%1,iX,"LOG")_" S li=cHOSe(""S"",%1,iX,""SEL"")" I  Q
 K cHOSe("S",%1) I $G(%3)="" D ErrMsg^%ZAPM.ed.BSXfun("ОШИБКА 3 УСЛОВИЯ В ФУНКЦИИ $$SELECT !") Q 0
 I $G(%2)="" S %2="LOGIC,%BSZ"
 S li=$$RIT(%2),li=$$KBD^%ZAPM.ed.BSF12(li) I li="" D ErrMsg^%ZAPM.ed.BSXfun("ОШИБКА В ИМЕНИ БАЗЫ УСЛОВИЙ $$SELECT !") Q 0
 S iX="" F  S iX=$O(@li@(%3,iX)) Q:iX=""  S cHOSe("S",%1,iX,"LOG")=$G(@li@(%3,iX,"L1")),cHOSe("S",%1,iX,"SEL")=$G(@li@(%3,iX,"S1"))
 I $D(cHOSe("S",%1)) G SELE
 Q 0
ERRSEL D ErrMsg^%ZAPM.ed.BSXfun($ZE_" "_$$ErrSay^%ZAPM.ed.BSF8($ZE)_" ---->  "_$G(cHOSe("S",%1,iX))) S $ZT=$G(%zT)
 Q 0
LOGIC(%1,%2,%3,%4,%5) ;БАЗА ЛОГ. УСЛОВИЙ ДЛЯ ФУНКЦИИ
 ;%1-НОМЕР ПЕРЕМЕННОЙ
 ;%2-ИМЯ ТАБЛИЦЫ,РАЗДЕЛА ОПИСАНИЯ БАЗЫ ЛОГ. УСЛОВИЙ (DFLT="LOGIC,%BSZ")
 ;%3-ИМЯ УЗЛА В БАЗЕ ЛОГ. УСЛОВИЙ
 ;%4-ВЫЗОВ ТАБЛИЦЫ\ТЕКСТА ПО <F1>
 ;%5-КОМАНДА НА ТРАНСЛЯЦИЮ ФОРМУЛ
 N TEST,ZrE,Dt,li
 S ZrE=$ZR,Dt=$T ;SAVETEST
 I $G(cHOSe("L",%1))="NOTE" X "I 0" Q 0
 I $G(cHOSe("L",%1))="ALWAY" X "I 1" Q 1
LOGI I $D(cHOSe("L",%1)) S $ZT="ERLOGIC^%ZAPM.ed.BSsFUNM" X "I "_cHOSe("L",%1) Q $T
 K cHOSe("L",%1) I $G(%3)="" D ErrMsg^%ZAPM.ed.BSXfun("ОШИБКА 3 УСЛОВИЯ В ФУНКЦИИ $$LOGIC !") S cHOSe("L",%1)="NOTE" Q 0
 I $G(%2)="" S %2="LOGIC,%BSZ"
 S li=$$RIT(%2),li=$$KBD^%ZAPM.ed.BSF12(li) I li="" D ErrMsg^%ZAPM.ed.BSXfun("ОШИБКА В ИМЕНИ БАЗЫ УСЛОВИЙ $$LOGIC !") S cHOSe("L",%1)="NOTE" Q 0
 N BSr,Bst I $G(%4)'="" S %4=$$RIT(%4)
 D NEWTAB($NA(@li@(%3)),$G(%4,"^%ZAPM.ed.BSZ(""LOGICHLP"")"),%1,$G(%5))
 G LOGI
LOGTXT(NUMBE) ;ВЫБРАННЫЙ ТЕКСТ ИЗ БАЗЫ ЛОГ. УСЛОВИЙ
 Q $G(cHOSe("L",NUMBE,"TXT"))
LOGSEL(NUMBE) ;ВЫБРАННЫЙ КОД ИЗ БАЗЫ ЛОГ. УСЛОВИЙ
 Q $G(cHOSe("L",NUMBE,"SEL"))
ERLOGIC D ErrMsg^%ZAPM.ed.BSXfun($ZE_" "_$$ErrSay^%ZAPM.ed.BSF8($ZE)_" ---->  "_$G(cHOSe("L",%1))) S $ZT=$G(%zT) S cHOSe("L",%1)="NOTE" Q 0
 Q
NEWTAB(B,H,NUMBE,TR) ;ФОРМИРОВАНИЕ ВРЕМЕННОЙ ТАБЛИЦЫ
 N BSR,BST,T,BSr,BSt,t1,t2,l1,l2,col,v,se,ke,I,tIT,Fk,PrEd,d,d1,d0,%1,%2,%3,%4,YES
 S T=$$TMPG^%ZAPM.ed.BSF11 N k1,k2,k3,k4,k5,k6,k7,k8,k9,L,C,D,BUF
 S BSr=$P(T,"("),BSt=$P($P($P(T,"(",2),")"),$C(34),2),l1=25,l2=50,v=2,ke=3,l3=3
 S (t3,t1)="",t2="ВЫБЕРИТЕ ФОРМУЛУ, или F1",(col(1),col(2))=$P(^%ZAPM.ed.BSZ("LOGIC",1,1),"@",10) D CR^%ZAPM.ed.BSN
 S col(1)=$P(^%ZAPM.ed.BSZ("LOGIC",2,1),"@",10),col(2)=$P(^%ZAPM.ed.BSZ("LOGIC",2,2),"@",10)
 S I="" F  S I=$O(@B@(I)) Q:I=""  S t1=" "_$G(@B@(I,"C1")),t2=$G(@B@(I,"L1")),t3=$G(@B@(I,"S1")) X TR D CR^%ZAPM.ed.BSN
 S tIT="2,2",Fk="" D CREAT^%ZAPM.ed.BSN S $P(@BSr@(BSt),"@",39)=H
NEWTA D Table^%ZAPM.ed.BSN(BSR,BST) I $P(YES,"@",4)=27 D Yes^%ZAPM.ed.BSXfun("УСЛОВИЕ ДЛЯ ЭТОГО НАСЧЕТА БУДЕТ ВСЕГДА ДА='ИСТИНО' НЕТ='ЛОЖЬ' ?") S cHOSe("L",NUMBE)=YES>0 G NEWE
 I $P($P(YES,"@",1),",",2)'=2 D ErrMsg^%ZAPM.ed.BSXfun("ВЫ НАХОДИТЕСЬ НЕ НА ФОРМУЛАХ !") G NEWTA
 I $P(YES,"@",9)["|" S cHOSe("L",NUMBE)=$P($P(YES,"@",9),"|",2),cHOSe("L",NUMBE,"TXT")=$P($P(YES,"@",9),"|"),cHOSe("L",NUMBE,"SEL")=$P($P(YES,"@",9),"|",3) G NEWE
 S cHOSe("L",NUMBE)=$P(YES,"@",9)
 S cHOSe("L",NUMBE,"TXT")=$P($G(@BSR@(BST,$P($P(YES,"@",1),","),$P($P(YES,"@",1),",",2)-1)),"@",15)
 S cHOSe("L",NUMBE,"SEL")=$P($G(@BSR@(BST,$P($P(YES,"@",1),","),$P($P(YES,"@",1),",",2)-1)),"@",15)
NEWE K @T Q
CHO(%1,%2,%3,%4,%5,%6,%7,%8) ;ФУНКЦИЯ ОПРЕДЕЛЕНИЯ ПЕРЕМЕННОЙ НА СЕАНС РАБОТЫ С ТАБЛИЦЕ
 ;%1-НОМЕР ПЕРЕМЕННОЙ
 ;%2-ТЕКСТ ПРИГЛАШЕНИЯ ДЛЯ ВВОДА
 ;%3=ДАННЫЕ ВВОДА ПРЕДЛАГАЕМЫЕ
 ;%4-ФОРМУЛА ТРАНСФОРМАЦИИ РЕЗУЛЬТАТА ВВОДА ДЛЯ d=РЕЗУЛЬТАТ ВВОДА
 ;%5-ИМЯ ПЕРЕМЕННОЙ, С КОТОРОЙ ПРЕДСТОИТ СРАВНИВАТЬ
 ;%6-ВЫЗОВ ТАБЛИЦЫ ПРИ ВВОДЕ <ENTER> ИЛИ "?" ССЫЛКА = "BST,BSR,UIC,SYS"
 ;%7-ВЫЗОВ ТАБЛИЦЫ\ТЕКСТА ПО <F1>            ССЫЛКА = "BST,BSR,UIC,SYS"
 ;%8-HISTORY
 N ZrE,Dt
 S ZrE=$ZR,Dt=$T ;SAVETEST
 I $G(cHOSe(%1))="ALL",$D(%5) X "S %0="_%5 Q %0
 I $D(cHOSe(%1)) S %0=cHOSe(%1) Q %0
 N lop,lon,ls,ll,%hlp,%HIS,%KAT ;ДЛЯ LE
 S cHOSe=$G(li) I $G(%7)'="" S %hlp=$$RIT(%7)
 S %HIS="^%ZAPM.ed.BSbufB(""HISCHOISE"_%1_""")"
CH U 0 W $$bel^%ZAPM.ed.BS3 S li=$G(%3),ls="Вводите "_$G(%2)_":" D LE^%ZAPM.ed.BSTT
 I YES<1 S (d,%0,li)="" G CHGO
 I li=""!(li="?"),$D(%6) S li=$$CALL(%6) S:li'="" %3=li G CH
 S d=li I $G(%4)'="" X "S d="_%4
 S %0=d I d="ALL",$D(%5) X "S %0="_%5
CHGO D RESTTEST
 S cHOSe($G(%1,9))=$G(d),li=$G(cHOSe)
 Q %0
RESTTEST I $D(@ZrE)
RESTT X "I Dt"
 Q
CALL(li) ;ВЫЗОВ ТАБЛИЦЫ
 N BSr,BSt,YES,%1,%2,%3,%4,%5,%6,%7,d,dat,date
 D RII^%ZAPM.ed.BSF3 D Table^%ZAPM.ed.BSN(BSr,BSt)
 Q:$P(YES,"@",4)=27 "" Q $P(YES,"@",9)
RIT(li) N BSr,BSt D RII^%ZAPM.ed.BSF3
 Q $NA(@BSr@(BSt))
DATpl(%1,%2,%3) ;$$h^%ZAPM.ed.BS3() +(-) время формата (ЧЧ:ММ)
 ;%1 - дата формата $$h^%ZAPM.ed.BS3(); %2 - время (ЧЧ:ММ); %3 - "-" (отнять), "" (прибавить)
 N i,i1,i2,i3
 I %1="" S %0="" Q %0
 I %2="" S %0=%1 Q %0
 S i=$P(%1,","),i1=$P(%1,",",2),i2=$P(%2,":")*3600+($P(%2,":",2)*60)
 S i2=i2*$S(%3="-":-1,1:1)
 S i3=(i1+i2)/86400
 S i3=i3+i
 S %0=$P(i3,".")_","_$E(($P(i3,".",2)*86400),1,5)
 Q %0
SelecBut(%1,%2,%3) ;CЕЛЕКТОРНЫЕ КНОПКИ
 ;%1- ТИП ПАНЕЛИ
 ;%2=1 ПЕРВОНАЧЕЛЬНАЯ УСТАНОВКА
 ;%3=ПЕРЕМЕННАЯ ВОЗВРАТА"
 N T
 I '$D(Sel(ny,nx)) S Sel(ny,nx)=$G(%3)
 I $G(%2),'$D(Sel(0)) S Sel(0)=(ny_","_nx)
 I $G(Sel(0),"?")=(ny_","_nx) S T=$S(%1=1:"X",%1=2:"o",1:"§")
 E  S T=" "
 Q $S(%1=1:"[",%1=2:"(",1:"<")_T_$S(%1=1:"]",%1=2:")",1:">")
MONEY(%1) ;123.4561 --> 123.46
 S %0=$J(%1,"",2) Q %0
MONT(%1) ;%1-НОМЕР МЕСЯЦА --> ТЕКСТ МЕСЯЦА
 Q $P("Янваpь,Февpаль,Маpт,Апpель,Май,Июнь,Июль,Август,Сентябpь,Октябpь,Ноябpь,Декабpь",",",+%1)
MONTD(%1,%2) ;%1=ГГММДД  %2=НА СКОЛЬКО МЕСЯЦЕВ ПРИБАВИТЬ ИЛИ ОТНЯТЬ
 N M,G S M=$$M^%ZAPM.ed.BSsFUNR(%1)+$G(%2),G=$$Y^%ZAPM.ed.BSsFUNR(%1)
 F  Q:M'<1  S G=G-1,M=12+M
 F  Q:M'>12  S G=G+1,M=M-12
 I G?3N S G=$E(G,2,3)
 I G<0 S G=99
 S G=$$ADDYEAR^%ZAPM.ed.BSsFUNR(G)
 I M?1N S M="0"_M
 S %0=G_M_$E(%1,5,6)
 Q %0
DSCORR(%1,MINUS) ;ФОРМАТИРОВАНИЕ ТЕКСТА В КЛЕТКЕ С ПЕРЕНОСОМ ПО СЛОВАМ          DSCORR^%ZAPM.ed.BSsFUNM
 ;автор : Дружинин Сергей (С) 1997
 N FLAG,LONG,VARIANT,RES,K,I,W,SS,J,S,S1,S2,B,E,KOL,NEW,DS ;ЛОКАЛЬНЫЕ ПЕРЕМЕННЫЕ
 I '$D(BS) Q ""
 S LONG=$P($G(BS),"@",4)-$G(MINUS)
 S K=1,NEW="",SS=0,Q=0,J=1,W="",S2=1,KK=0 ; ПРИСВОЕНИЕ НАЧАЛЬНЫХ ЗНАЧЕНИЙ ПЕРЕМЕННЫМ
LOOP S S1=$F(%1,".",S2),B=$E(%1,S1-2),E=$E(%1,S1),S2=S1 I S1'=0 I ((B'=" ")&(E'=" ")) I ((B?1N'=1)&(E?1N'=1)) S %1=$E(%1,1,S1-1)_" "_$E(%1,S1,9999) S S2=S1+1 G LOOP
 S S2=1
LOOP1 S S1=$F(%1,",",S2),B=$E(%1,S1-2),E=$E(%1,S1),S2=S1 I S1'=0 I ((B'=" ")&(E'=" ")) S %1=$E(%1,1,S1-1)_" "_$E(%1,S1,9999) S S2=S1+1 G LOOP1
 S S2=1
LOOP2 S S1=$F(%1,"-",S2),B=$E(%1,S1-2),E=$E(%1,S1),S2=S1 I S1'=0 I ((B'=" ")&(E'=" ")) S %1=$E(%1,1,S1-1)_" "_$E(%1,S1,9999) S S2=S1+1 G LOOP2
 F I=1:1 Q:$P(%1," ",I,I+1)=""  D
 .S W=$P(%1," ",I)
 .I ($L(W)<LONG)!($L(W)=LONG) S VARIANT(J)=W S J=J+1 Q
 .E  S SS=$L(W)\LONG F Q=1:1:SS+1 S VARIANT(J)=$E(W,(Q-1)*LONG+1,Q*LONG) S J=J+1
 S KOL=J-1
 F I=1:1:KOL D                                   ;БЛОК
 .I NEW="" S NEW=VARIANT(I)                      ;ДОБАВЛЕНИЯ
 .E  S NEW=NEW_" "_VARIANT(I)                    ;НЕОБХОДИМОГО
 .I $L(NEW)=LONG S DS(K)=NEW S NEW="" S K=K+1 Q
 .I $L(NEW)>LONG D                               ;КОЛИЧЕСТВА
 ..S DS(K)=$E(NEW,1,LONG)                        ;ПРОБЕЛОВ СПРАВА
 ..I DS(K)[" " S DS(K)=$E(DS(K),1,($L(NEW)-$L(VARIANT(I))-1)) S DS(K)=DS(K)_$J("",(LONG-$L(DS(K)))) S NEW=VARIANT(I)
 ..E  S NEW=$E(NEW,LONG+1,9999) I $E(NEW,1)=" " S NEW=$E(NEW,2,9999)
 ..S K=K+1
 S DS(K)=NEW I $E(DS(K),1)=" " S DS(K)=$E(DS(K),2,9999)
 S RES="" F S=1:1:K S RES=RES_DS(S)
 Q RES  ;РЕЗУЛЬТАТ КОРРЕКЦИИ ПОЛОЖЕНИЯ СЛОВ В ЯЧЕЙКЕ
 
CENTR(%1,%2,%3) ;%3=1 - ЦЕНТР %3=2 ВПРАВО    %2-ДЛИНА КЛЕТКИ
 I '$G(%2) S %2=$P($G(BS),"@",4)
 I '$G(%3) S %3=1
 I %3=1 S %0=$J("",(%2-$L(%1))\2)_%1 Q %0
 S %0=$J(%1,%2) Q %0
KCHO K cHOSe
e Q
OT D O^%ZAPM.ed.BSF7 S YES=0,%0="" X "I 0" Q ""
ASYS(%1,I) ;ДОСТУПНЫЙ ТОМ
 I %1="*" S @(I_"%1)")=""
 I 1 S YES=1,%0=%1 Q %0
AUCI(%2,I) ;ДОСТУПНЫЙ КИП
 I %2="*" S @(I_"%2)")=""
 S YES=1,%0=%2 I 1 Q %2
APAR(%3,I) ;ДОСТУПНЫЙ РАЗДЕЛ
 I %3="*" S @(I_"%3)")=""
 S YES=1,%0=%3 I 1 Q %3
ATAB(%4,I) ;ДОСТУПНАЯ ТАБЛИЦА
 I %4="*" S @(I_"%4)")=""
 S YES=1,%0=%4 I 1 Q %4
NEWuser(%5) ;НОВЫЙ ПОЛЬЗОВАТЕЛЬ
 I $L(%5)>7!($TR(%5," .\/+!@#$%^&*()_-|","")'=%5) S ls="ВВЕДИТЕ ДО 7 БУКВЕННЫХ КОМБИНАЦИЙ" G OT
 D USER^%ZAPM.ed.BSF4 I $G(%PAROL)="" S ls=" НЕ ВВЕДЕН " G OT
 S YES=1,@BSD@(".@PASSWORD",%5)=%PAROL,@BSD@(".@WHO",%5)=$G(li),%0=%5,@BSD@(%5)="" D  I 1 Q %5
 .S i="" F  S i=$O(@BSD@(".@PASSWORD",i)) Q:i=""  I '$D(@BSD@(i)) K @BSD@(".@PASSWORD",i)
 .S i="" F  S i=$O(@BSD@(".@WHO",i)) Q:i=""  I '$D(@BSD@(i)) K @BSD@(".@WHO",i)
 Q
Fio(S,D) ;В ПРЕДЛОЖЕНИИ ПОСТАВИТЬ ЗАГЛАВНЕ БУКВЫ
 N i,ii,Di S Di=" " I $D(D) S Di=$E(D,1)
 F i=1:1 Q:$P(S,Di,i,i+1)=""  S $P(S,Di,i)=$$BIGL($E($P(S,Di,i),1))_$$LITL($E($P(S,Di,i),2,$L($P(S,Di,i))))
 Q S
LITL(%1) ;БОЛЬШИЕ БУКВЫ В МАЛЕНЬКИЕ
 Q $TR(%1,"QWERTYUIOPASDFGHJKLZXCVBNMЙЦУКЕНГШЩЗХЪФЫВАПРОЛДЖЭЯЧСМИТЬБЮЁ","qwertyuiopasdfghjklzxcvbnmйцукенгшщзхъфывапролджэячсмитьбюе")
BIGL(%1) ;МАЛЕНЬКИЕ В БОЛЬШИЕ
 Q $TR(%1,"qwertyuiopasdfghjklzxcvbnmйцукенгшщзхъфывапролджэячсмитьбю","QWERTYUIOPASDFGHJKLZXCVBNMЙЦУКЕНГШЩЗХЪФЫВАПРОЛДЖЭЯЧСМИТЬБЮ")
TR(%1,%2,%3) N p,l,l3 ;ТРАНСФОРМАЦИЯ !!!!!
 I %2="" Q %1
 I %1'[%2 Q %1
 S p=1,l=$L(%2),l3=$L(%3),%z=$ZT,$ZT="TRMAX^%ZAPM.ed.BSsFUNM"
 F  S p=$F(%1,%2,p) Q:p=0  S $E(%1,p-l,p-1)=%3,p=p+l3-l
 S $ZT=$G(%z) Q %1
TRMAX W $$bel^%ZAPM.ed.BS3 S $ZT=$G(%z) Q $G(%1,$G(s))
NINV(%1,%2,%3) N li,GL ;КОРРЕКЦИЯ КОДИФИКАТОРА
 Q:%1="" 1 ;%1-МАССИВ %2-КОД %3-ТЕКСТ
 Q:%2="" 1 S li=%1 D GL^%ZAPM.ed.BSF3 I 'YES Q 1
 I $D(@(GL_"(%2)")) D  Q 1
 .I %3="" S ls=" УДАЛИМ ИЗ "_GL_" КОД:"_%2_". ВЫ УВЕРЕНЫ ? " D YES^%ZAPM.ed.BSF D  Q
 ..I YES S %3=@(GL_"(%2)") K @$ZR K:%3'="" @(GL_"in(%3)")
 .I $D(@(GL_"in(%3)")) Q
 .S @(GL_"in(%3)")=%2
 I %3'="" S ls=" ДОБАВИМ В "_GL_" НОВЫЙ КОД :"_%2_" . ВЫ УВЕРЕНЫ ? " D YES^%ZAPM.ed.BSF I YES S @(GL_"(%2)")=%3,@(GL_"in(%3)")=%2 Q 1
 I 1 Q 1
DELCTL(d) N UP,i ;УДАЛЕНИЕ ИЗ d УПРАВЛЯЮЩИХ КОДОВ С ЗАМЕНОЙ НА "¬"
 S UP="" F i=0:1:31 S UP=UP_$C(i)
 Q $TR(d,UP,$TR($J("",$L(UP))," ","¬"))
COLOR(%1) ;ЦВЕТ !!! CLR !!!
 S CLR=%1 Q ""
CELL(%1,%2,%3,%4) ;ПСЕВДОГРАФИКА    ;             %3 |
 N l,h,s,i,t Q:'$D(BS) ""           ;     %2  =======| %4
 S l=$P($G(BS),"@",4),h=$P($G(BS),"@",3)
 S s=$G(%1)_$J("",h*l),t=$L(%1)
 I $G(%3)'="" S l=l-1,t=t+h
 F i=1:1:h-1  S s(i)=$E(s,i*l-l+1,l+(l*(i-1)))_$E(%3,1)
 I $G(%2)'="" S t=t+l,s(h)=$TR($J("",l)," ",$E(%2,1))_$G(%4,$G(%3))
 E  S s(h)=$E(s,h*l-l+1,l+(l*(h-1)))_$G(%4,$G(%3))
 S s="" F i=1:1:h S s=s_s(i)
 Q s_$J(".",t)
Butt(%1) ;КНОПКА
 N l,h,s,i,t,%2,%3,%4 S %3="¶",%2="®",%4="®" ; ¶  код220 _  код221 [  код222 ]  код223 ®
 S l=$P($G(BS),"@",4),h=$P($G(BS),"@",3)
 S s=$G(%1)_$J("",$P(BS,"@",3)*$P(BS,"@",4))
 S t="",k=1 F i=0:(l-$L($G(%3))) Q:k=h  S t=t_$E(s,1+i,l-$L($G(%3))+i)_$S(k=1:"_",1:$G(%3)),k=k+1
 I $G(%2)="" S t=t_$E(s,1+i,l-1+i)_$G(%4)_$E(s,l+i,$$LNG^%ZAPM.ed.BSF12) Q t
 S t=t_" "_$TR($J("",l-2)," ",$E(%2))_$G(%4)_$E(s,l+i,$$LNG^%ZAPM.ed.BSF12) Q t
