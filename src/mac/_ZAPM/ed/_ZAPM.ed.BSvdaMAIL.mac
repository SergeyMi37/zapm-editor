ROUTINE %ZAPM.ed.BSvdaMAIL
%BSvdaMAIL ;ПОЧТОВЫЕ ОПЕРАЦИИ - ПРИЕМ/ОТПРАВКА ПОЧТОВЫХ СООБЩЕНИЙ
 ;       
 ;  Copyright (C) Воронов Д.А.
 ;  Copyright (C) Serge W. Mikhaylenko
 ;  All Rights Reserved
 q
MainServerSMTP() //какой нынче почтовый сервер главный?
 ;N A,IP,port,sport,TCP S ErrMsg="Error "
  Q $ZU(110) ;пока так
 S A=$ZCONVERT($P($P($$GETOPT^%ZAPM.ed.BSC4cfgP("^%ZAPM.ed.BSETUP(""ISS"")","Admin"),"@",2),">"),"U")
 I A="" S ErrMsg="Не определен Главный почтовый сервер ИнфоПортала" Q ""
 I A'=$ZU(110) D  Q A
 .S IP=$P($ZU(54,13,A),",") I IP="" S ErrMsg="Главный почтовый сервер ИнфоПортала '"_A_"' не доступен",A=""
 .Q  ;ПРОВЕРИТЬ ЗАПУЧЕН ЛИ ОН НА 25 ПОРТУ?
 .F sport=1965,25 D
 ..S TCP="|TCP|"_sport Open TCP:(A:sport):2 I  C TCP K TCP Q 
 .I $D(TCP) S ErrMsg="На сервере '"_A_"' не запущен SMTP сервер",A=""
 S port=$P($G(^%ZAPM.ed.BSpgWEMM("HOST",$ZCONVERT($ZU(110),"U")),"25,110"),",",1) L ^%ZAPM.ed.BSpgWEBC(port):0 
 I  S ErrMsg="Главный почтовый сервер ИнфоПортала '"_A_"' не запущен",A="" Q A
 Q $ZU(110)
NEWS //ПОЧТА?
 I $$MainServerSMTP="" Q
 N BSmName,BSmPass S BSmName=BSLOGIN,BSmPass=BSSES
 W "<HR>" S MSG=$$NEWSMAIL(BSLOGIN,BSSES) 
 I MSG'="0/0" W !,RED,"Для Вас есть письма ! Новых сообщений "_MSG_". <A HREF='"_$$ADDBSGET^%ZAPM.ed.BSC4(,"POP2^~BSvdaMAIL")_"' TITLE='письма, пришедшие на ваш адрес'>загрузить</A>"_EF i 1
 e  W !,RED,"Для Вас писем нет !",EF
 W " Можете сами создавать сообщения. <A HREF='"_$$ADDBSGET^%ZAPM.ed.BSC4(,"SMTP1^~BSvdaMAIL")_"' TITLE='написать письмо другому пользователю портала'> Написать письмо</A>"
 Q
ADMINMAIL ;СОБРАТЬ ВСЕ ПОЧТУ ДЛЯ АДМИНА
 N BSmName,BSmPass,ADMINMAIL S BSmName=BSLOGIN,ADMINMAIL=1
 D VAR,BEG1^%ZAPM.ed.BSC4 
 W $$TITL^%ZAPM.ed.BSCGIS("ПРИЕМ ПОЧТЫ"),$$B27^%ZAPM.ed.BSCGIS(1)
 I $G(EMO)="" S EMO=$$MainServerSMTP
 I EMO'="" D POPP(EMO)
 S MDE="" F  S MDE=$O(^%ZAPM.ed.BScSMTP(MDE)) Q:MDE=""  I MDE'=EMO D POPP(MDE,BsMAILpass)
 d EOF
 Q
NEWSMAIL(BSmName,PASS) //КОЛИЧЕСТВО ПИСЕМ N/L
 //N - всего 
 //L - из них от живых пользователей
 N i,I,MASS,EMO,L
  S EMO=$$MainServerSMTP I EMO="" Q "'ErrorServerName'"
  S BSmName1=$G(BSmName)_"@"_EMO
 I $$POP3(EMO,BSmName1,PASS,.MASS,$$DIRUSER^%ZAPM.ed.BSCmDDR($P(BSmName,"@")))<1 Q "0/0"
 S I="",L=0 F i=1:1 S I=$O(MASS(I)) Q:I=""  I $P($G(MASS(I,"from")),"@",1)'="BSystemGuard" S L=L+1
 Q (i-1)_"/"_L
VAR      ;ФОРМИРОВАНИЕ БАЗОВЫХ ПЕРЕМЕННЫХ 
 ;
 q
BOF ;НАЧАЛО ФАЙЛА HTML
 D BEG1^%ZAPM.ed.BSC4
 w "<TITLE>ОТПРАВКА/ПРИЕМ ПОЧТОВЫХ СООБЩЕНИЙ</TITLE>",BK
 q
EOF ;ЗАВЕРШЕНИЕ ФАЙЛА HTML
 D END^%ZAPM.ed.BSC4
 q
MAIN      d BOF
HOMEPAGE ;Главная страница
 S A=$$MainServerSMTP I A="" W ErrMsg Q
 D SMTP1
 D POP1
 d EOF
 Q
SELFILES(N) //СПИСОК ФАЙЛОВ ИЗ РАБОЧЕЙ ДИРЕКТОРИИ
 N MAS,U1,i,I,FN,Q,FN1,FN2
 S FN2=$ZCVT($$DIRUSER^%ZAPM.ed.BSCmDDR,"L")
 S MAS="in" D FileListDir^%ZAPM.ed.BSCEXE(FN2,MAS,2,"")
 S Q="<select name=FILE"_N_" >"_"<option> </option>"
 S U1="",i=0,I=0 F  S U1=$O(@MAS@(U1)) Q:U1=""  D
 .S FN=$ZCVT($P(@MAS@(U1),"?",1),"L"),FN1=$P(FN,FN2,2)
 .S Q=Q_"<option>"_FN1_"</option>"
 Q Q_"</select>"_BK
SELLSERV N list D USERSMAILIST^%ZAPM.ed.BSpgWEMM(.list) w "<option VALUE='' ></option>"
 //$$CFG^%ZAPM.ed.BSCp()="ASU"
 s i="" f  s i=$o(list(i)) Q:i=""  I $G(@BDUSE@(list(i),"FNM"))'="" s z=$G(@BDUSE@(list(i),"FNM")) i $a($e(z))>73  S list2(z)=list(i)
 s i="" f  s i=$o(list2(i)) Q:i=""  w "<option VALUE="_list2(i)_" " W:$G(BSend2mail)=list2(i) "SELECTED" W ">" D  W "</option>"
 .W i ;_"("_list2(i)_")"
 .;I $G(@BDUSE@(list(i),"FNM"))'="" W "("_@$ZR_")"
 w "</select>" Q
atth(cf) i cf="VV" q " title='для того чтобы вложить в письмо файл, его надо сначало отправить в рабочую директорию FTP-сервера, а потом после обновления панели выбрать его из трех списков' " 
 q " title='выберите только один файл и вложите его в письмо' " 
SMTP1    d VAR   D BEG1^%ZAPM.ed.BSC4
 w "<TITLE>ОТПРАВКА ПОЧТОВЫХ СООБЩЕНИЙ</TITLE>",BK
 S EMO=$$MainServerSMTP I EMO="" W ErrMsg Q
 I $G(BSLOGIN)="" W RED_"Сначало откройте сессию ИнфоПортала !"_EF Q
 w "<form action="""_$$ADDLIB^%ZAPM.ed.BSC4()_""" method=""POST"" name=""frmsmp"">",BK
 S BSLABEL="SEND^~BSvdaMAIL"    
 D ADDBSPOST^%ZAPM.ed.BSC4reg
 w "<table border=0 width=100%><TR><TD class=s5 align=center><B>Отправка почты</B></TD></TR></TABLE>",BK
 w "<table border=0 width=100%><tr><TD class=s3 align=center><img border=0 src='"_BSDOMAIN_"/img/letter.gif' >Кому*</td> <TD class=s3>" ;<input type=""text"" name=""komu"">"
 ;w "@"
 i $G(BSend2mail)'="" w "<input type=""text"" disabled value='"_$G(BSend2mail)_"' size=90>" w "<input type=""hidden"" name=komu value='"_$G(BSend2mail)_"' size=90>"
 e  w "<select name=komu>" D SELLSERV
 w "</td></tr>"
 w "<TD class=s3 align=center>От кого </td><TD class=s3><input DISABLED type=""text"" value="_$G(BSLOGIN,"NoName")_" >"
 w "<input type=""HIDDEN"" name=otkogo value="_$G(BSLOGIN,"NoName")_" >"
 w "<input DISABLED type=""text"" name=EMO1 value='@"_EMO_"' >"
 w "</td></tr>"
 w "<TD class=s3 align=center>Тема </td><TD class=s3><input type=""text"" name=""SUBJ"" value='"_$G(SUBJ)_"' size=90></td></tr>"
 w "<tr><td class=s3 align=center>Сообщение</td> <td class=s3><textarea rows=10 cols=90 name='TXTMAIL' >" D  W "</textarea></td></tr>"
 .I '$D(BSmN) W "Здравствуйте ..."_BK_BK_BK_"С Уважением ..." Q
 .F TA=1:1 Q:'$D(TA(TA))  W TA(TA)_BK
 s zBS4cfg=$G(^%ZAPM.ed.BS("Config")) 
 w "<tr><td class=s3 align=center "_$$atth(zBS4cfg)_">"_$s(zBS4cfg="VV":"Прикрепить файл<img border=0 src='"_BSDOMAIN_"/img/attach.gif' >",1:"")_"</td>"
 w "<td "_$$atth(zBS4cfg)_" class=s3>" d  w "</td></tr></table>"
 .i zBS4cfg="VV" w $$FTPBUTT^%ZAPM.ed.BSCmDDR(BSLOGIN)_$$SELFILES(1)_$$SELFILES(2)_$$SELFILES(3)  q
 .Q  //ПОКА ТАК
 .I $G(@BDSES@(BSSES,"VAR","Attach"))'="" W RED_$G(@BDSES@(BSSES,"VAR","Attach"))_EF_BBK
 .w $$BUTTBS^%ZAPM.ed.BSCp2("UPL^~BSvdaMAIL","attach.gif","_new","Выберите файл",300,500,"Выберите файл") 
 .S @BDSES@(BSSES,"VAR","HTTPREF")=$P(HTTPREF," ",2,999)
 w "<center>"
 w "<BUTTON onclick='Prov(komu.value);'  name=""sseenndd""  title='save currently setting' ><img border=0 src='"_BSDOMAIN_"/img/send.gif' > Отправить</BUTTON>"_BK
 //w "<input Type=""submit"" name=""sseenndd"" value=""Отправить"" >
 
 w " * - поля, обязательные для заполнения </CENTER>"
 w "</form>",BK
 D JS^%ZAPM.ed.BSCp
 w "function Prov(v) {"_BK
 w "if (v=='') {alert('Поле адресата пустое!'); return false; }"_BK
 w "frmsmp.submit(); "_BK
 w "}"_BK
 D JSE^%ZAPM.ed.BSCp
 w "<HR>"
 Q
UPL  //UPLOAD FILE TO SERVER
 S BSS1=""
 S BSS6="FILE1^~BSvdaMAIL" //ПРОГРАММА КОТОРАЯ ИСПОЛНЯЕТСЯ ПОСЛЕ ЗАГУЗКИ ФАЙЛА
 S BS1="" 
 S BS2=""  I $$SetVar^%ZAPM.ed.BSC4("NSpace",BSNSP) 
 S BSS3=BSSES,BS3=MGWLIB,BSS8=$$BSADDVAR^%ZAPM.ed.BSC4(),BSS4="" 
 D UP^%ZAPM.ed.BSC4r3
 Q 
FILE1 D BEG1^%ZAPM.ed.BSC4 W $$TITL^%ZAPM.ed.BSCGIS("UPLOAD. Step 2"),$$B27^%ZAPM.ed.BSCGIS(1) 
 W RED_$G(FiNa)_EF_BBK //ИМЯ ФАЙЛА
 //S NoCloseWin=1 //ФЛАГ НЕ ЗАКРЫВАТЬ ОКНА
 N AA M AA=@GL D Arr2File^%ZAPM.ed.BSCEXE(.AA,"c:\!\1222") K @GL //МАССИВ В КОТОРОМ ЛЕЖИТ ЗАГРУЖЕННЫЙ ФАЙЛ
 S @BDSES@(BSSES,"VAR","Attach")=$G(FiNa)
 D JS^%ZAPM.ed.BSCp
 w "opener.location='"_$G(@BDSES@(BSSES,"VAR","HTTPREF"))_"';"_BK
 D JSE^%ZAPM.ed.BSCp
 D END^%ZAPM.ed.BSC4
 Q 
ANSVER1 
 ;D locvar^%ZAPM.ed.BSCh0("",1) //1 - выводить %KEY,%CGI,%ZCS,%CGIEVAR
 N MA M MA=@BDSES@(BSSES,"VAR","MSG",BSmN) K TA 
 S I="" F  S I=$O(MA("z",I)) Q:I=""  D
 .I $G(MA("z",I,"msg"))="text" D
 ..S II="" F TA=1:1 S II=$O(MA("z",I,"msg",II)) Q:II=""  S TA(TA)=$E($G(BSend2mail),1)_"&gt;"_$G(MA("z",I,"msg",II))
 S SUBJ="Re: "_$G(MA("subject"))
 S BSend2mail=$ZCONVERT($P($G(MA("from")),"@",1),"U")
 G SMTP1
SEND //ПОСЛАТЬ ПИСЬМО
 d VAR
 d BOF
 if $G(komu)="" W RED_"Не заполнено поле 'КОМУ'"_EF G MAIN
 I $G(SUBJ)="" S SUBJ=$TR($E(TXTMAIL,1,50),$C(13,10),"  ") I SUBJ="" S SUBJ="NO SUBJ"
 F I=1:1:3 I $G(@("FILE"_I))'=""  D  S ATT(I)=DIRUSER_$G(@("FILE"_I))
 .I '$D(DIRUSER) S DIRUSER=$$DIRUSER^%ZAPM.ed.BSCmDDR()
 S EMO=$$MainServerSMTP I EMO="" W ErrMsg Q
 S S=$$SMTP(EMO,$G(TXTMAIL),$G(otkogo)_"@"_EMO,$G(komu)_"@"_EMO,$G(SUBJ),.ATT)
 I 'S W RED_S_EF
 E  w GREEN_"Отправлено"_EF_" Письмо от <I><B>"_$G(otkogo)_"@"_EMO_"</B></I> к <I><B> "_komu_"@"_EMO_"</B></I> на тему <I><B>"_SUBJ_"</B></I>"
 G HOMEPAGE
 
SMTP(MDEAM,TXTMAIL,otkogo,komu,SUBJ,att) //ПОСЛАТЬ
 N OK1 S OK1=1
 set s=##class(%Net.SMTP).%New()
 set s.port=$G(^%ZAPM.ed.BScSMTP(MDEAM,"SMTP"),"25")
 set s.smtpserver=MDEAM         // Specify SMTP server name.
 set s.timezone=$$TimeZone^%ZAPM.ed.BSpgWEMM()  // Timezone by Grinwitch
 set m=##class(%Net.MailMessage).%New() 
 set m.From=$G(otkogo)        // From 
 do m.To.Insert($G(komu)) // List of emails to send this mail
 set m.Subject=$G(SUBJ)     // Subject
 // Charset specification
 //set m.Charset="iso-8859-1" 
 // Text
 do m.TextData.Write(TXTMAIL)
 // Attachments
 n i
 I $D(att) S i="" F  S i=$O(att(i)) Q:i=""  D
 .set status=m.AttachFile($P(att(i),"\",1,$L(att(i),"\")-1)_"\",$P(att(i),"\",$L(att(i),"\")))
    //Send letter   
 set status=s.Send(m)    if ('status) S OK1=$$ERRWF(status)
 do m.%Close()
 do s.%Close()
 quit $G(OK1)
 
POP1 D VAR,BEG1^%ZAPM.ed.BSC4
 N BSmName
 w "<TITLE>ПРИЕМ ПОЧТЫ</TITLE>",BK
 w "<form action="""_$$ADDLIB^%ZAPM.ed.BSC4()_""" method=""POST"" name=""frmsmp"" target='RECEIVE'>",BK
 S BSLABEL="POP2^~BSvdaMAIL"    
 D ADDBSPOST^%ZAPM.ed.BSC4reg
 w "<table border=0 width=100%><TR><TD align=center class=s5><B>Просмотр входящих писем</B></TD></TR></TABLE>",BK
 w "<table border=0 width=100%>"
 w "<tr><TD class=s3>Имя пользователя</td><TD class=s3><input type=""text"" DISABLED value="""_BSLOGIN_""" ><input type=""HIDDEN"" name=""BSmName"" value="""_BSLOGIN_""" >"
 ;w "@"
 ;w "<select name=EMO>" D SELLSERV
 w "</td></tr>"
 ;w "<tr><TD class=s3>Пароль</td><TD class=s3 ><input type=""password"" name=""BSmPass"" value=""""></td>"
 w "</table><center><img border=0 src='"_BSDOMAIN_"/img/recv.gif' ><input Type=""submit"" value=""Проверить почту""></center>",BK
 w "</form>",BK
 w "<IFRAME HEIGHT='98%' width='98%' SRC='"_$$ADDBSGET^%ZAPM.ed.BSC4(,"stub^~BSCrep")_"' name='RECEIVE' SCROLLING='auto'></IFRAME><BR>"
 q
TABHEAD(A) w "<table border=0 width=100%><tr>"_BK
 F I="№","От кого","Написанное","Тема","Объем (байт)","Файлы","Действие" I A'[I W "<td class=s2>"_I_"</td>"
 W "</tr>"_BK
 Q
TABMAIL(MASS,BSmN,S) F I="from","date","subject","messagesize" D
 .N COL1,COL2  //
 .I I="from" D
 ..I MASS(BSmN,I)'["BSystemGuard" S COL1=RED,COL2=EF
 ..i $$CFG^%ZAPM.ed.BSCp="ASU",$p(MASS(BSmN,I),"@")'="" s fio=$G(@BDUSE@($p(MASS(BSmN,I),"@"),"FNM"))  s MASS(BSmN,I)=fio
 .W "<TD class=s1>"_$G(COL1)_MASS(BSmN,I)_$G(COL2)_" " D:S  W "</TD>"_BK
 ..I I="subject" N i S i="" F  S i=$O(MASS(BSmN,"z",i)) Q:i=""  D TEA(.MASS,BSmN,i,5,45)
 Q
TEA(MA,BSmN,I,rows,cols)  I $G(MA(BSmN,"z",I,"head","content-type"))["text/plain" D
 .N II w "<textarea rows="_rows_" cols="_cols_">"
 .S II="" F  S II=$O(MA(BSmN,"z",I,"msg",II)) Q:II=""  W $G(MA(BSmN,"z",I,"msg",II))
 .W "</textarea>"_BBK 
 Q
AttachCount(MA,BSmN) //ПОСЧИТАТЬ СКОЛЬКО ВЛОЖЕНО ФАЙЛОВ
 N I,Q S Q=""
 S I="" F  S I=$O(MA(BSmN,"z",I)) Q:I=""  D
 .I $G(MA(BSmN,"z",I,"msg"))="binary" I $G(MA(BSmN,"z",I,"msgFN"))'="" S Q=Q+1
 Q Q
POP2 //ПОКАЗАТЬ СПИСОК ПИСЕМ
 i $d(main) g MAIN
 D VAR,BEG1^%ZAPM.ed.BSC4 
 W $$TITL^%ZAPM.ed.BSCGIS("ПРИЕМ ПОЧТЫ"),$$B27^%ZAPM.ed.BSCGIS(1)
 I $G(EMO)="" S EMO=$$MainServerSMTP
 I EMO'="" D POPP(EMO)
 d EOF
 q
POPP(EMO,BSmPass)
 S BSmName1=$G(BSmName)_"@"_$G(EMO) K MASS
 I $$POP3(EMO,BSmName1,$G(BSmPass,BSSES),.MASS,$$DIRUSER^%ZAPM.ed.BSCmDDR($P(BSmName1,"@")))<1 W "Сервер: "_EMO W RED D ERRW($G(%objlasterror)) W EF_BBK Q:$D(ADMINMAIL)  q // G POP1
 K @BDSES@(BSSES,"VAR","MSG")
 M @BDSES@(BSSES,"VAR","MSG")=MASS //СОХРАНИТЬ В БУФЕРЕ ПОЛЬЗОВАТЕЛЯ
 N BSLABEL,BSLOGINDFLT,BSNOREG,BSTOP,BSTOP1,BSTOP3
 w "<form action="""_$$ADDLIB^%ZAPM.ed.BSC4()_""" method=""POST"" name=""messages"">",BK
 S BSLABEL="POP4^~BSvdaMAIL"
 D ADDBSPOST^%ZAPM.ed.BSC4reg
 w "<table border=0 width=100%><TR><TD align=center class=s5><B>Сервер: "_RED_EMO_EF_" Входящие письма для "_RED_BSmName1_EF_"</B></TD></TR></TABLE>",BK
 D TABHEAD("1")
    //                                                                   "Fri, 13 Jan 2006 21:45:25 0300"           
    N z,zz,zzz S zz="" F   S zz=$O(MASS(zz)) Q:zz=""  S zzz=$TR($ZDTH($P(MASS(zz,"date")," ",2,5),2),",",".") M z(+zzz)=MASS(zz)
    K MASS S zz="" F z=1:1 S zz=$O(z(zz)) Q:zz=""  M MASS(z)=z(zz)
 S BSmN="" F  S BSmN=$O(MASS(BSmN),-1) Q:BSmN=""  W "<TR><TD class=s1>"_BSmN_"</TD>" D
 .D TABMAIL(.MASS,BSmN,1)
 .W "<TD class=s1>"_$$AttachCount(.MASS,BSmN)_"</BUTTON>"_BK
 .//S BSLABEL="READMSG^~BSvdaMAIL"
 .W "<TD class=s1>" //<BUTTON title='Смотреть письмо в новом окне' onclick='open("""_$$ADDBSGET^%ZAPM.ed.BSC4()_""",""MSG"",""toolbar=no,menubar=no,scrollbars=yes,width=800,height=600,status=1,resizable=1"");' ><img border=0 src='"_BSDOMAIN_"/img/lett-op.gif' >Смотреть</BUTTON>"_BK
 .S BSLABEL="KILLMSG^~BSvdaMAIL"
 .W "<input Type=CHECKBOX name=DelMail"_BSmN_"> Удалить письмо</TD>"_BK
 .W "</TR>"_BK
 w "</table>"_BK
 w "<div align=RIGHT><input Type=submit name=backtomaiN value=""Закрыть окно""><input Type=HIDDEN name=DE value=''>"
 w "<BUTTON onclick='messages.DE.value=1; submit();' TITLE='Удалить помеченные' ><img border=0 src='"_BSDOMAIN_"/img/kill.gif' >Удалить помеченные</BUTTON>"_BK
 w "<BUTTON onclick='messages.DE.value=2; submit();' TITLE='Удалить все' ><img border=0 src='"_BSDOMAIN_"/img/del.gif' >Удалить все</BUTTON>"_BK
 w "</div>",BK
 w "</form>"
 Q
POP4 //ПОКАЗАТЬ СПИСОК ПИСЕМ
 D VAR N OK D BEG1^%ZAPM.ed.BSC4
 ;D locvar^%ZAPM.ed.BSCh0("",1)
 ;D JS^%ZAPM.ed.BSCp
 if $G(DE)     D  W RET //"alert('"_RET_"');"_BK
 .F I=1:1:350 I $D(@("DelMail"_I))!(DE=2) S MA(I)=""
 .S OK=$$KILLMAIL($P($G(BSmName1),"@",2),$G(BSmName1),$G(BSmPass,BSSES),.MA,.Kill) I OK<1 S RET=$$ERRWF(OK) I 1
 .E  S RET="удалено "_+$G(Kill)
 ;W "window.close();"_BK
 ;D JSE^%ZAPM.ed.BSCp
 d EOF
 Q
KILL1 S DELE=1,@("DelMail"_BSmN)=1 
  G POP4
READMSG //ЧИТАТЬ ПИСЬМО
 d VAR D BEG1^%ZAPM.ed.BSC4 W "<TITLE>Письмо № "_BSmN_" для "_BSmName1_"</TITLE>"
 w "<BODY "_$$27^%ZAPM.ed.BSCGIS_">"_BK
 N MA M MA=@BDSES@(BSSES,"VAR","MSG") //ВОССТАНОВИТЬ ИЗ БУФЕРА ПОЛЬЗОВАТЕЛЯ
 ;D locvar^%ZAPM.ed.BSCh0("",1)
 D TABHEAD("№ДействиеФайлы")
 D TABMAIL(.MA,BSmN,0) W "</TABLE>"
 S I="" F  S I=$O(MA(BSmN,"z",I)) Q:I=""  D
 .D TEA(.MA,BSmN,I,25,90)
 .W "<A HREF='"_$$ADDBSGET^%ZAPM.ed.BSC4(,"ANSVER1^~BSvdaMAIL")_"'>Ответить</A>"
 .W " <A HREF='"_$$ADDBSGET^%ZAPM.ed.BSC4(,"KILL1^~BSvdaMAIL")_"'>Удалить</A>"
 .I $G(MA(BSmN,"z",I,"msg"))="binary" S FN=$G(MA(BSmN,"z",I,"msgFN")) I FN'="" W "<img border=0 src='"_BSDOMAIN_"/img/attach.gif' >Вложенный файл <b style='color: white; background-color: Gray;' >"_FN_"</b> можно загрузить через FTP-Cервер "_$$FTPBUTT^%ZAPM.ed.BSCmDDR(BSLOGIN)_BBK
 d EOF
 Q
 
ERRW(S) D DecomposeStatus^%apiOBJ(S,.err) 
 i err(err)["ОШИБКА #6015" w !,"Сообщений нет!",! q
 write !,err(err),! 
 Q
ERRWF(S) D DecomposeStatus^%apiOBJ(S,.err) Q err(err)
POP3(MDEAM,BSmName,BSmPass,MASS,dir) //ВОЗВРАТИТ МАССИВ СПИСКА ПИСЕМ
 ;MDEAM - ИМЯ ПОЧТОВОГО СЕРВЕРА ИЛИ ЕГО IP
 ;,BSmName - ИМЯ ПОЛЬЗОВАТЕЛЯ  = "NAME_user@"_MDEAM
 ;,BSmPass - ПАРОЛЬ ПОЛЬЗОВАТЕЛЯ
 ;,MASS - МАССИВ КОТОРЫЙ СОЗДАСТ ПРОГРАММА, ГДЕ БУДЕТ ЛЕЖАТЬ СПИСОК ПИСЕМ
 ;,dir - ПУТЬ К ДИРЕКТОРИИ, КУДА ЗАКАЧАЮТСЯ ВЛОЖЕННЫЕ ФАЙЛЫ
 new mailserver,status,from,to,date,subject,messagesize,m,hdrs,key,mailMsg,lined
 N mssg
 set mailserver=##class(%Net.POP3).%New()
 I $D(dir) set mailserver.AttachDir=dir
 set mailserver.Debug=0
 set mailserver.port=$G(^%ZAPM.ed.BScSMTP(MDEAM,"POP"),"110")
 set mailserver.StoreAttachToFile=1
 set status=mailserver.Connect(MDEAM,BSmName,BSmPass)
 if ('status) g clo
 f mssg=1:1 d  if ('status) Q
 .set status=mailserver.FetchMessage(mssg,.from,.to,.date,.subject,.messagesize,.hdrs,.mailMsg,0)
 .if ('status) Q
 .S MASS(mssg,"subject")=subject
 .S MASS(mssg,"date")=date
 .S MASS(mssg,"from")=from
 .S MASS(mssg,"messagesize")=messagesize
 .S MASS(mssg,"to")=to
 .N MAS
 .d Dump(mailMsg,.MAS)
 .M MASS(mssg,"z")=MAS
 .s status=mailMsg.%Close()
clo s status=mailserver.%Close()
 q $G(mssg)-1
Dump(msg,MASS) 
 new i,index,value
 if msg.IsMultiPart do  quit
 . for i=1:1:msg.Parts.Count() do Dump(msg.Parts.GetAt(i),.MASS)
 s index=""  f  s value=msg.Headers.GetNext(.index) q:index=""  S MASS(msg,"head",index)=value
 if msg.IsBinary do
 . S MASS(msg,"msg")="binary"
 . S MASS(msg,"msgFN")=msg.FileName
 . S MASS(msg,"msgGA")=msg.GetAttribute("content-disposition","filename")
 else  do
 . ;w !,"Dumping text msg Filename="_msg.FileName_" filename="_msg.GetAttribute("content-disposition","filename"),!
 . S MASS(msg,"msg")="text"
 . S MASS(msg,"msgFN")=msg.FileName
 . S MASS(msg,"msgGA")=msg.GetAttribute("content-disposition","filename")
 . set stream=msg.TextData
 . do stream.Rewind()
 . new len,line
 . for l=1:1 set len=32763,line=stream.Read(.len) do  quit:stream.AtEnd
 ..S MASS(msg,"msg",l)=line         ;write line
 quit 
KILLMAIL(MDEAM,BSmName,BSmPass,MSGK,Kill) //УДАЛИТЬ СПИСОК ПИСЕМ
 new mailserver,status,from,to,date,subject,messagesize,m,hdrs,key,mailMsg,lined,I
 N OK S OK=1,Kill=0
 set mailserver=##class(%Net.POP3).%New()
 set mailserver.port=$G(^%ZAPM.ed.BScSMTP(MDEAM,"POP"),"110")
 set mailserver.Debug=0
 set mailserver.StoreAttachToFile=1
 set status=mailserver.Connect(MDEAM,BSmName,BSmPass)
 if ('status) S OK=status g Kclo
 S I="" F  S I=$O(MSGK(I)) Q:I=""  set OK=mailserver.DeleteMessage(I) S:OK Kill=Kill+1 I 'OK Q
 s status=mailserver.QuitAndCommit()
Kclo s status=mailserver.%Close()
 q $G(OK)
T1 W $$POP3^%ZAPM.ed.BSvdaMAIL("IVCMSW","SSS1","S",.MASS,"D:\!\1\") ZW MASS
 Q
T2       ;F I=2,3 S MA(I)=""  //УДАЛИТЬ 2,3 ПИСЬМО
 W $$KILLMAIL("IVCMSW","SSS1","S",.MA,.KKK)
 Q
 //ORIGINALLLLLL
 D ^%ZAPM.ed.BSCmail
