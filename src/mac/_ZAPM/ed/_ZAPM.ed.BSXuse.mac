ROUTINE %ZAPM.ed.BSXuse
%BSXuse            ; History,Поиск,Ввод,Таблица   (В.Лащев) ; 9:56   02.07.99
e q
 
FindHist() q $NA(@"^%ZAPM.ed.BSbufB(""HISSEARCH""_$S('$E(%BS(1),12):$G(%BS(3),$P)_$G(%BS(12)),$E(%BS(1),12)=1:""1""_$G(%BS(12)),1:""1""))")
 
Search(Next,Repl) i $D(Repl),'Edit d NoJob^%ZAPM.ed.BSXFkey q  ; поиск в тексте
 n Method,SubS,Base i Edit x PutS
 s Base=$$FindBase(),Method=$G(@Base),SubS=$G(@Base@(0))
 i 'Next d  i YES<1 q
 . s Tmp="Enter string to search "_$S($D(Repl):"and replace :",1:":")
 . s SubS=$$LineEdit^%ZAPM.ed.BSXfun(SubS,Tmp,"",Win(19),"",$$FindHist())
 . s:SubS="" YES=0 q:'YES  i $G(@Base@(0))'=SubS s @Base@(0)=SubS
 . i $D(Repl) d  i 'YES q
 .. s Tmp="Replace '"_SubS_"' into :"
 .. s Repl=$$LineEdit^%ZAPM.ed.BSXfun($G(@Base@(1)),Tmp,"",Win(19),"",$$FindHist())
 .. i YES i $G(@Base@(1))'=Repl s @Base@(1)=Repl
 . i $L(Method)'=5 s Method=$P(^%ZAPM.ed.BSX("Search"),"@")
 . s $E(Method,5)="1"
 . i '$D(Repl) s YES=" Find the context '"_SubS_"' "
 . e  s YES=" Replace the context '"_SubS_"' with '"_Repl_"' "
 . d SetMetod,Yes^%ZAPM.ed.BSXfun($$PoiskStr(Method),1,YES) i YES<0 q
 . i 'YES d  i 'YES q
 .. s %GO=1 f  d  q:YES'<0  x "s $E(Method,"_%GO
 ... s YES=$$Menu^%ZAPM.ed.BSXfun("^%ZAPM.ed.BSX",4,$E(%GO)+1,$$PoiskStr(Method))
 ... i YES i %GO'="Poisk" s YES=-1
 . d SetMetod i $G(@Base)'=Method s @Base=Method
 d Say^%ZAPM.ed.BSXfun($$PoiskStr(.Method))
 s Base=$$SeeBlock^%ZAPM.ed.BSXblck(Str,SubS)
 d ClrBot^%ZAPM.ed.BSXfun
 i Base<0 d ErrMsg^%ZAPM.ed.BSXfun(" Стpока '"_SubS_"' не найдена ") s:$D(Repl) Edit=1 q
 i Base>0 s Str=Base d
 . s Tmp=Col+$L(SubS)-Right-1 i Tmp>(Col-Left) s Tmp=Col-Left
 . i Tmp>0 s Left=Left+Tmp,Right=Left+Len,Qu=2
 . i $D(Repl) s Edit=4 q
 . s Tmp=" Контекст '"_SubS_"' найден"
 . d Say^%ZAPM.ed.BSXfun(Tmp_" в стpоке "_Str_" в позиции "_Col_" ")
 q
 
Replace n SubS,Repl,Base s Base=$$FindBase(),SubS=@Base@(0),Repl=@Base@(1)
 i '$E(@Base,5) s %GO=1                              ; подтвеpждение замены
 e  s Tmp=" Заменяем контекст '"_SubS_"' на '"_Repl_"' ?" d
 . i '$$Menu^%ZAPM.ed.BSXfun("^%ZAPM.ed.BSX",10,1,Tmp) s %GO=4
 i %GO=4 s Edit=1 q
 i %GO=3 s %GO=1,$E(@Base,5)="0"
 i %GO=1 d  d WrStr^%ZAPM.ed.BSXdop(Str-Up,.Bstr,Win(12),Win(16),.Str),SetEd^%ZAPM.ed.BSXedit(3)
 . d R^%ZAPM.ed.BS3($NA(@Head)) s $E(@Bstr,Col,Col+$L(SubS)-1)=Repl d D^%ZAPM.ed.BS3($NA(@Head))
 d Search(1,1) q
 
SetMetod f Tmp=1:1:$L(Method) s $E(Method,Tmp)=((+$E(Method,Tmp))'=0)
 q
 
PoiskStr(Method) n Msg,Dop               ; стpока метода
 s Msg=" Режим поиска : ",Dop="",j=(4+$D(Repl)),Tmp=^%ZAPM.ed.BSX("Search")
 f i=1:1:j s Msg=Msg_Dop_$P($P(Tmp,"@",i+1),";",$E(Method,i)+1),Dop=","
 q Msg_" . "
 
FindBase() q "^%ZAPM.ed.BSbufB(2,"_$G(%BS(3),$P)_",5,""Search"")"
 
BsxWin(Head) n Win,MaxX,MaxY s MaxX=80,MaxY=25         ; пpовеpить окно BSX
 s Win(0,0)=+$P(@Head,"@",8),Win(0,1)=+$P(@Head,"@",9)
 s Win(1,0)=+$P(@Head,"@",10),Win(1,1)=+$P(@Head,"@",11)
 i $E($G(%BS(14)),2) s MaxX=64
 i (Mode'="LINE")&(+$P(@Head,"@",12)=0) s MaxY=$S('$E($G(%BS(14)),2):22,1:21)
 s Win=$$CheckWin^%ZAPM.ed.BSXfun(1,1,MaxY,MaxX)
 s $P(@Head,"@",8,11)=Win(0,0)_"@"_Win(0,1)_"@"_Win(1,0)_"@"_Win(1,1)
 q Win
 
AddHist(Node,What,Max) i What?." " q
 n Cur s Cur=+$P($G(@Node),"@",28)
 i '$D(Max) s Max=$G(^%ZAPM.ed.BSETUP("SETUP",13,2),20),Max=+$P(Max,"@",15)
 e  s Max=+Max
 i Max<1 s Max=20
 i Cur>Max d  s $P(@Node,"@",28)=Cur
 . i $D(@Node@(0))!1 f  k ^(Cur) s Cur=Cur-1 q:Cur'>Max
 i $G(@Node@(1))=What q
 i Cur<Max s Cur=Cur+1,$P(@Node,"@",28)=Cur
 i $D(@Node@(0))!1 f Cur=Cur:-1:2 s ^(Cur)=^(Cur-1)
 I $ZV["Cach"||($ZV["IRIS") s ^(1)=What q
 s ^(1)=$E(What,1,$$LNG^%ZAPM.ed.BSF12)
 q
DelHist(Node,Num) n Max,Old s Max=+$P($G(@Node),"@",28),Num=+Num
 i (Num<1)!(Num>Max) q ""
 s Old=$G(@Node@(Num)) f Num=Num:1:Max-1 s ^(Num)=$G(^(Num+1))
 k ^(Max) s $P(@Node,"@",28)=Max-1 q Old
 
History n bsr s Tmp=$P(@Head,"@",12) i (Tmp="")!(+Tmp'=0) q
 s $E(Tmp,$L(Tmp))="",bsr=$P(Tmp,"("),Tmp=$P(Tmp,"(",2,999)
 i $E(Tmp)="""" s $E(Tmp)="",$E(Tmp,$L(Tmp))=""
 s Tmp=$$History^%ZAPM.ed.BSXfun(bsr,Tmp) i 'YES q
 x PutS d SetEd^%ZAPM.ed.BSXedit(3) s LocS=Tmp,cLen=$L(LocS)+1,Col=cLen,Qu=1,R1=-1 q
 
IniCreat(Tab,LenMass,ColNum) n i
 d Wait^%ZAPM.ed.BSXfun("Модифициpую таблицу '"_Tab_"' ") i $D(@Tab)
 s CreatTBL=$ZR,i=$G(@CreatTBL) k @CreatTBL s @CreatTBL=i
 s:$D(@$NA(Tab,0))[0 @$NA(Tab,0)="BaSe MsW @"
 s CreatTBL("Tab")=Tab,CreatTBL(1)=0,CreatTBL(2)=$G(ColNum,1),CreatTBL(2,0)=0
 F i=1:1:CreatTBL(2) s CreatTBL(2,i)=$G(@LenMass@(i),1) d
 . i CreatTBL(2,i)>70 s CreatTBL(2,i)=70
 . s CreatTBL(2,0)=CreatTBL(2,0)+CreatTBL(2,i)
 q
 
CreatJob(Mass,DBF,High) n i s CreatTBL(1)=CreatTBL(1)+1 x WA f i=1:1:CreatTBL(2) d
 . s @CreatTBL@(CreatTBL(1),i)="@@"_$G(High,1)_"@"_CreatTBL(2,i)_"@@"_$G(@DBF@(i))_"@@@1@@@@@@"_$TR($G(@Mass@(i)),"@#","§§")
 q
 
EndCreat(Nazv,Titul,Fk) n y1,x1,y2,x2,ke,se,H,BSR,BST
 i CreatTBL(1)<1 k @CreatTBL d ErrMsg^%ZAPM.ed.BSXfun(" Таблица '"_$NA(@CreatTBL)_"' пуста !") q
 s se=CreatTBL(1),ke=CreatTBL(2)
 i CreatTBL(2,0)>77 s CreatTBL(2,0)=77
 e  s CreatTBL(2,0)=CreatTBL(2,0)-1
 i CreatTBL(1)>19 s CreatTBL(1,0)=19
 e  s CreatTBL(1,0)=CreatTBL(1)-1
 s y1=(19-CreatTBL(1,0))\2+2,y2=y1+CreatTBL(1,0)
 s x1=(77-CreatTBL(2,0))\2+2,x2=x1+CreatTBL(2,0)
 s H=$E($G(Nazv),1,70)_"@@@@@@@"_y1_"@"_x1_"@"_y2_"@"_x2_"@1@@1@"
 s H=H_$G(Fk,1)_"@@1@@"_$G(Titul)_"@1@@@1@@@@@"_se_"@"_ke_"@@1;32;44@@@@@@@1"
 s BSR=$$CutLevel^%ZAPM.ed.BSXfun1(CreatTBL) ;...|
 s BST=$$GetIndex^%ZAPM.ed.BSXfun1(CreatTBL) ;...("Tab"))
 I BST[$C(34) S BST=$TR(BST,$C(34),"")
 s @CreatTBL=H k @CreatTBL@("STA") k CreatTBL
 i $E(BST)="@" s $E(BST)="" k @(BSR_"(BST,""STA"")") s BST="@"_BST
 d RESZER^%ZAPM.ed.BSF2,TAB^%ZAPM.ed.BSF1 q
 
BigEdit(Input,y1,x1,y2,x2,BadChar,Clr,Help,AltCmp) n Obraz s Obraz="BigEdit"
 N MouseYes
 i $G(%BS(13))'="%BS-PC" q $$LineEdit^%ZAPM.ed.BSXfun(.Input,"Введите стpоку :",.BadChar,"",.Help,.AltCmp)
 s Clr(0)=$P($G(Clr),"/",3) d CheckPar,SaveWin^%ZAPM.ed.BSXfun(y1-1,x1-1,y2+2,x2+3)
 i $G(Clr(0))="" s Clr(0)="1;6;37;40"
 d Ramka^%ZAPM.ed.BSXfun(y1-1,x1-1,y2+1,x2+1,1,Clr(0),1)
 s Input=$$Edit(.Input,y1,x1,y2,x2,.BadChar,Clr_"/"_Clr(1),.Help,.AltCmp)
 d LoadWin^%ZAPM.ed.BSXfun q Input
 
Edit(Input,y1,x1,y2,x2,BadChar,Clr,Help,AltCmp) 
 N MouseYes
 n LenW,HiW,Flag,cLen,MaxLen,Pos,Off,Max,Tmp,sp,Ins,Undo,i,Obraz
 d:$D(%BS)["0" CLr^%ZAPM.ed.BSF4 U $I::%BS(32)
 s Off=1,MaxLen=($$LNG^%ZAPM.ed.BSF12-1),Obraz="EditBar"
CheckPar s Input=$G(Input),BadChar=$G(BadChar),AltCmp=$G(AltCmp),Clr=$G(Clr)
 s Clr(1)=$P(Clr,"/",2) s YES="" i $G(%BS(20))'="" s YES=$G(@%BS(20)@(Obraz))
 i $G(y1)=""!($G(y2)="")!($G(x1)="")!($G(x2)="") d
 . i YES'="" s y1=$P(YES,"@",8)+1 d  i y1'>y2!(x1'>x2) q
 .. s x1=$P(YES,"@",9)+1,y2=$P(YES,"@",10)-1,x2=$P(YES,"@",11)-1
 . s (x1,y1,y2)=2,x2=79
 s Clr(2)="",Clr=$P(Clr,"/") i YES'="" s Clr(2)=$P(YES,"@",31)
 i +Clr(1)<32 s Clr(1)=$P(Clr(2),"/",11) i +Clr(1)<32 s Clr(1)=176
 i $G(Clr(0))="" s Clr(0)=$P(Clr(2),"/")
 i Clr="" s Clr=$P(Clr(2),"/",5) s:Clr="" Clr=Clr(0) i Clr="" s Clr="1;6;33;40"
 i $G(Help)="",YES'="" s Help=$P(YES,"@",39)
 i $G(AltCmp)="",YES'="" s AltCmp=$P(YES,"@",41)
 i Obraz="BigEdit" q
 k Clr(0) s LenW=x2-x1+1,HiW=y2-y1+1,Max=(LenW*HiW) d Echo^%ZAPM.ed.BSXfun(0)
 s cLen=$L(Input),sp=$TR($J("",Max)," ",$C(Clr(1))),Pos=cLen+(cLen<Max)
 s Undo=Input,Flag=1 f  d Check,ReDraw:Flag,Read i Flag<0 q
 q $E(Input,cLen>0,cLen)
 
ReDraw w $$atr^%ZAPM.ed.BS3(Clr),$$atr^%ZAPM.ed.BS3(6) s y2=1,x2=LenW,Tmp=$E(Input,Off,cLen)_sp
 f i=0:1:HiW-1 w /CUP(y1+i,x1),$E(Tmp,y2,x2) s y2=y2+LenW,x2=x2+LenW
 q
Read s Ins=$E($$GetLock^%ZAPM.ed.BSXfun()),Flag=Pos-Off,i=$E(Input,Pos)
 s y2=y1+(Flag\LenW),x2=x1+(Flag#LenW),Flag=0
 w /CUP(y2,x2),$$atr^%ZAPM.ed.BS3(Clr),$$atr^%ZAPM.ed.BS3(5+Ins),i,/CUP(y2,x2)
 x $$Read^%ZAPM.ed.BSXfun("^%ZAPM.ed.BSX(""keyB"")","","d ABC",AltCmp) i Flag q
 w $$atr^%ZAPM.ed.BS3(Clr),$$atr^%ZAPM.ed.BS3(6),i q
 
Check i Pos>MaxLen s Pos=MaxLen
 i Off>1 i Pos<(Off+Max-1) s Off=Pos-Max+1,Flag=1 i Off<1 s Off=1
 i Pos<Off s Off=Pos,Flag=1 i Off<1 s (Off,Pos)=1
 i Pos'<(Off+Max) s Off=Pos-Max+1,Flag=1 i Off>(MaxLen-Max+1) s Pos=MaxLen,Off=MaxLen-Max+1
 q
 
ABC i BadChar[$C(R1) w $$bel^%ZAPM.ed.BS3 q                   ; Ввод символа
 i Pos>(cLen+1) i BadChar[" " w $$bel^%ZAPM.ed.BS3 q
 i cLen'<MaxLen w $$bel^%ZAPM.ed.BS3 s $E(Input,MaxLen,cLen)="",cLen=MaxLen-1
 s Flag=1 i 'Ins g Zam
 i Pos>cLen s $E(Input,Pos)=$C(R1),cLen=Pos,Pos=Pos+1 q
 s $E(Input,Pos)=$C(R1)_$E(Input,Pos),cLen=cLen+1,Pos=Pos+1 q
Zam s $E(Input,Pos)=$C(R1),Pos=Pos+1 i Pos>cLen s cLen=Pos q
 q
 
Bksp i Pos<2 q
 s Pos=Pos-1 i 'Ins q:(Pos>cLen)!(BadChar[" ")  s $E(Input,Pos)=" ",Flag=1 q
Del q:(Pos>cLen)  s $E(Input,Pos)="",cLen=cLen-1,Flag=1 q
 
AltT s Tmp=$$History^%ZAPM.ed.BSXfun("^%ZAPM.ed.BSbufB","HISCTL"_$S('$E(%BS(1),12):$G(%BS(3),$P)_$G(%BS(12)),$E(%BS(1),12)=1:"1"_$G(%BS(12)),1:"1")) i 'YES q
 g InsToEL
ReadToEL i $G(^%ZAPM.ed.BSbufB("HISCTL"_$S('$E(%BS(1),12):$G(%BS(3),$P)_$G(%BS(12)),$E(%BS(1),12)=1:"1"_$G(%BS(12)),1:"1")))="" d  q
 . d ErrMsg^%ZAPM.ed.BSXfun("Нет запомненой стpоки.")
 s Tmp=$G(^%ZAPM.ed.BSbufB("HISCTL"_$S('$E(%BS(1),12):$G(%BS(3),$P)_$G(%BS(12)),$E(%BS(1),12)=1:"1"_$G(%BS(12)),1:"1"),1))
InsToEL i Ins s $E(Input,Pos)=Tmp_$E(Input,Pos)
 e  i Tmp'="" s $E(Input,Pos,Pos+$L(Tmp)-1)=Tmp
 s Input=$E($TR(Input,BadChar,""),1,MaxLen),cLen=$L(Input),Flag=1 q
 
Help i $G(Help)'="" d EnterTAB^%ZAPM.ed.BSXfun(Help) q  ; F1 помощь
 d ^%ZAPM.ed.BSX("TEXT","^%ZAPM.ed.BSX","HelpBar") q
