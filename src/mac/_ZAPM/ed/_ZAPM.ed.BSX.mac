ROUTINE %ZAPM.ed.BSX
%BSX(Mode,BSR,BST,BadChar) ; Вход в Текст (В.Лащев) ; 16:51   01.04.2001
 N MouseYes
 n Refr,Edit,Qu,Str,St,Col,Co,Tmp,Head,Bst,Bstr,MinStr,MaxStr,Up,Down,StrY,StY,MaxLen,Left,Right,Hi,Len,Info,Block,s1,s2,s3,BST0,InHead,Ed,View,Fk,AltCmp,Win,i,j,ReDraw,UpDn,ToEL,More,Ins,cLen,LocS,Tab
 i $D(%BS)[0 d CLr^%ZAPM.ed.BSF4,Registr(1) ;То же в %BSXprog
 U $I::%BS(32)
 d Echo^%ZAPM.ed.BSXfun(0)
 s UpDn(0)=$Y,UpDn(1)=$X w $$atr^%ZAPM.ed.BS3(0)
 d CheckMod i Edit=-1 g Out
 i $$NoHead() g Out
 i $$Registr(0) g Out
 i $$ChckHead()>0 g Out1
 d Locs0 f  q:(Edit<0)  d Locs,Save3Lin^%ZAPM.ed.BSXfun(23),@("^%ZAPM.ed.BSX"_$S(Edit:"edit",1:"view")),LoadScr
Out1 d Registr(-1)
Out w $$atr^%ZAPM.ed.BS3(0),/CUP(UpDn(0)+1,UpDn(1)+1) s $Y=UpDn(0),$X=UpDn(1) k li
 i Mode="LINE" k YES i $G(R1)=13 s YES=$G(@Bstr)
 i Mode="TEXT" s YES=+$G(Str) i $G(R1)'=13 s YES=-YES
e q
 
LoadScr d LoadWin^%ZAPM.ed.BSXfun i %BS(13)="%BS-PC" d Load3Lin^%ZAPM.ed.BSXfun(23) q
 d ClrBot^%ZAPM.ed.BSXfun q
 
CheckMod s Mode=$$ToUp^%ZAPM.ed.BSXfun(Mode),Edit=0
 i Mode'="TEXT",Mode'="PROG",Mode'="LINE",Mode'="EDIT" d  s Edit=-1 q
 .d ErrMsg^%ZAPM.ed.BSXfun(" Режимы ^%ZAPM.ed.BSX : ""TEXT"",""PROG"",""LINE"",""EDIT"" ")
 s Head="@BSR@(BST)"
 i Mode="LINE" s Edit=1
 i Mode="EDIT" s Edit=1,Mode="TEXT"
 i Mode="TEXT" s BST0=BST
 i Mode="PROG" s Win=$G(BST),Edit=1 d ChckName^%ZAPM.ed.BSXprog
 ;i '$$PassWord^%ZAPM.ed.BSXfun(2,"на вход в текст") s Edit=-1
 q
 
Locs0 s (Up,Str,Col)=1 s Ed=+$G(Ed) ; локали
 s (Block,Block(0,0),Block(0,1),Block(1,0),Block(1,1))=0
 i Mode="PROG" d
 . s (Str,Col)=2
 . s Tmp=$$StatNode^%ZAPM.ed.BSXedit() i Tmp="" q
 . i +$P(Win,",")>0 s $P(@Tmp,"@")=+$P(Win,","),$P(@Tmp,"@",3)=+$P(Win,",")
 . i +$P(Win,",",2)>0 s $P(@Tmp,"@",2)=+$P(Win,",",2)
 . s $P(@Tmp,"@",4)=1
 q
 
Locs n H,i                                      ; локали Общие
 s H=$G(@Head),(YES,Err)="" i $G(%BS(20))'="" d
 . s YES=$G(@%BS(20)@($S(Mode="PROG":"ProgEdit",1:"TextEdit")))
 s MaxLen=0 ;```$P(H,"@",29) i MaxLen="" s MaxLen=$P(YES,"@",29)
 s MaxLen=+MaxLen i MaxLen>($$LNG^%ZAPM.ed.BSF12-1)!(MaxLen<1) s MaxLen=($$LNG^%ZAPM.ed.BSF12-1)
 s Tmp=$P(H,"@",23) i Tmp="" s Tmp=$P(YES,"@",23)
 i +$E(Tmp) s Ins=($E(Tmp)=2) d SetLock^%ZAPM.ed.BSXfun(Ins_$E($$GetLock^%ZAPM.ed.BSXfun(),2,8))
 s Tab=+$E(Tmp,5,6) i Tab<1!(Tab>MaxLen) s Tab=10
 d ReadFk s AltCmp=$P(H,"@",41) i AltCmp="" s AltCmp=$P(YES,"@",41)
 s ToEL="^%ZAPM.ed.BSbufB(""HISCTL"_$S('$E(%BS(1),12):$G(%BS(3),$P)_$G(%BS(12)),$E(%BS(1),12)=1:"1"_$G(%BS(12)),1:"1")_""")"
 s Bstr="@Head@(Str)",Bst="@Head@(St)"
 s ReDraw="d WrStr(St-Up,"""_Bstr_""",Win(14),Win(15),.St) "
 s ReDraw=ReDraw_" d WrStr(Str-Up,"""_Bstr_""",Win(12),Win(16),.Str) "
 
 s Tmp=$P(H,"@",31) ;окно/блок/стpока/инфо/текст/метка/меткаВстpоке////заполнение
 f i=1:1:10 s Win(9+i)=$P(Tmp,"/",i)
 s Win=$P(Tmp,"/",11),Tmp=$P(YES,"@",31)
 i Win="" s Win=$P(Tmp,"/",11)
 s Win=+Win i Win<32!(Win>255) s Win=32
 s Win=$TR($J("",80)," ",$C(Win))
 i Win(10)="" f i=1:1:10 s Win(9+i)=$P(Tmp,"/",i)
 i Win(10)="" d  s Win(10)="1;6;36;44"
 . f i=10:1:19 s Win(i)=""
 i Win(11)="" s Win(11)=$P(%BS,"!",3)
 i Win(13)="" s Win(13)=Win(10)_$S($F(Win(10),"33"):";37;1",1:";33;1")
 i Win(14)="" s Win(14)=Win(10)
 i Win(12)="" s Win(12)=Win(14)
 i Win(15)="" s Win(15)=Win(10)_$S($F(Win(10),"32"):";37;1",1:";32;1")
 i Win(16)="" s Win(16)=Win(12)_$S($F(Win(12),"32"):";37;1",1:";32;1")
 i $G(%BS(20))'="" s Win(19)=$P($G(@%BS(20)@("TextLine")),"@",31)
 I $E(%BS(1),19) f i=1,3:1:10 s Win(9+i)=$P(%BS,"!",$E(%BS(1),19))
 i $G(Win(19))="" s Win(19)="1;6;37;42//////////176"
 s Win(19)="@@@@@"_Win(19)
 s Tmp=$P(H,"@",38) i Tmp="" s Tmp=$P(YES,"@",38) i Tmp="" s Tmp=1
 i Tmp'=1,Tmp'=2 s Tmp=" "
 s Win(20)=$S(Tmp=1:"|",Tmp=2:"|",1:" ")
 s Win(21)=$S(Tmp=1:"|",Tmp=2:"|",1:"[")
 s Win(22)=$S(Tmp=1:"|",Tmp=2:"|",1:"]")
 s Win(23)=$S(Tmp=1:"-",Tmp=2:"=",1:" ")
 s Win(24)=Tmp
 i Win(12)=Win(14),Win(16)=Win(15) s ReDraw=""
 s ReDraw="i St'=Str "_ReDraw_"s St=Str"
 
 n y1,y2,x1,x2
 s y1=$P(H,"@",8),x1=$P(H,"@",9),y2=$P(H,"@",10),x2=$P(H,"@",11)
 i y1=""!(y2="")!(x1="")!(x2="") d
 . i YES'="" s y1=$P(YES,"@",8) d  i y1'=""&(y2'="")&(x1'="")&(x2'="") q
 .. s x1=$P(YES,"@",9),y2=$P(YES,"@",10),x2=$P(YES,"@",11)
 . s (x1,y1)=2,y2=21,x2=79
 s $P(H,"@",8,11)=y1_"@"_x1_"@"_y2_"@"_x2
 i $$BsxWin^%ZAPM.ed.BSXuse("H")
 s Win(0,0)=+$P(H,"@",8),Win(0,1)=+$P(H,"@",9)
 s Win(1,0)=+$P(H,"@",10),Win(1,1)=+$P(H,"@",11)
 i Mode="PROG" d
 . s i=0,Tmp="" f  s Tmp=$O(@BSR@(Tmp)) q:Tmp=""  i $D(@BSR@(Tmp))>9 s i=i+1
 . i $D(@Head)>9 s i=i-1
 . s Win(0,0)=Win(0,0)+i,Win(0,1)=Win(0,1)+i
 
 s UpDn=0,Win(3,0)=Win(0,0)+1,Win(3,1)=Win(0,1)+1,Win(2,0)=Win(1,0)
 s Hi=Win(1,0)-Win(3,0)-1,Len=Win(1,1)-Win(3,1)-1
 s Info="00000000",(More(0),More(1),More(2),More(3))=""
 i Hi'<0,(Len'<0) d
 . s i=Len+1,Info=$P(@Head,"@",13) i Info="" s Info=$P(YES,"@",13)
 . f Tmp=1:1:8 i $E(Info,Tmp)'=0,$E(Info,Tmp)'=1 d
 .. s $E(Info,Tmp)=$E("11111100",Tmp)
 . i '$E(Info) q
 . i $E(Info,2) s $E(Info,2)=(Len>(3+(2*$E(Info,5))))
 . f Tmp=3:1:7 s j=+$P("3,12,1,7,5,5",",",Tmp-2) d
 .. i i<j s $E(Info,Tmp)=0 q
 .. i $E(Info,Tmp) s i=i-j-1
 i '$E(Info) d
 . s Info="00000000",Hi=Hi+2,Len=Len+2
 . s Win(3,1)=Win(3,1)-1,Win(3,0)=Win(3,0)-1
 e  d
 . s Win(2,3)=Win(0,1)+1,Win(2,5)=Win(1,1)-1 ; pедакция,стpелки
 . s Win(2,4)=Win(2,3)+(4*$E(Info,3)) ; кооpд
 . s Win(2,8)=Win(2,4)+(13*$E(Info,4)) ; длина
 . s Win(2,6)=Win(2,5)-8+(2*'$E(Info,5))
 . s Win(2,7)=Win(2,6)-6+(8*'$E(Info,6))
 s Left=1,Right=Left+Len s Win=$E(Win,1,1+Len)
 q
 
DelKills n DelBuf,Head0                   ; удалить буффеp Delete
 s DelBuf=$$KillsBuf(),Head0=$G(@DelBuf)
 k @DelBuf s @DelBuf=Head0,@($P(DelBuf,")")_",1)")=""
 q
 
KillsBuf() q "^%ZAPM.ed.BSbufB(2,"_$G(%BS(3),$P)_",4,3)"
 
Registr(vhod) n Out ; входили ли в текст
 i vhod>0 d SetLock^%ZAPM.ed.BSXfun("1"_$E($$GetLock^%ZAPM.ed.BSXfun(),2,8)) w $$bel^%ZAPM.ed.BS3
 s Out=0 i vhod'>0 d:vhod=0 IsView i Mode="LINE" g QRegis
 s Out=$$DoRegis()
QRegis i vhod=0 q Out
 q
DoRegis() n Txt,Node s Node="^%ZAPM.ed.BSbufB(2,$G(%BS(3),$P),5,""Reg"")"
 i vhod>0 k @Node q 0
 s Txt=$$BSR^%ZAPM.ed.BSA($NA(@Head))
 i vhod<0 k @Node@(Txt) q 0
 i $D(@Node@(Txt))'[0 d  q 1
 .d ErrMsg^%ZAPM.ed.BSXfun(" The second time in the text '"_$NA(@Head)_"' cannot be entered. ") q
 s @Node@(Txt)=$$UserNAME^%ZAPM.ed.BSXfun() q 0
 
NoHead() i Mode="TEXT",$D(@Head)[0 d  q 1 ; есть ли текст
 .d ErrMsg^%ZAPM.ed.BSXfun(" Нет текста '"_$NA(@Head)_"' ")
 q 0
 
ChckHead() n H,i,col,atr,y,x,y1,x1,Err      ; пpовеpка/pемонт заголовка
 s H=$G(@Head),Err=""
 f i=1,2,4,5 s col=$P(H,"@",i) d
 . i $L(col)>80 s $P(H,"@",i)=$E($P(col,"@",i),1,80) d ErrHead("поле:"_i)
 s y=$P(H,"@",8),x=$P(H,"@",9),y1=$P(H,"@",10),x1=$P(H,"@",11)
 i y'=""!(x'="")!(y1'="")!(x1'="") d
 . i y<1!(x<1)!(y>25)!(x>80)!(y1<y)!(y1>25)!(x1<x)!(x1>80) d ErrHead("Y1 X1 Y2 X2") s $P(H,"@",8,11)="@@@"
 s x=$P(H,"@",15) i x'="",x'=0,x'=1,x'["^" s $P(H,"@",15)="" d ErrHead("Fk")
 i $P(H,"@",17)'=5 s $P(H,"@",17)=5 d ErrHead("Тип")
 s col=$P(H,"@",29) i col'="" i +col>($$LNG^%ZAPM.ed.BSF12-1)!(+col<1) s $P(H,"@",29)=($$LNG^%ZAPM.ed.BSF12-1) d ErrHead("MaxLen")
 s col=$P(H,"@",31)
 f x=1:1:10 s y=$P(col,"/",x) i y'="" d
 . s x1=0 f i=$L(y,";"):-1:1 d  i x1 q
 .. s atr=+$P(y,";",i)
 .. i atr<0!(atr=3)!((atr>8)&(atr<30))!((atr>37)&(atr<40))!(atr>47) s x1=1
 . i x1 s $P(col,"/",x)="" d ErrHead("Clr"_x)
 s atr=$P(col,"/",11) i atr'="" i +atr<32!(+atr>256) d ErrHead("Заполнение") s atr=""
 i $P(col,"/",11)'=atr s $P(col,"/",11)=atr
 i $P(H,"@",31)'=col s $P(H,"@",31)=col
 s atr=$P(H,"@",38) i atr'=1,(atr'=2),(atr'=" "),(atr'="") s $P(H,"@",38)="" d ErrHead("Ramka")
 s col=$P(H,"@",41) i col'="",col'["^" d ErrHead("Пpедобpаботка")
 s YES=0 i @Head'=H d
 . i View d ErrMsg^%ZAPM.ed.BSXfun("Ошибка заголовка в '"_$NA(@Head)_"'. ( а текст недоступен ! )") s YES=1 q
 . i (Mode'="PROG")&(Mode'="LINE") d Yes^%ZAPM.ed.BSXfun(" Устpанять ошибку заголовка '"_$NA(@Head)_"' в"_Err_" ") i YES<1 s YES=1 q
 . d R^%ZAPM.ed.BS3($NA(@Head)) s @Head=H d D^%ZAPM.ed.BS3($NA(@Head)) s YES=-1,Ed=1 q
 q YES
ErrHead(Pole) s Err=Err_" "_Pole q
 
AltF w $$bel^%ZAPM.ed.BS3 i Fk'=1 s Fk=1 q
ReadFk s Tmp=$P(@Head,"@",15) i Tmp="" d  i Tmp="" s Tmp=1
 . i $G(%BS(20))'="" s Tmp=$P($G(@%BS(20)@($S(Mode="PROG":"ProgEdit",1:"TextEdit"))),"@",15)
 s Fk=$S((Tmp["^")!(Tmp=1):Tmp,1:0) q
 
BusyV(Vhod,NoReg) n Name s Qu=0 i View,'Vhod q
 d R^%ZAPM.ed.BS3($NA(@Head)) s Name=$$UserNAME^%ZAPM.ed.BSXfun()
 i Vhod s Qu=$P(@Head,"@",30) d
 . i Qu'="",Qu'=Name d  I Qu'=Name s Edit=-1,Qu=1 q         ; текст занят
 .. d Yes^%ZAPM.ed.BSXfun("'"_$NA(@Head)_"' pедактиpует пользователь "_Qu_".Игнорируем ?",2,"Утвердительный ответ может давать только Администратор!")
 .. I YES>0 S Qu=Name,$P(@Head,"@",30)=Qu
 . s Qu=0
 i Qu=0,'$G(NoReg),'View s Qu="@Head@(""User"",Name)" d  s Qu=0
 . i 'Vhod k @Qu
 . e  d RegView("")
 d D^%ZAPM.ed.BS3($NA(@Head)) q
 
BusyE(Vhod) n Name i View d ErrMsg^%ZAPM.ed.BSXfun("Текст '"_$NA(@Head)_"' недоступен.") s Qu=1 q
 i Vhod d BusyV(Vhod,1) i Qu q
 d R^%ZAPM.ed.BS3($NA(@Head))
 i 'Vhod s Qu=0 s:Mode'="PROG" $P(@Head,"@",30)=""
 e  s Qu="@Head@(""User"")",Name=$$UserNAME^%ZAPM.ed.BSXfun() d
 . n User i $L($D(@Qu))>1 d  i Qu d ErrMsg^%ZAPM.ed.BSXfun(" Текст '"_$NA(@Head)_"' смотpит пользователь : "_User) q
 .. s User=$$GetIndex^%ZAPM.ed.BSXfun1($ZO(@Qu))  ; Пpовеpка на пользователей
 .. i User'=Name s Qu=1 q
 .. k @$ZO(@Qu) i $L($D(@Qu))<2 s Qu=0 q
 .. s User=$$GetIndex^%ZAPM.ed.BSXfun1($ZO(@Qu)),Qu=1
 . i Mode'="PROG" s $P(@Head,"@",30)=Name           ; заняли текст
 d D^%ZAPM.ed.BS3($NA(@Head)) q
 
RegView(H) n zt s zt=$ZT,$ZT="Prot^%ZAPM.ed.BSX",@Qu=H
ProtC s $ZT=$G(zt),Qu=0 q
Prot s View=1 g ProtC             ; только VIEWER
IsView s View=0,Qu=Head d R^%ZAPM.ed.BS3($NA(@Head)),RegView($G(@Head)),D^%ZAPM.ed.BS3($NA(@Head)) q
