ROUTINE %ZAPM.bs.BSDIAL
%BSDIAL ;``` (А.Нефедов) ; 16:57 10-JUN-98
 ;
 S SystemMSG=0,UserMSG=1000
 S MSGCreate=SystemMSG+1
 S MSGDestroy=SystemMSG+2
 S MSGPaint=SystemMSG+3
 S MSGKeyPress=SystemMSG+4
 S MSGChFocus=SystemMSG+5
 S MSGSetCursor=SystemMSG+6
 S MSGUnCheck=SystemMSG+7
 S MSGIdle=SystemMSG+8
 S MSGDelCtrl=SystemMSG+9
 S MSGCrFcs=SystemMSG+10
 S MSGAddCtrl=SystemMSG+11
 S MSGGetCtrl=SystemMSG+12
 S ControlFunc("TDialog")="DialogFunc"
 S ControlFunc("TControl")="CtrlFunc"
 S ControlFunc("TLabel")="LabelFunc"
 S ControlFunc("TButton")="ButtonFunc"
 S ControlFunc("TCheck")="CheckBut"
 S ControlFunc("TRadio")="RadioBut"
 S ControlFunc("TEdit")="EditFunc"
 S ControlFunc("TClock")="ClockFunc"
 ;
 ; Now, we have to declare basic class
 ;
 ; ClassName@Left@Top@Width@Height@Text@Enable@Visible@Color@Hint@HintColor@
 ; 1         2    3   4     5      6    7      8       9     10   11
 ; Alight@Focused@TabStop@TabOrder@CursorDef@EmptyChar@ForeColor@BackColor@
 ; 12     13      14      15       16           17        18        19
 ; FocusColor@ShadowColor1@ShadowColor2@Control1@Command@GroupNum@CanClose
 ; 20         21           22           23       24      25       26
 ; CursorDef1@CommitKey@ExMode@Control2@BackUpText
 ; 27         28        29     30       31
 ;
 D ^%ZAPM.bs.BSDIAL1
 Q
 
ExecDlg(DlgName,Name) 
 N R1,R2,R3,ComKey,Flag
 N Temp
 S R2=-1,R3=-1
 S ExecName=Name
 D Copy^%ZAPM.bs.BSF8(DlgName,Name)
 S $P(@Name,"@",7)=1
 D SendMessage(Name,MSGCreate)
 D SendMessage(Name,MSGPaint)
WHILE 
 S ComKey=$P(@Name,"@",28) S:ComKey="" ComKey=13
 D CL^%ZAPM.bs.BSF4
 F  R *R1:1 R:R1=27 *R2:0,*R3:0 D:(R1=-1) SendMessage(Name,MSGIdle) Q:(R1'=-1)!($P(Name,"@",7)=0)
 I ($P(@Name,"@",29)=1)&(R1=153) D
 .S ls="Read pattern again ?" D YES^%ZAPM.bs.BSF
 .D:YES=1
 ..D Copy^%ZAPM.bs.BSF8(DlgName,Name)
 ..S $P(@Name,"@",7)=1
 ..S $P(@Name,"@",29)=1
 ..D SendMessage(Name,MSGDestroy)
 ..D SendMessage(Name,MSGCreate)
 ..D SendMessage(Name,MSGPaint)
 ..Q
 .Q
 I ($P(@Name,"@",29)=1)&(R1=161) D
 .S ls="Write pattern ?" D YES^%ZAPM.bs.BSF D:YES=1
 ..D Copy^%ZAPM.bs.BSF8(Name,DlgName)
 ..Q
 .Q
 D:(R1'=-1) SendMessage(Name,MSGKeyPress,R1,R2,R3)
 S:$P(@Name,"@",7)=0 R1=ComKey
 G:R1'=ComKey&((R1'=27)!((R2'=-1)!(R3'=-1))) WHILE
 S Flag=1
 I (R1=ComKey) D
 .S Temp=$P(@Name,"@",26)
 .I (Temp'="")  I @(Temp_"=0") S Flag=0,$P(@Name,"@",7)=1 Q
 .S Temp=$P(@Name,"@",24)
 .X:Temp'="" Temp
 .Q
 G:Flag=0 WHILE
 D SendMessage(Name,MSGDestroy)
 K Temp
 K R1,R2,R3,ComKey,Flag
 D OP^%ZAPM.bs.BSF4
 Q
 
DoneDlg ;
 S $P(@ExecName,"@",7)=0
 Q
 
SendChil(Name,Message,Param1,Param2,Param3) 
 N Child,Name1
 S:$D(Param1)=0 Param1=""
 S:$D(Param2)=0 Param2=""
 S:$D(Param3)=0 Param3=""
 S Child=$O(@Name@(""))
 F  Q:Child=""  D
 .I $F(Name,")")=0 S Name1=Name_"("
 .S:'$T Name1=$E(Name,1,$L(Name)-1)_","
 .S Name1=Name1_""""_Child_""""_")"
 .D SendMessage(Name1,Message,.Param1,.Param2,.Param3)
 .S Child=$O(@Name@(Child))
 .Q
 K Child,Name1
 Q
 
SendGrou(Name,Message,Param1,Param2,Param3) 
 Q:$F(Name,"(")=0
 N Child,Name1,Temp1,Temp2
 S:$D(Param1)=0 Param1=""
 S:$D(Param2)=0 Param2=""
 S:$D(Param3)=0 Param3=""
 S Temp1=0,Temp2=1
 F  S Temp1=Temp2,Temp2=$F(Name,",",Temp2) Q:Temp2=0
 I Temp1'=1 S Name=$E(Name,1,Temp1-2)_")"
 S:'$T Name=$E(Name,1,$F(Name,"(")-2)
 S Child=$O(@Name@(""))
 F  Q:Child=""  D
 .I $F(Name,")")=0 S Name1=Name_"("
 .S:'$T Name1=$E(Name,1,$L(Name)-1)_","
 .S Name1=Name1_""""_Child_""""_")"
 .D SendMessage(Name1,Message,.Param1,.Param2,.Param3)
 .S Child=$O(@Name@(Child))
 .Q
 K Child,Name1,Temp1,Temp2
 Q
 
SendMess(Name,Message,Param1,Param2,Param3) 
 N ClassName,FuncName
 S:$D(Param1)=0 Param1=""
 S:$D(Param2)=0 Param2=""
 S:$D(Param3)=0 Param3=""
 S ClassName=$P(@Name,"@")
 S FuncName=$G(ControlFunc(ClassName))
 D:FuncName'="" @(FuncName)(Name,Message,.Param1,.Param2,.Param3)
 K ClassName,FuncName
 Q
 
ParentCo(Parent,Name,Message,Param1,Param2,Param3) 
 N ClassName,FuncName
 S:$D(Param1)=0 Param1=""
 S:$D(Param2)=0 Param2=""
 S:$D(Param3)=0 Param3=""
 S ClassName=Parent
 S FuncName=$G(ControlFunc(ClassName))
 D:FuncName'="" @(FuncName)(Name,Message,.Param1,.Param2,.Param3)
 K ClassName,FuncName
 Q
 
DialogFu(Name,Message,Param1,Param2,Param3) 
 N X,Y,StartX,StartY,EndX,EndY,Char,Z,Temp
 I Message=MSGCreate D
 .D SA^%ZAPM.bs.BSsBUF W $$atr^%ZAPM.bs.BS3(0)
 .S $P(@Name,"@",29)=0
 .D SendChildren(Name,Message,.Param1,.Param2,.Param3)
 .S Param1=1
 .D SendChildren(Name,MSGCrFcs,.Param1,0)
 .S $P(@Name,"@",30)=Param1
 .D SendChildren(Name,MSGChFocus,1,0)
 .Q
 I Message=MSGDestroy D
 .D SendChildren(Name,Message,.Param1,.Param2,.Param3)
 .S $P(@DlgName,"@",29)=0
 .D RE^%ZAPM.bs.BSsBUF W $$atr^%ZAPM.bs.BS3(0)
 .Q
 I $P(@Name,"@",8)=1&(Message=MSGPaint) D
 .D RE^%ZAPM.bs.BSsBUF W $$atr^%ZAPM.bs.BS3(0)
 .D SA^%ZAPM.bs.BSsBUF W $$atr^%ZAPM.bs.BS3(0)
 .S StartX=$P(@Name,"@",2),StartY=$P(@Name,"@",3)
 .S EndX=StartX+$P(@Name,"@",4),EndY=StartY+$P(@Name,"@",5)
 .S Char=$P(@Name,"@",17)
 .W $$Color($P(@Name,"@",18),$P(@Name,"@",19)),/CUP(StartY,StartX),"-"
 .F X=StartX+1:1:EndX-1 W "-"
 .W "-" ;$$Color($P(@Name,"@",21),$P(@Name,"@",22)),"_"
 .F Y=StartY+1:1:EndY-1 D
 .. W /CUP(Y,StartX),$$Color($P(@Name,"@",18),$P(@Name,"@",19)),"|"
 ..F X=StartX+1:1:EndX-1 W Char
 ..W "|",$$Color($P(@Name,"@",21),$P(@Name,"@",22)),"¶"
 ..Q
 .W /CUP(EndY,StartX),$$Color($P(@Name,"@",18),$P(@Name,"@",19)),"-"
 .F X=StartX+1:1:EndX-1 W "-"
 .W "-",$$Color($P(@Name,"@",21),$P(@Name,"@",22)),"¶",/CUP(EndY+1,StartX+1)
 .F X=StartX:1:EndX W "®"
 .W $$Color($P(@Name,"@",18),$P(@Name,"@",19))
 .S Char=" "_$P(@Name,"@",6)_" ";
 .S:$P(@Name,"@",29)'=0 Char=Char_"[EditMode]"
 .S X=EndX-StartX,Y=$L(Char)
 .I Y>X S Char=$E(Char,1,X-1),Y=X-1
 . W /CUP(StartY,StartX+((X-Y)\2)),Char
 .D SendChildren(Name,Message,.Param1,.Param2,.Param3)
 .D SendChildren(Name,MSGSetCursor)
 .Q
 I Message=MSGKeyPress D
 .D SendChildren(Name,Message,.Param1,.Param2,.Param3)
 .D:$P(@Name,"@",29)=0 SendChildren(Name,MSGSetCursor)
 .Q
 I (Message=MSGKeyPress)&($P(@Name,"@",29)'=0) D EditMode^%ZAPM.bs.BSDIAL1(Name,.Param1,.Param2,.Param3)
 I (Message=MSGKeyPress)&((Param1=9)!(Param1=15)!((Param1=27)&(Param2=91)&(Param3>64)&(Param3<69))) D
 .I (Param1=9)!(Param3=67)!(Param3=66) S Y=1
 .S:'$T Y=-1
 .S Z=$P(@Name,"@",15),X=$P(@Name,"@",15)+Y
 .S:X<0 X=$P(@Name,"@",30)-1
 .S:X=$P(@Name,"@",30) X=0
 .S $P(@Name,"@",15)=X
 .D SendChildren(Name,MSGChFocus,X,$P(@Name,"@",29))
 .D:X=0
 ..S Char=$P(@Name,"@",10),X=$L(Char)
 ..F Y=1:1:(80-X)/2 S Char=" "_Char_" "
 ..W /CUP(24,1),$$Color($P(@Name,"@",11)),Char
 ..Q
 .D:(Z=0)!($P(@Name,"@",15)=0)
 ..S StartX=$P(@Name,"@",2),StartY=$P(@Name,"@",3)
 ..S EndX=StartX+$P(@Name,"@",4),EndY=StartY+$P(@Name,"@",5)
 ..W:X=0 $$Color($P(@Name,"@",20),$P(@Name,"@",19))
 ..W:X'=0 $$Color($P(@Name,"@",18),$P(@Name,"@",19))
 ..S Char=" "_$P(@Name,"@",6)_" "
 ..S:$P(@Name,"@",29)'=0 Char=Char_"[EditMode]"
 ..S X=EndX-StartX,Y=$L(Char)
 ..I Y>X S Char=$E(Char,1,X-1),Y=X-1
 ..W /CUP(StartY,StartX+((X-Y)\2)),Char
 ..Q
 .D SendChildren(Name,MSGSetCursor)
 .Q
 I (Message=MSGKeyPress)&(Param1=33) D
 .S $P(@Name,"@",29)=1-$P(@Name,"@",29)
 .S Param1=1
 .D SendChildren(Name,MSGCrFcs,.Param1,$P(@Name,"@",29))
 .S $P(@Name,"@",30)=Param1
 .S $P(@Name,"@",15)=1
 .D SendChildren(Name,MSGChFocus,1,$P(@Name,"@",29))
 .D SendMessage(Name,MSGPaint)
 .Q
 I Message=MSGIdle D
 .D SendChildren(Name,Message,.Param1,.Param2,.Param3)
 .Q
 I Message=MSGDelCtrl D
 .D SendChildren(Name,Message,.Param1,.Param2,.Param3)
 .S Param1=1
 .D SendChildren(Name,MSGCrFcs,.Param1,1)
 .S $P(@Name,"@",30)=Param1
 .S $P(@Name,"@",15)=1
 .D SendChildren(Name,MSGChFocus,1,$P(@Name,"@",29))
 .D SendMessage(Name,MSGPaint)
 .Q
 I Message=MSGAddCtrl D
 .S Temp=Param1
 .F X=1:1:20 Q:$D(@Name@(Temp_X))=0
 .;M @Name@(Temp_X)=@Param2
 .D Copy^%ZAPM.bs.BSF8(Param2,$NA(@Name@(Temp_X)))
 .S Param1=1
 .D SendChildren(Name,MSGCrFcs,.Param1,1)
 .S $P(@Name,"@",30)=Param1
 .S $P(@Name,"@",15)=1
 .D SendChildren(Name,MSGChFocus,1,$P(@Name,"@",29))
 .D SendMessage(Name,MSGPaint)
 .Q
 I Message=MSGGetCtrl D
 .I $P(@Name,"@",15)=0 S Param1=Name
 .D:'$T SendChildren(Name,Message,.Param1,.Param2,.Param3)
 .Q
 K X,Y,StartX,StartY,EndX,EndY,Char,Z,Temp
 Q
 
LabelFun(Name,Message,Param1,Param2,Param3) 
 D ParentControl("TControl",Name,Message,.Param1,.Param2,.Param3)
 Q:Message=MSGDelCtrl
 N StartX,StartY,Char,AType,Temp
 I $P(@Name,"@",8)=1&(Message=MSGPaint) D
 .S StartX=$P(@Name,"@",2),StartY=$P(@Name,"@",3)
 .S Char=$P(@Name,"@",6)
 .S Temp=0
 .S AType=$P(@Name,"@",12)
 .D:AType=3
 ..S Temp=($P(@Name,"@",4)-$L(Char))\2 S:Temp<0 Temp=0
 ..Q
 .D:AType=1
 ..S Temp=0
 ..Q
 .D:AType=2
 ..S Temp=$P(@Name,"@",4)-$L(Char) S:Temp<0 Temp=0
 ..Q
 .S StartX=StartX+Temp
 .S $P(@Name,"@",16)=StartX
 .W /CUP(StartY,StartX),Char
 .S $P(@Name,"@",27)=$X
 .Q
 I (Message=MSGSetCursor)&($P(@Name,"@",13)=1) D
 .W /CUP($P(@Name,"@",3),$P(@Name,"@",16))
 .Q
 K StartX,StartY,Char,AType,Temp
 Q
 
ButtonFu(Name,Message,Param1,Param2,Param3) 
 D ParentControl("TLabel",Name,Message,.Param1,.Param2,.Param3)
 Q:Message=MSGDelCtrl
 N Temp,StartX,StartY,EndX,EndY,IsPress,ComKey
 I Message=MSGPaint D
 .S IsPress=$P(@Name,"@",23),Temp=" "
 .S:IsPress'=1 Temp="®"
 .S StartX=$P(@Name,"@",2),StartY=$P(@Name,"@",3)
 .S EndX=StartX+$P(@Name,"@",4),EndY=StartY+$P(@Name,"@",5)
 .W /CUP(EndY-1,EndX),$$Color($P(@Name,"@",21),$P(@Name,"@",22))
 .W:IsPress'=1 "_"
 .W /CUP(StartY+1,StartX+1),$TR($J("",$P(@Name,"@",4))," ",Temp)
 .W:IsPress=1 /CUP(StartY,StartX-1)," ",/CUP(StartY+1,StartX)," "
 .Q
 S ComKey=$P(@DlgName,"@",28) S:ComKey="" ComKey=32
 I $P(@Name,"@",13)=1&(Message=MSGKeyPress)&(Param1=ComKey) D
 .S $P(@Name,"@",23)=1
 .S StartX=$P(@Name,"@",2),$P(@Name,"@",2)=StartX+1
 .D SendMessage(Name,MSGPaint)
 .W $$rt^%ZAPM.bs.BS3(0.7)
 .S $P(@Name,"@",2)=StartX
 .S $P(@Name,"@",23)=0
 .D SendMessage(Name,MSGPaint)
 .S Temp=$P(@Name,"@",24)
 .X:Temp'=0 Temp
 .Q
 I (Message=MSGSetCursor)&($P(@Name,"@",13)=1) D
 .W /CUP($P(@Name,"@",3),$P(@Name,"@",16))
 .Q
 K Temp,StartX,StartY,EndX,EndY,IsPress,ComKey
 Q
 
CheckBut(Name,Message,Param1,Param2,Param3) 
 D ParentControl("TLabel",Name,Message,.Param1,.Param2,.Param3)
 Q:Message=MSGDelCtrl
 N Temp,StartX,StartY,EndX,EndY,IsChecked,ComKey,Char
 I Message=MSGCreate D
 .S Temp=" "
 .S:$P(@Name,"@",23)=1 Temp="X"
 .S Char=$P(@Name,"@",6)
 .S $P(@Name,"@",6)=$E(Char,1,$F(Char,"[")-1)_Temp_$E(Char,$F(Char,"]")-1,$L(Char))
 .Q
 S ComKey=$P(@DlgName,"@",28) S:ComKey="" ComKey=32
 I $P(@Name,"@",13)=1&(Message=MSGKeyPress)&(Param1=ComKey) D
 .S IsChecked=$P(@Name,"@",23)
 .S:IsChecked="" IsChecked=0
 .S IsChecked=1-IsChecked
 .S $P(@Name,"@",23)=IsChecked
 .S Temp=" "
 .S:IsChecked Temp="X"
 .S Char=$P(@Name,"@",6)
 .S $P(@Name,"@",6)=$E(Char,1,$F(Char,"[")-1)_Temp_$E(Char,$F(Char,"]")-1,$L(Char))
 .D SendMessage(Name,MSGPaint)
 .Q
 I (Message=MSGSetCursor)&($P(@Name,"@",13)=1) D
 .W /CUP($P(@Name,"@",3),$P(@Name,"@",16)+$F($P(@Name,"@",6),"[")-1)
 .Q
 K Temp,StartX,StartY,EndX,EndY,IsChecked,ComKey,Char
 Q
 
RadioBut(Name,Message,Param1,Param2,Param3) 
 D ParentControl("TLabel",Name,Message,.Param1,.Param2,.Param3)
 Q:Message=MSGDelCtrl
 N Temp,StartX,StartY,EndX,EndY,IsChecked,ComKey,Char
 I Message=MSGCreate D
 .S Temp=" "
 .S:$P(@Name,"@",23)=1 Temp="*"
 .S Char=$P(@Name,"@",6)
 .S $P(@Name,"@",6)=$E(Char,1,$F(Char,"(")-1)_Temp_$E(Char,$F(Char,")")-1,$L(Char))
 .Q
 S ComKey=$P(@DlgName,"@",28) S:ComKey="" ComKey=32
 I $P(@Name,"@",13)=1&(Message=MSGKeyPress)&(Param1=ComKey) D
 .S IsChecked=1 ;This is a radiobutton
 .S $P(@Name,"@",23)=IsChecked
 .S Temp="*"
 .S Char=$P(@Name,"@",6)
 .S $P(@Name,"@",6)=$E(Char,1,$F(Char,"(")-1)_Temp_$E(Char,$F(Char,")")-1,$L(Char))
 .D SendMessage(Name,MSGPaint)
 .D SendGroup(Name,MSGUnCheck,$P(@Name,"@",25));
 .Q
 I $P(@Name,"@",25)=Param1&(Message=MSGUnCheck)&($P(@Name,"@",13)'=1) D
 .S IsChecked=0 ;This is a radiobutton
 .S $P(@Name,"@",23)=IsChecked
 .S Temp=" "
 .S Char=$P(@Name,"@",6)
 .S $P(@Name,"@",6)=$E(Char,1,$F(Char,"(")-1)_Temp_$E(Char,$F(Char,")")-1,$L(Char))
 .D SendMessage(Name,MSGPaint)
 .Q
 I (Message=MSGSetCursor)&($P(@Name,"@",13)=1) D
 .W /CUP($P(@Name,"@",3),$P(@Name,"@",16)+$F($P(@Name,"@",6),"(")-1)
 .Q
 K Temp,StartX,StartY,EndX,EndY,IsChecked,ComKey,Char
 Q
 
EditFunc(Name,Message,Param1,Param2,Param3) 
 D ParentControl("TLabel",Name,Message,.Param1,.Param2,.Param3)
 Q:Message=MSGDelCtrl
 N Temp,StartX,StartY,EndX,EndY
 I $P(@Name,"@",13)=1&(Message=MSGKeyPress)&((Param1>31)!(Param1=8)) D
 .S Temp=$P(@Name,"@",6)
 .I Param1=8 S:$L(Temp)>0 Temp=$E(Temp,1,$L(Temp)-1)
 .S:('$T)&($L(Temp)<$P(@Name,"@",4)) Temp=Temp_$C(Param1)
 .S $P(@Name,"@",6)=Temp
 .D SendMessage(Name,MSGPaint)
 .Q
  I (Message=MSGSetCursor)&($P(@Name,"@",13)=1) D
 .W /CUP($P(@Name,"@",3),$P(@Name,"@",27)+1)
 .Q
 K Temp,StartX,StartY,EndX,EndY
 Q
 
ClockFun(Name,Message,Param1,Param2,Param3) 
 N X,Y
 S X=$X,Y=$Y
 D ParentControl("TLabel",Name,Message,.Param1,.Param2,.Param3)
 ;I Message=MSGPaint D SendGroup(Name,MSGSetCursor)
 I Message=MSGPaint W /CUP(Y+1,X+1)
 I Message=MSGIdle D
 .S $P(@Name,"@",6)=$P($$ESDAY^%ZAPM.bs.BSsFUNR(8,$$h^%ZAPM.bs.BS3())," ")
 .D SendMessage(Name,MSGPaint)
 .Q
 K X,Y
 Q
 
CtrlFunc(Name,Message,Param1,Param2,Param3) 
 Q:$P(@Name,"@",7)=0
 N Focus,Char,X,Y,StartX,StartY,Char1,F1
 I Message=MSGCreate D
 .Q
 I Message=MSGDestroy D
 .Q
 I $P(@Name,"@",8)=1&(Message=MSGPaint) D
 .S StartX=$P(@Name,"@",2),StartY=$P(@Name,"@",3)
 .S Char1=$TR($J("",$P(@Name,"@",4))," ",$P(@Name,"@",17))
 .I $P(@Name,"@",13)=1 D
 ..S Char=$P(@Name,"@",10),X=$L(Char)
 ..F Y=1:1:(80-X)/2 S Char=" "_Char_" "
 ..W /CUP(24,1),$$Color($P(@Name,"@",11)),Char
 ..W /CUP(StartY,StartX),$$Color($P(@Name,"@",20),$P(@Name,"@",19)),Char1
 ..Q
 .W:$P(@Name,"@",13)'=1 /CUP(StartY,StartX),$$Color($P(@Name,"@",18),$P(@Name,"@",19)),Char1
 .Q
 I (($P(@Name,"@",14)=1)!(Param2=1))&(Message=MSGChFocus) D
 .S Focus=0
 .I $P(@Name,"@",15)=Param1 S Focus=1
 .S F1=$P(@Name,"@",13)
 .S $P(@Name,"@",13)=Focus
 .D:F1'=Focus SendMessage(Name,MSGPaint)
 .Q
 I (($P(@Name,"@",14)=1)!(Param2=1))&(Message=MSGCrFcs) D
 .S $P(@Name,"@",15)=Param1
 .S Param1=Param1+1
 .Q
 I ($P(@Name,"@",13)=1)&(Message=MSGGetCtrl) D
 .S Param1=Name
 .Q
 I ($P(@Name,"@",13)=1)&(Message=MSGDelCtrl) D
 .K @Name
 .Q
 K Focus,Char,X,Y,StartX,StartY,Char1,F1
 Q
 
Color(ByteF,ByteB) 
 N Temp
 S Temp=0,Result=""
 S:ByteF>7 Temp=1,ByteF=ByteF-8
 S ByteF=ByteF+30
 S Result="$$atr^%ZAPM.bs.BS3("""_Temp_";"_ByteF_""")"
 D:$D(ByteB)'=0
 .S ByteB=ByteB+40
 .S Result=Result_",$$atr^%ZAPM.bs.BS3("""_ByteB_""")"
 .Q
 K Temp
 S Result="W "_Result
 X Result
 Q ""
