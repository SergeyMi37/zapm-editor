ROUTINE %ZAPM.bs.BSZRAPP
 U 0 W #," Восстановление в <",$$ZU^%ZAPM.bs.BS3(0),">. Все Фоновые MUMPS процесы Остановлены ? <Y> " R R H:"Yy"'[R ; ; 14:03  6-DEC-97
 W !!,"...ЗАГРУЗКА...",!,$P($T(%),";",2,999)
 ;
 ;            ПРОГРАММА ЗАГРУЗКИ ПРИЛОЖЕНИЯ КОМПЛЕКСА %BS
 ;             ПРОИЗВОДИТСЯ ИЗ %BSINST.PAC ИЛИ ^%PACKAGE
ZV ;                           ДЛЯ MSM4.0.14C
 ;
 I +$P($ZV,"Version ",2)<3 W !,"НЕСОВМЕСТИМЫЕ ВЕРСИИ !!! ВЕРСИЯ MSM ДОЛЖНА БЫТЬ НЕ НИЖЕ 3.0 !!!",*7 Q
 K NOMXTR,EST S $ZT="MX^BSZRAPP" D R^%ZAPM.bs.BS0("^TR")
 S ^TR("?")=$J(1,500) K ^TR("?") S $ZT="ERROR^BSZRAPP"
1 K ^UTILITY($J,"ROU"),^("GLO")
 F  U %DEV R %RN Q:%RN=";GLOBALS;"  D LOAD U 0 W "."
 U 0 D x^%ZAPM.bs.BS3(%RN)     ;---//--- для msm 4.x.x
 S (GLO,GL)="" F i=1:1 U %DEV R %G,%V Q:%G=""  S GL=$P(%G,"(") D  I '(i#50) U 0 W "*"
 .I GLO'=GL S ^UTILITY($J,"GLO",GL)=1,GLO=GL K @GLO
 .I $L(%V)<256 S @%G=%V Q
 .I '$D(NOMXTR) S @%G=%V Q
 .S EST=1
 C %DEV U 0 D TRANS,CHAR,INSTALL
 K ^UTILITY($J,"ROU"),^("GLO"),^BStranSU
 I $D(EST) W *7,!!," ВНИМАНИЕ !!! ИНСТАЛЛЯЦИЯ НЕУДАЧНАЯ ! В ВАШЕЙ СИСТЕМЕ НЕ ВЫСТАВЛЕН ФЛАГ,",!,"РАЗРЕШАЮЩИЙ ДАННЫЕ В ГЛОБАЛАХ ИМЕТЬ ДЛИНЕЕ 256 СИМВОЛОВ"
 Q
EXIT X "ZR  ZS BSZRAPP" H
MX U 0 W *7 S NOMXTR=1,$ZT="" G 1
ERROR W *7,!!," ВНИМАНИЕ !!! ОШИБКА ПРИ ИНСТАЛЛЯЦИИ",!!!,$$ErrSay^%ZAPM.bs.BSF8($ZE) Q
LOAD X "S ^UTILITY($J,""ROU"",%RN)=1 ZL  ZS @%RN" Q
INSTALL ;Выполнение программ
 I $D(^BStranSU("Install_R")) W !,"Выполнение программ..." S %RN="" F  S %RN=$O(^BStranSU("Install_R",%RN)) Q:%RN=""  S ROU=^BStranSU("Install_R",%RN) X ROU
 Q
CHAR ;Восстановление Характерристик
  I $D(^BStranSU("RightS")) W !,"Восстановление Характерристик массивов..." S %RN="" F  S %RN=$O(^BStranSU("RightS",%RN)) Q:%RN=""  S PRO=^BStranSU("RightS",%RN) I PRO'="4114",PRO>0 D CP^%ZAPM.bs.BSGCH(%RN,PRO) U 0 W "+"
 Q
TRANS ;ТрансляциЯ ссылок на КИП и ТОМ
 Q:'$D(^BStranSU)  K TRA F I=1:1 Q:'$D(^BStranSU(I))  S TRA(I)=^BStranSU(I,1),TRA(I,1)=^BStranSU(I,2),TRA(I,2)=^BStranSU(I,3)
 F i=1:1 Q:'$D(TRA(i))  S TRA(i,3)="["""_TRA(i,1)_""","""_TRA(i,2)_"""]",TRA(i,5)="["""_TRA(i,1)_"""]",TRA(i,6)="["""""_TRA(i,1)_"""""]",TRA(i,7)="["""""_TRA(i,1)_""""","""""_TRA(i,2)_"""""]" D
 .I TRA(i)="" S TRA(i,4)=$$ver(1)_""""_$P($$ZU^%ZAPM.bs.BS3(0),",")_""","""_$P($$ZU^%ZAPM.bs.BS3(0),",",2)_""""_$$ver(2),TRA(i,8)=$$ver(1)_""""""_$P($$ZU^%ZAPM.bs.BS3(0),",")_""""","""""_$P($$ZU^%ZAPM.bs.BS3(0),",",2)_""""""_$$ver(2) Q
TRAN .S COM=$$LineEdit^%ZAPM.bs.BSXfun("",TRA(i),"","","","","") I COM="" K TRA(i) Q
 .I COM'?3A1","3A W *7 G TRAN
 .S TRA(i,4)=$$ver(1)_""""_$P(COM,",")_""","""_$P(COM,",",2)_""""_$$ver(2),TRA(i,8)=$$ver(1)_""""""_$P(COM,",")_""""","""""_$P(COM,",",2)_""""""_$$ver(2) Q
 I $D(TRA) S I="" W !,"Трансляция ссылок в массивах..." F  S I=$O(^UTILITY($J,"GLO",I)) Q:I=""  S II=I D
 .I $D(@II)'["0" D TRA(II)
 .F ii=1:1 S II=$ZO(@II) Q:II=""  D TRA(II) I '(ii#50) U 0 W "§"
 Q
TRA(G) Q:@G'["["  F iii=1:1 Q:'$D(TRA(iii))  D
 .I @G[TRA(iii,3)  S @G=$$TR^%ZAPM.bs.BSsFUNM(@G,TRA(iii,3),TRA(iii,4))
 .I @G[TRA(iii,5)  S @G=$$TR^%ZAPM.bs.BSsFUNM(@G,TRA(iii,5),TRA(iii,4))
 .I @G[TRA(iii,6)  S @G=$$TR^%ZAPM.bs.BSsFUNM(@G,TRA(iii,6),TRA(iii,8))
 .I @G[TRA(iii,7)  S @G=$$TR^%ZAPM.bs.BSsFUNM(@G,TRA(iii,7),TRA(iii,8))
 Q
ver(V) i $P($ZV,"Version ",2)'<4 Q "|"
 Q $S(V=1:"[",1:"]")
