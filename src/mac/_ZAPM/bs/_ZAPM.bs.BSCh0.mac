ROUTINE %ZAPM.bs.BSCh0
%BSCh0 ;HTML; 12:48   05.01.00 ; 13:27   04.01.2002
 ;
 ; Copyright (C) Serge W. Mikhaylenko
 ; All Rights Reserved
 ;
TESTING D TEST("Тестирование ...") Q
VARTEST D TEST("Показать Локальные Переменные","") Q
BSG(bsg) ;ДИСПЕТЧЕР ПРОГРАММ %BS+++CACHE WEB-LINK
 s $zt="ErrBSG^%ZAPM.bs.BSCh0"
 S bsd=$$KBD^%ZAPM.bs.BSF12("^%ZAPM.bs.BSC(""BSG"")")
 I bsd="" W $$CRP("БАЗА ДАННЫХ ССЫЛКИ '"_bsg_"' НЕ ОПРЕДЕЛЕНА !") Q
 I '$D(@bsd@(bsg)) W $$CRP("ССЫЛКА '"_bsg_"' ПУСТАЯ !") Q
 S BSROU=$G(@bsd@(bsg,"ROU")),NSPACE=$G(@bsd@(bsg,"NSP")) ;,ROU=$P(BSROU,"^",2) /////
 I $G(MGWLIB)["mgwms32.dll" I $G(@bsd@(bsg,"MGWSTO")) S MGWSTO=+@$ZR I MGWSTO<120 S MGWSTO=120
 I $G(@bsd@(bsg,"BSdebug"))'="" S BSdebug=@$ZR //ОТЛАДЧИК ВКЛЮЧЕН
 I $G(NSPACE)'="",'$$ZU^%ZAPM.bs.BSF4(NSPACE) W $$CRP("Нет NameSpace='"_NSPACE_"' !") Q
 I '$$EXIST^%R($P(BSROU,"^",2)_".INT") W $$CRP("В NameSpace='"_$zu(5)_"' НЕТ ПРОГРАММЫ '^"_BSROU_"' !") Q
 S %KEY("BSNSPACE")=NSPACE,%KEY("BSROU")=BSROU
 D @BSROU
 Q
CRP(S) Q "<H2>"_S_"</H2>"
ErrHEAD ;ЗАГОЛОВОК
 S ErrorCount=$G(ErrorCount)+1 I ErrorCount>10 H  //Если зацикливание...
 w $c(13,10)," <STYLE TYPE=""text/css"">",$c(13,10)
 w "  DIV {color:black; }",$c(13,10)
 w "  .expandedVAR {color:black; }",$c(13,10)
 w "  .collapsedVAR {display:none}",$c(13,10)
 w " </STYLE>",$c(13,10)
 w " <SCRIPT LANGUAGE=""JavaScript"">",$c(13,10)
 w "  function outlinerVAR() {",$c(13,10)
 w "      var child =",$c(13,10)
 w "        document.all[event.srcElement.getAttribute(""child"",",$c(13,10)
 w "           false)];",$c(13,10)
 w "     if (null != child) ",$c(13,10)
 w "        child.className = child.className == ""collapsedVAR"" ?",$c(13,10)
 w "           ""expandedVAR"" : ""collapsedVAR"";",$c(13,10)
 w "  }",$c(13,10)
 w " </SCRIPT>",$c(13,10)
 W $$meta^%ZAPM.bs.BSC4,$c(13,10)
 Q
ErrTxt(ZE) Q "<STRONG><FONT TITLE='"_$$ErrSay^%ZAPM.bs.BSF8(ZE)_"' COLOR=RED>"_$$TRa(ZE)_"</FONT></STRONG>"
ErrBSG I $ZE["<WRITE" Q
 S CRLF=$C(13,10) D ErrHEAD
 W "<TABLE><TR><TD>ОШИБКА:</TD><TD>"_$$ErrTxt($ZE)_"</TD></TR>"
 D Tex^%ZAPM.bs.BSMSMF("Error Web="_$ze,$G(bsg,"?")_","_$G(BSROU)_","_$ZN_","_$ZU(5)_","_$G(%KEY("MGWLPN"),"?")_","_$s_","_$G(BSSES))
 W "<TR><TD>WEBLINK ПРИЛОЖЕНИЕ:</TD><TD>",$G(bsg,"?"),"</TD></TR>"
 W "<TR><TD>ВЫЗЫВАЕМАЯ ПРОГРАММА:</TD><TD>",$G(BSLABEL,"?"),"</TD></TR>"
 W "<TR><TD>ВЫЗЫВАЮЩАЯ ПРОГРАММА:</TD><TD>",$ZN,"</TD></TR>"
 W "<TR><TD>РАБОЧАЯ ОБЛАСТЬ:</TD><TD>",$ZU(5),"</TD></TR>"
 W "<TR><TD>ID ПРОЦЕССА:</TD><TD>",$J,"</TD></TR>"
 W "<TR><TD>ИМЯ СЕРВИСА:</TD><TD>",$G(%KEY("MGWLPN"),"?"),"</TD></TR>"
 W "<TR><TD>РАЗМЕР РАЗДЕЛА</TD><TD>",$S,"</TD></TR></TABLE>"
 D LOCLIST(1)
 Q
RUN ;ДИСПЕТЧЕР ПРОГРАММ %BS+CACHE WEB-LINK
 N ROU,RO,OldNS,NSPACE
 s $zt="ErrBSG^%ZAPM.bs.BSCh0" 
 S ROU=$$%^%ZAPM.bs.BSCh(%KEY("BSROU")),RO=$P(ROU,"^",2)
 I $D(%KEY("BSNSPACE")) S NSPACE=$$%^%ZAPM.bs.BSCh(%KEY("BSNSPACE"))
 s OldNS=$zu(5)
 I $G(NSPACE)'="",$zu(5,NSPACE)
 D
 .I '$D(^ROUTINE(RO)) W "В NSP='"_$zu(5)_"' НЕТ ПРОГРАММЫ '"_ROU_"' !" Q
 .D @ROU
 s OldNS=$zu(5,OldNS)
 Q
TRQ(S) ;ТРАНСЛИРОВАТЬ ""
 Q $$TR^%ZAPM.bs.BSsFUNM(S,$C(34),"&quot;")
TRa(S) ;ТРАНСЛИРОВАТЬ <>
 S S=$$TR^%ZAPM.bs.BSsFUNM(S,"<","&lt;")
 Q $$TR^%ZAPM.bs.BSsFUNM(S,">","&gt;")
Err I $ZE["<WRITE" Q
 W RED_$$TRa($ZE)_EF,$C(13,10)
 D TEST("Error Executing ! "_$ZT,$ze)
 Q
ERRSEND ;ПОСЫЛКА ОШИБКИ АДМИНИСТРАТОРУ
 N BD,U1,U2,U3,NuMHtml,CurUS
 D MAINVAR^%ZAPM.bs.BSCh1
 S BD=$$KBD^%ZAPM.bs.BSF12($$0^%ZAPM.bs.BSCh(24)) I '$$Data^%ZAPM.bs.BSF12(BD) Q
 S U1=$P($$h^%ZAPM.bs.BS3,","),U2=$P($$h^%ZAPM.bs.BS3,",",2)
 S @BD@(U1,CurUS,U2,"B")=$G(ErR)
 S @BD@(U1,CurUS,U2,"U")=$G(%BS(CurUS))
 S @BD@(U1,CurUS,U2,"T")="<ERROR>"
 S @BD@(U1,CurUS,U2,"A")=4
 Q
TEST(SaY,ErR) ; Simple test page
 D MAINVAR^%ZAPM.bs.BSCh1
 D CRP^%ZAPM.bs.BSCh(SaY,"<H3>Тестовая страница</H3>",4)
 D VAR^%ZAPM.bs.BSCh D ErrHEAD
 I $D(ErR) W RED_$$TRa(ErR)_EF D ERRSEND
 W "<HR>"
 W "НОМЕР ЗАДАНИЯ $J=",RED,$J,EF," НА М-СЕРВЕРЕ=",RED,$G(%KEY("MGWLPN")),EF," ТЕКУЩАЯ NameSpace=",RED,$zu(5),EF,BR,BK
 W "ТЕКУЩЕЕ УСТРОЙСТВО $I=",RED,$I,EF," В РАЗДЕЛЕ $S=",RED,$S,EF," БАЙТ",BK
 W " ВЫЗЫВАЕМАЯ ПРОГРАММА :",RED,$G(BSROU,"?"),$C(13,10),EF
 W " ВЫЗЫВЮЩАЯ ПРОГРАММА :",RED,$ZN,$C(13,10),EF
 I $D(ErR) G TEST2
 I '$D(CurUS)!('$D(%BS(CurUS)))!('$D(%BS(18)))!('$D(%BS(CurUS))) G TEST1
 I '$$ADMIN^%ZAPM.bs.BSCh G TEST1
TEST2 D LOCLIST(0)
 W "<H5 child='LOC2' onclick=""outlinerVAR();"">ПЕРЕМЕННЫЕ (%KEY(Node)=Value)</H5>",CRLF
 W "<DIV ID='LOC2' CLASS='collapsedVAR'>",CRLF
 W "<TABLE BORDER>",CRLF
 W "<TR><TH>Node</TH><TH>Value</TH></TR>",CRLF
 S KEY=""  F  S KEY=$O(%KEY(KEY)) Q:KEY=""  W "<TR><TD>",KEY,"</TD><TD>",$G(%KEY(KEY)),"</TD></TR>",CRLF
 W "</TABLE></DIV>",CRLF
 
 W "<H5 child='LOC3' ONCLICK=""outlinerVAR();"">ПЕРЕМЕННЫЕ CGI ОКРУЖЕНИЯ (%CGIEVAR(Node)=Value)</H5>",CRLF
 W "<DIV ID='LOC3' CLASS='collapsedVAR'>",CRLF
 W "<TABLE BORDER>",CRLF
 W "<TR><TH>Node</TH><TH>Value</TH></TR>",CRLF
 S KEY=""  F  S KEY=$O(%CGIEVAR(KEY)) Q:KEY=""  W "<TR><TD>",KEY,"</TD><TD>",%CGIEVAR(KEY),"</TD></TR>",CRLF
 W "</TABLE></DIV>",CRLF
 
 W "<H5 child='LOC4' ONCLICK=""outlinerVAR();"">ПЕРЕМЕННЫЕ CGI ОКРУЖЕНИЯ (%CGI(Node)=Value)</H5>",CRLF
 W "<DIV ID='LOC4' CLASS='collapsedVAR'>",CRLF
 W "<TABLE BORDER>",CRLF
 W "<TR><TH>Node</TH><TH>Value</TH></TR>",CRLF
 S KEY=""  F  S KEY=$O(%CGI(KEY)) Q:KEY=""  W "<TR><TD>",KEY,"</TD><TD>",%CGI(KEY),"</TD></TR>",CRLF
 W "</TABLE></DIV>",CRLF
 
 W "<H5 child='LOC5' ONCLICK=""outlinerVAR();"">ПЕРЕМЕННЫЕ TP-WEB-LINK ОКРУЖЕНИЯ (%ZCS(Node)=Value)</H5>",CRLF
 W "<DIV ID='LOC5' CLASS='collapsedVAR'>",CRLF
 W "<TABLE BORDER>",CRLF
 W "<TR><TH>Node</TH><TH>Value</TH></TR>",CRLF
 S KEY=""  F  S KEY=$O(%ZCS(KEY)) Q:KEY=""  W "<TR><TD>",KEY,"</TD><TD>",%ZCS(KEY),"</TD></TR>",CRLF
 W "</TABLE></DIV>",CRLF
TEST1
 ;D RETURN("")
 D END^%ZAPM.bs.BSCh("")
 Q
HREFVAR(S) Q "<a href="""_$$A^%ZAPM.bs.BSCh()_"BSROU=VARTEST^~BSCh0"">"_S_"</a>"
RETURN(RETURN,CLOSE) I $G(%KEY("BSONLY")) Q
 W "<HR>",BK
 D 3^%ZAPM.bs.BSCh("BSROU="_$G(MainRef,"MAIN^~BSCh1"),"Вернуться в Главное Меню","BSROU=MAIN0^~BSCh1","Заново Регистрироваться","BSROU=MAIL^~BSCh1","Сообщения",$G(RETURN),$G(CLOSE))
 Q
LOCLIST(Tmp) ;ВЫВЕСТИ СПИСОК ЛОКАЛЬНЫХ ПЕРЕМЕННЫХ
 W "<A child='LOC11' style='color:blue; text-decoration:underline; cursor:hand;' onclick=""outlinerVAR();"" title='смотреть переменные, определенные на момент возникновения прерывания'>Current Local Variables</A>",CRLF
 W "<DIV ID='LOC11' CLASS='collapsedVAR'>",CRLF
 D locvar("",1)
 W "</DIV>",CRLF
 Q
locvar(%zz4,Tmp) W "<style type=""text/css"">"_CRLF
 W ".X1 {BACKGROUND:#FFFFB7; COLOR:BLACK; font-size: xx-small; font-family :FixedSys, Courier New, Courier; }"_CRLF
 W ".X0 {BACKGROUND:#FFFF37; COLOR:BLACK; font-size: xx-small; font-family :FixedSys, Courier New, Courier; }"_CRLF
 W "}</style>"_CRLF
 W "<TABLE BORDER=0 WIDTH=100%>",CRLF
 W "<TR><TD class=X0 style='font-weight: bold'>Name Variable</TD><TD class=X0 style='font-weight: bold'>Data Variable</TD></TR>",CRLF
 N %zz1,%zz0,%zz2,%zz3,%zz5,%zz6
 S %zz5="",%zz1="",%zz1=$O(@%zz1),%zz6=0 I '$D(Tmp) S Tmp=0
 S %zz0=0,%zz4=","_%zz4_"," F  Q:%zz1=""  D:%zz1'["%zz"  S %zz1=$O(@%zz1)
 .I "11"[$D(@%zz1) D LOCVAR(%zz1)
 .I $D(@%zz1)>9 S %zz3=%zz1 F  S %zz3=$Q(@%zz3) Q:%zz3=""  D LOCVAR(%zz3)
 W "</TABLE>"
 Q
LOCVAR(%z) I '$G(Tmp) I %z["%CGI"!(%z["%KEY")!(%z["%ZCS")!(%z["%CGIEVAR") Q
 I %zz4'=",,",%zz4'[(","_$QS(%z,0)_",") Q
 I %zz5'=$QS(%z,0) S %zz5=$QS(%z,0),%zz6=%zz6=0
 W "<TR><TD class=X"_%zz6_">",$G(%z),"</TD><TD class=X"_%zz6_">",$G(@%z),"</TD></TR>",$G(CRLF)
 Q
INTXT ;ВВОД ТЕКСТА В ГЛОБАЛ
 N L,dev,%ANS,IO,IOBS,IOF,IOM,IOPAR,IOSL,IOST,IOT,MSYS,POP,GLO,RMSDF,gui
1 W !,"ВВОД ПОЛНЫЙ ПУТЬ К ДОС-ФАЙЛУ:",! D IN^%IS Q:POP
 I IO=$I W !,"УСТРОЙСТВО ВВОДА НЕ ОПРЕДЕЛЕНО" G 1
 W !,"ВВЕДИТЕ ПОЛНУЮ ГЛОБАЛЬНУЮ ССЫЛКУ ^%ZAPM.bs.BSCh(""HTML"","""_$P($P(IO,"\",$L(IO,"\")),".")_""")" R GLO
 I GLO="" S GLO="^%ZAPM.bs.BSCh(""HTML"","""_$P($P(IO,"\",$L(IO,"\")),".")_""")"
 s dev=IO_":"_IOPAR I GLO'["^" S GLO="^"_GLO
 s gui=0
 S $ZT="ERRF^%ZAPM.bs.BSCh0"
 K @GLO
 F L=1:1 U IO R @GLO@(L)
EXIT C IO Q
ERRF I $ze["ENDOFILE"!($ze["ENDOFFILE") G EXIT
 W !,$ZE,*7 G EXIT
RGR ;ВВОД МАССИВОВ И ПРОГРАММ
 N L,dev,%ANS,IO,IOBS,IOF,IOM,IOPAR,IOSL,IOST,IOT,MSYS,POP,GLO,RMSDF,gui,NS,S,V,SS,NS1,OWER
RG W !,"ВВОД ПОЛНЫЙ ПУТЬ К ДОС-ФАЙЛУ:",! D IN^%IS Q:POP
 I IO=$I W !,"УСТРОЙСТВО ВВОДА НЕ ОПРЕДЕЛЕНО" G RG
 S NS=$E($P($P(IO,"\",$L(IO,"\")),"."),6,8) I NS="" S NS="USER"
 W !,"КУДА ВОССТАНАВЛИВАТЬ ? <",NS,"> " R NS1 I NS1="" S NS1=NS
 S $ZT="ERRGR^%ZAPM.bs.BSCh0"
 S NS=$$ZU^%ZAPM.bs.BS3(0)
 I NS1'="" I '$$ZU^%ZAPM.bs.BSF4(NS1) D ErrMsg^%ZAPM.bs.BSXfun("НЕ МОЖЕМ ТУДА ПЕРЕЙТИ !") Q
 I $D(%BS)["0" D CLr^%ZAPM.bs.BSF4
 D Yes^%ZAPM.bs.BSXfun("УЖЕ СУЩЕСТВУЮЩИЕ МАССИВЫ УДАЛЯЕМ ?") S OWER='(YES<0)
 s dev=IO_":"_IOPAR
 s gui=0
 U IO R S U 0 W !,"МАССИВЫ :",S,!
 U IO R S U 0 W !,"ПРОГРАММЫ :",S,!!
 S SS="" F  U IO R S,V D  I S="**" U IO R S,V Q
 .I S="*" S SS="" Q
 .I S="**" Q
 .I S="" Q
 .I SS="" S SS=$QS(S,0) U 0 W SS," " I OWER K @SS
 .S @S=V
 S S="F L=0:1 Q:'$D(V(L))  ZI V(L):+L"
 U 0 W !
 F  U IO R NM Q:NM=""  D
 .S NM=$P(NM,".") K V
 .F L=0:1 U IO R V(L) I V(L)="" K V(L) Q
 .S $ZT="ERROU^%ZAPM.bs.BSCh0"
 .X "ZR  X S ZS @NM U 0 W NM,"" """
RGEX U 0 W !,"ЗАВЕРШЕНО УСПЕШНО..."
RGEXIT C IO I $G(NS)'="" I '$$ZU^%ZAPM.bs.BSF4(NS) D ErrMsg^%ZAPM.bs.BSXfun("НЕ МОЖЕМ ВЕРНУТЬСЯ") Q
 Q
ERRGR U 0 I $ze["ENDOFILE"!($ze["ENDOFFILE") G RGEX
 W !,$ZE,*7 G RGEXIT
ERROU U 0 W !,NM," <--- ",$ZE,*7,! Q
