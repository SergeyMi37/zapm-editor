ROUTINE %ZAPM.bs.BSMSMGF
%BSMSMGF ; ПАКЕТНАЯ РАБОТА С ГЛОБАЛАМИ ; 17:54   23.12.1999
 Q
PROTOKOL(N) ;ПРОТОКОЛ РАБОТЫ
 N (%BS,vl,N) S BSR="^%ZAPM.bs.BSbufB",BST="%RSGBUF"_N D ^%ZAPM.bs.BST
 Q
NAKED(G) ;РАБОТА С ССЫЛКОЙ
 S t0=G_" ="_@G W !,t0 D CR^%ZAPM.bs.BSTT
 Q
FIND N PROX ;ПОИСК
 S PROX=1
FI N %GSEL,%GTEMP,FIND,%SE,%ST,%X,G,GG,g,g,t,t0,Coment,BSr,BSt,Obraz,%G,%JO
 S %zT=$ZT,$ZT="ER^%ZAPM.bs.BSMSMGF"
 Q:'$$SEL
ASK3 D OP^%ZAPM.bs.BSF4 S %X=$$LineEdit^%ZAPM.bs.BSXfun("","Введите подстроку","","","",$$HISFIND^%ZAPM.bs.BSS()) I %X="",'$D(%SE(1)) Q
 I '$G(REPLAY),%X'="" S %ST=$G(%ST)+1,%SE(%ST)=%X G ASK3
 I $G(REPLAY) S %SE(1)=%X,%ST=1,REPLAY=$$LineEdit^%ZAPM.bs.BSXfun("","На что заменим ?") D Yes^%ZAPM.bs.BSXfun("С подтверждением ?",1) S REPLA=YES
GO D TXT(PROX) S G="" F  S G=$O(@%GTEMP@(G)) Q:G=""  S g="^"_G F  S g=$ZO(@g) Q:g=""  D  I $D(STOP) ZT "EXIT"
 .F I=1:1:%ST I @g[%SE(I) D NAKED(g) I $D(REPLAY) D REPL(REPLA>0) U 0 W $$atr^%ZAPM.bs.BS3(0),/ED(2),/CUP(1,1)
EXIT D QU Q
ER I $ZE["ZEXI" D ErrMsg^%ZAPM.bs.BSXfun("Прервано...") S $ZT=$G(%zT) G EXIT
 D ErrMsg^%ZAPM.bs.BSXfun($ZE_" "_$$ErrSay^%ZAPM.bs.BSF8($ZE)) S $ZT=$G(%zT) G EXIT
REPL(Y) I Y W !!,"Заменим на: ",$$TR^%ZAPM.bs.BSsFUNM(@g,%SE(1),REPLAY),!!,"[E]-Редактор ручками...Есть варианты, выберай [Y/N/E/Q] <N>" R *Y S Y=$C(Y) Q:("N"_$C(13))[Y  I Y="E" D  Q
 .S Y=$$LineEdit^%ZAPM.bs.BSXfun(@g,g) Q:YES<0  S @g=Y
 I "^Q"[Y S STOP=1
 S @g=$$TR^%ZAPM.bs.BSsFUNM(@g,%SE(1),REPLAY)
 Q
SEL() U 0 W $$atr^%ZAPM.bs.BS3(0),/ED(2),/CUP(1,1) D OP^%ZAPM.bs.BSF4
 I $ZV["MSM" D ^%GSEL,CL^%ZAPM.bs.BSF4 S %Jo=$J,%G=%GSEL I +$G(%GSEL)=0 G NOSEL
 I $ZV["Cach"||($ZV["IRIS") D ^%GSET S %Jo=%JO,%GTEMP="^UTILITY(%JO)",%GSEL=%G I '%G G NOSEL
 D CL^%ZAPM.bs.BSF4
 I $D(@%GTEMP@("%BSbufB")) K @%GTEMP@("%BSbufB")
 Q 1
NOSEL D ErrMsg^%ZAPM.bs.BSXfun("Массивы не выбраны") Q 0
QU D OkMsg^%ZAPM.bs.BSXfun("Обработан(о) "_$G(%GSEL,%G)_" Массив(ов)")
 S Coment=$$TX D CREAT^%ZAPM.bs.BSTT
 Q
TX() Q "- Поиск "_$S(PROX=2:"И Замена данных",PROX=3:"по фильтру",1:"")_" в массивах."
TXT(PROX) ;ПОДГОТОВКА ПРОТОКОЛА
 S BSr="^%ZAPM.bs.BSbufB",BSt="%RSGBUF"_PROX,Obraz(12)=1,Obraz(21)=1
 S t0=$$TX_" Введите <ALT-G> для входа в редактор" D CR^%ZAPM.bs.BSTT U 0 W !,$$TX,!
 I $D(%SE) F I=1:1:$G(%ST,1) S t0="- "_%SE(I) W t0,! D CR^%ZAPM.bs.BSTT
 I $D(FIL) S t0="- "_FIL W t0,! D CR^%ZAPM.bs.BSTT
 S t0=" " D CR^%ZAPM.bs.BSTT
 Q
REPLAY ;ЗАМЕНА
 N REPLAY,PROX,REPLA
 S REPLAY=1,PROX=2 G FI
FILTER ;ФИЛЬТР
 N FIL,%GSEL,%GTEMP,PROX,FIND,%SE,%ST,%X,G,GG,g,t,t0,Coment,BSr,BSt,Obraz
 S %zT=$ZT,$ZT="ER^%ZAPM.bs.BSMSMGF"
 Q:'$$SEL  S FIL="",PROX=3,g="^%ZAPM.bs.BSbufB" D FILT^%ZAPM.bs.BSMSMG Q:$G(FIL)=""
 D TXT(3) S G="" F  S G=$O(@%GTEMP@(G)) Q:G=""  S g="^"_G F  S g=$ZO(@g) Q:g=""  X FIL_" D NAKED(g)"
 D QU Q
