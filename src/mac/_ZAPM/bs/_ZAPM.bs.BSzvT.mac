ROUTINE %ZAPM.bs.BSzvT
%BSzvT ;Формирование таблицы сводки  (А.Тимофеев) ; 13:44   09.11.2001
 N masI,masT,name,C1,i,i1,i2,i3,i4,i5,i6,y,y1,strS,strC,strC1,strC2,strP,strP2,kolT,kolP,kolPGN,adr,uslS,prg,yt,xt,ROU
 N NaMe,NaPr,I,CurSTR,FormSTR,HEAD,TItuL,YYy,ROU
 S %zT=$ZT,$ZT="ERROR^%ZAPM.bs.BSzvU"
 S NaMe=$G(@mas0@("tab",4),"?UNKNKOW") ;БАЗОВОЕ ИМЯ СВОДКИ
 S NaPr=$$cB^%ZAPM.bs.BSSV I $G(@mas0@("tabE",3))'="" S NaPr=@mas0@("tabE",3) ;ИМЯ РАЗДЕЛА
 S masI=$NA(@NaPr@($$NamePack^%ZAPM.bs.BSzZ()_NaMe)) ;masI - ИМЯ НАСЧИТАННОЙ СВОДКИ
 I PACET'=0 I $D(@masI) K @masI
 I PACET=0 D  ;ФОРМИРОВАНИЕ ИМЕНИ СВОДКИ
 .I "Y"[$G(@mas0@("tabE",4)) D  Q
 ..F I=1:1 I '$D(@NaPr@(NaMe_"_"_$$SYSDATE^%ZAPM.bs.BSsFUNR(6,$$h^%ZAPM.bs.BS3())_"_"_I)) S masI=$ZR Q
 .I $D(@masI) S masI=$NA(@NaPr@(NaMe_"."_$P($$h^%ZAPM.bs.BS3,",",2)))
 
 S CurSTR=1 ;ТЕКУЩАЯ СТРОКА СВОДКИ
 S masT=$G(@mas0@("tab",3)) ;masT - ИМЯ ШАБЛОНА
 S (nameS,name)=$G(^(2)) ;КОМЕНТАРИЙ
 S strS=$G(^(7)) ;ШАПКА
 S strC=$G(^(8)) ;ЦИКЛ
 S strC1=$G(^(9)) ;ВЫДЕЛЕНИЕ
 S strC2=$G(^(10)) ;ВЫДЕЛЕНИЕ ВЫДЕЛЕННОЕ
 S strP=$G(^(11))  ;ПОДВАЛ
 S kolI=$G(^(12))  ;ВЫБРАННЫЕ КОЛОНКИ
 S strP2=$G(^(13)) ;СТРОКИ ВЫДЕЛЯТЬ ВЫДЕЛЕННЫМ РАЗДЕЛИТЕЛЕМ 1,2,,,y,
 S kolT=$G(^(14))  ;КОЛОНКА БОКОВИНЫ
 S kolP=$G(^(15))  ;КОЛОНКА ПЕРИОДОВ СРАВНЕНИЯ
 S adr=$G(^(16)) ;АДРЕС УГЛА
 S BSr=$P(masI,"("),BSt=$TR($P($P(masI,"(",2),")"),$C(34),"")
 S ls="Формируется таблица сводки" D WAITS^%ZAPM.bs.BSF2
 S @masI=$G(@masT)
 
 I kolI'="" D Nshab  ;Создание НОВОЙ таблицы-шаблона
 
 S kolPGN=$G(@mas0@("tabE",14)) ;В КОЛОНКЕ ПЕРИОДОВ СРАВНЕНИЯ ГОД или НОМЕР G\N
 
 ;строка (название) ЗАГОЛОВОК
 I $G(@mas0@("tabE",2))'="" S name=@mas0@("tabE",2)
 I name["~" F I=1:1 Q:$P(name,"~",I,I+1)=""  S $P(name,"~",I)=$$EXT^%ZAPM.bs.BSzmOL($P(name,"~",I)) D ADDHEAD($P(name,"~",I))
 I name'["~" S name=$$EXT^%ZAPM.bs.BSzmOL(name) D ADDHEAD(name)
 S @masI@("PERHEAD")=$$HEAD^%ZAPM.bs.BSzmOL(name)
 
 ;строка с условиями
 S uslS="" S k="" F  S k=$O(@mas0@("zp",k)) Q:k=""  D  X $G(WA)
 .I $G(@mas0@("zp",k,1))'="" S uslS=uslS_" "_$G(^(1)) Q
 I $TR(uslS," ","")'="" D ADDHEAD(uslS)
 
 ;ПЕРИОДЫ СРАВНЕНИЯ
 I $G(@mas0@("tabE",5))'="" I $D(G1) s Form=$G(@mas0@("tabE",5)) D
 .F C1=1:1:C I $D(G1(C1)),G1(C1)'=0 D
 ..N i6
 ..i Form="Y" S i6=C1_" ПЕРИОД : С "_$$D^%ZAPM.bs.BSsFUNR($G(G1(C1)))_"."_$$M^%ZAPM.bs.BSsFUNR($G(G1(C1)))_"."_$$Y^%ZAPM.bs.BSsFUNR($G(G1(C1)))_" ПО "_$$D^%ZAPM.bs.BSsFUNR($G(G2(C1)))_"."_$$M^%ZAPM.bs.BSsFUNR($G(G2(C1)))_"."_$$Y^%ZAPM.bs.BSsFUNR($G(G2(C1)))
 ..i "YN"'[Form x "s i6="_Form
 ..D ADDHEAD(i6) S @masI@("PER",C1)=i6
 
 ;ДАТА - ВРЕМЯ СОЗДАНИЯ
 I $G(@mas0@("tabE",9))="Y" D
 .N i6 S i6="ВЫДАНО: "_$$ESDAY^%ZAPM.bs.BSsFUNR(7,$$h^%ZAPM.bs.BS3())
 .I $G(@mas0@("tabE",10))="Y" S i6=i6_" КОД ИСПОЛНИТЕЛЯ:"_$G(%BS(12),"АДМ")
 .I $G(@mas0@("tabE",12))="Y" S i6=i6_" КОД СВОДКИ:"_$G(NaMe)
 .D ADDHEAD(i6)
 
 ;ШТАМПИК
 I $G(@mas0@("tabE",7))'="" D
 .N N,LONG,KOLS,XX
 .S N=@$ZR I $$Data^%ZAPM.bs.BSF12(N) D
 ..S LONG=+$P($G(@masT@("PRN")),"#",2),KOLS=$P(@N,"@",28),XX=$O(@masT@(1,""),-1),XX=$P(@masT@(1,XX),"@",2)+$P(@masT@(1,XX),"@",4)
 ..I LONG>XX S LONG=XX
 ..I +$G(@mas0@("tabE",8)) S LONG=$G(@mas0@("tabE",8))+$L($G(@N@(1)))
 ..I 'LONG S LONG=$$MAXHEAD()+$L($G(@N@(1)))+3
 ..D BUILD(LONG-$L($G(@N@(1)))-2,KOLS)
 
 F CurSTR=1:1 Q:'$D(HEAD(CurSTR))  D COPYLINE(CurSTR,HEAD(CurSTR))
 
 S $P(@BSr@(BSt,"ExcelOut"),",",3)=CurSTR
 ;СКРЫТАЯ СТРОКА С ФОРМУЛАМИ
 S (FormSTR,y1)=CurSTR,y=y1-1  ;скрытая строка с формулами
 S x=1,i="" F  S i=$O(@masT@(strC,i)) Q:i=""  D  X $G(WA)
 .S @masI@(y1,i)=$G(@masT@(strC,i))
 .S:$D(@masT@("FCL",strC,i)) @masI@("FCL",y1,i)=$G(@masT@("FCL",strC,i)),@masI@("FCL",y1,i,1)=$G(@masT@("FCL",strC,i,1))
 .S:$D(@masT@("FTR",strC,i)) @masI@("FTR",y1,i)=$G(@masT@("FTR",strC,i)),@masI@("FTR",y1,i,1)=$G(@masT@("FTR",strC,i,1))
 .S $P(@masI@(y1,i),"@")=2,$P(^(i),"@",3)=0
 S CurSTR=CurSTR+1
 
 ;Строки шапки таблицы
 S x=1,i="" F  S i=$O(@masT@(strC,i)) Q:i=""  D  X $G(WA)
 .S y1=CurSTR,y=y1-1
 .X "F i1="_strS_" D STRh1"
 S CurSTR=y1
 S i="" F  S i=$O(@mas0@("bok",i)) Q:i=""  D STRp  X $G(WA) ;ТЕКСТЫ БОКОВИНЫ
 
 I $G(strP)'="" X "F i="_strP_" D STRz"
 
 ;ТИТУЛ ПЕЧАТИ```MSW
 S $P(@masI,"@",50)=CurSTR_","_$S($G(kolP)'="":(kolP+2),1:(kolT+2))
 S TItuL="" I +$G(@mas0@("tabE",11)) S TItuL=@$ZR
 I $P(@masI,"@",19),$P(@masI,"@",19)<$G(TItuL) S $P(TItuL,",")=$P($P(@masI,"@",19),",")
 I $P(@masI,"@",19),'$G(TItuL) S TItuL=$P(@masI,"@",19)
 S $P(@masI,"@",19)=""
 
 S $P(@masI,"@",32)=1 ;ПЕРЕСЧИТЫВАТЬ ФОРМУЛЫ ВО ВСЕХ КЛЕТКАХ
 ;Установки на вывод
 F prg="PRN","HFS","TXT","SHR","SVM","RSM" D
 .I $D(@masT@(prg)) S @masI@(prg)=$G(@masT@(prg))
 
 S @masI@("PER")=C ;ПЕРИОДЫ СРАВНЕНИЯ
 S @masI@("PERDATE")=$$ESDAY^%ZAPM.bs.BSsFUNR(7,$$h^%ZAPM.bs.BS3())
 S @masI@("PERUSER")=$G(%BS(12))
 S $P(@masI@("ExcelOut"),",",1)=$G(@mas0@("tabE",15)) ;информация для экспорта в Excel - Кол-во строк шапки после разбора
 S @masI@("LST")=$H_","_$G(%BS(12))_",Greated"
 
 N BSR,BST,ke,se S BSR=BSr,BST=BSt,se=$P(@masI,"@",28),ke=$P(@masI,"@",29) D TAB^%ZAPM.bs.BSF1 K BSR,BST ;```MSW
 
 D TPr^%ZAPM.bs.BSF8(masI) ;ТИТУЛ ПРОСМОТРА
 I '$P(@masI,"@",19),TItuL S YYy=FormSTR+1+TItuL-strS,TItuL=+$P(TItuL,",",2) I YYy,TItuL,($P($G(@masI@(YYy,TItuL)),"@")+$P($G(@masI@(YYy,TItuL)),"@",3))'>20 S $P(@masI,"@",19)=YYy_","_TItuL
 
 I $G(@mas0@("tabE",21))'="" S ROU=@mas0@("tabE",21) X $S($E(ROU)="^":"D ",1:"")_ROU
 S $P(@masI,"@")=C_";"_($P($$h^%ZAPM.bs.BS3,",",2)-Total)_" сек. "_$G(nameS,name)
 I $P(@masI,"@",51) D APRESS^%ZAPM.bs.BSSR(masI)
 I PACET=0 S IYI=masI D NE^%ZAPM.bs.BSN
 S $ZT=%zT,BSl=masI
 Q
BUILD(L,K) ;ДОСТРОИТЬ К ЗАГОЛОВКУ ШТАМПИК
 F I=1:1:K S:'$D(HEAD(I)) HEAD(I)="" S HEAD(I)=$E(HEAD(I),1,L),HEAD(I)=HEAD(I)_$J("",L-$L(HEAD(I)))_" "_$G(@N@(I))
 Q
ADDHEAD(S) S HEAD(CurSTR)=S,CurSTR=CurSTR+1
 Q
COPYLINE(Y,name) ;КОПИРОВАТЬ СТРОКУ СВОДКУ
 N x,i
 S x=1,i="" F  S i=$O(@masT@(strC,i)) Q:i=""  D  X $G(WA)
 .S @masI@(Y,i)=$G(@masT@(strC,i))
 .S $P(@masI@(Y,i),"@",15)=$E(name,x,(x+$P(@masT@(strC,i),"@",4)-1))
 .S $P(@masI@(Y,i),"@",3)=1,x=x+$P(@masT@(strC,i),"@",4)
 .S $P(@masI@(Y,i),"@",7)="",$P(@masI@(Y,i),"@",8)=""
 Q
STRh1 ;ШАПКА
 S @masI@(y1,i)=$G(@masT@(i1,i))
 I $G(adr)'="",$G(@mas0@("tabE",5))'="Y" I $P($P(adr,","),"{",2)=i1&($P($P(adr,",",2),"}")=i) D STRh2
 S $P(@masI@(y1,i),"@")=y,y1=y1+1,y=y+$P(@masT@(i1,i),"@",3)
 Q
STRh2 ;Клетка с периодами сравнения
 S i6="",i5=$P(@masI@(y1,i),"@",15) F C1=1:1:($P(^(i),"@",3)-2) D
 .S i6=i6_$J($E($S($G(G1(C1))'="":"С  ",1:"   ")_$$D^%ZAPM.bs.BSsFUNR($G(G1(C1)))_$$M^%ZAPM.bs.BSsFUNR($G(G1(C1)))_$$Y^%ZAPM.bs.BSsFUNR($G(G1(C1))),1,$P(^(i),"@",4)),$P(^(i),"@",4))
 .S i6=i6_$J($E($S($G(G2(C1))'="":"ПО ",1:"   ")_$$D^%ZAPM.bs.BSsFUNR($G(G2(C1)))_$$M^%ZAPM.bs.BSsFUNR($G(G2(C1)))_$$Y^%ZAPM.bs.BSsFUNR($G(G2(C1))),1,$P(^(i),"@",4)),$P(^(i),"@",4))
 S i6=$E(i6,1,$P(^(i),"@",4)*($P(^(i),"@",3)-2))_$J("",$P(^(i),"@",4)*($P(^(i),"@",3)-2)-$L(i6))
 S $P(^(i),"@",15)=$E(i5,1,$P(^(i),"@",4))_i6_$E(i5,($P(^(i),"@",3)-1*$P(^(i),"@",4))+1,$L(i5))
 Q
 
STRp ;Строки цикла
 F C1=1:1:C D STRp1
 D STRp3
 Q
STRp1 ;
 S i1="" F  S i1=$O(@masT@(strC,i1)) Q:i1=""  D STRp2
 S y1=y1+1,y=y+$P(@masT@(strC,1),"@",3)
 Q
STRp2 ;
 S i2=((i-1)*C)+C1
 S @masI@(y1,i1)=$G(@masT@(strC,i1))
 I i1=kolT D        ;Текст боковины
 .I $G(@mas0@("bok",i))="#" S $P(@masI@(y1,i1),"@",15)="!!!" Q
 .I $P($G(@mas0@("bok",i)),"#")'="" S $P(@masI@(y1,i1),"@",15)=$E($P($G(@mas0@("bok",i)),"#"),1+($P(@masT@(strC,i1),"@",4)*(C1-1)),($P(@masT@(strC,i1),"@",4)*C1)) Q
 .I $P($G(@mas0@("bok",i)),"#",2)="" Q
 .I C1'=1 Q
 .S $P(@masI@(y1,i1),"@",15)=$P($G(@mas0@("bok",i)),"#",2) Q
 I i1=kolP D        ;Период насчета
 .I $G(kolPGN)="N" S $P(@masI@(y1,i1),"@",15)=C1
 .E  S $P(@masI@(y1,i1),"@",15)=$$YY^%ZAPM.bs.BSsFUNR(G2(C1))
 .I $P(@masI@(y1,i1),"@",15)=0 S $P(@masI@(y1,i1),"@",15)=""
 I kolI'="" D
 .S iN1=$P(@masT@(strC,i1),"@",17) I iN1'="" S:$D(@mas1@(i2,iN1))&($G(^(iN1))'=0) $P(@masI@(y1,i1),"@",15)=$G(@mas1@(i2,iN1))
 I kolI="" S:$D(@mas1@(i2,i1))&($G(^(i1))'=0) $P(@masI@(y1,i1),"@",15)=$G(@mas1@(i2,i1))
 S:$P(@masI@(y1,i1),"@",7)'="" $P(^(i1),"@",7)=FormSTR_","_i1
 S:$P(@masI@(y1,i1),"@",8)'="" $P(^(i1),"@",8)=FormSTR_","_i1
 S $P(@masI@(y1,i1),"@")=y
 Q
STRp3 ;Подчеркивание и выделение
 I $G(strC1)="" Q
 S i6=0 D                  ;Выделение
 .I $G(strC2)=""!($G(strP2)="") Q
 .F i4=1:1 S i5=$P(strP2,",",i4) Q:i5=""  S:i5=i!(i5=(i+1)) i6=1 Q:i6=1
 S i1="" F  S i1=$O(@masT@(strC,i1)) Q:i1=""  D
 .S @masI@(y1,i1)=$S(i6=1:$G(@masT@(strC2,i1)),1:$G(@masT@(strC1,i1)))
 .S $P(@masI@(y1,i1),"@")=y
 S y1=y1+1,y=y+$P(@masT@(strC1,1),"@",3)
 Q
 
STRz ;Строки подвала
 S i1="" F  S i1=$O(@masT@(strC,i1)) Q:i1=""  D
 .S @masI@(y1,i1)=$G(@masT@(i,i1))
 .S $P(@masI@(y1,i1),"@")=y
 S y1=y1+1,y=y+$P(@masT@(strC1,1),"@",3)
 Q
 
Nshab ;Создание новой таблицы-шаблона
 S:$E(kolI,1)'=1 kolI="1:1:"_$S(kolP'="":kolP+1,kolT'="":kolT+1,1:1)_","_kolI
 S masI1=masI,masI="^%ZAPM.bs.BSbufB(""fTS""_$G(%BS(3),$P)_$J_""u"")" K @masI
 S (x,x1)=1 X "F i="_kolI_" D STRN"
 F prg="PRN","HFS","TXT","SHR","SVM","RSM" D
 .I $D(@masT@(prg)) S @masI@(prg)=$G(@masT@(prg))
 S masT=masI,masI=masI1
 Q
STRN ;Строки таблицы
 S strNT=$O(@masT@(10000),-1),strNT="1:1:"_strNT
 S y1=1,y=1 X "F i1="_strNT_" D STRN1"
 S x1=x1+1,x=x+$P(@masT@(i1,i),"@",4)
 Q
STRN1 ;
 S @masI@(y1,x1)=$G(@masT@(i1,i)) X $G(WA)
 S:$D(@masT@("FCL",i1,i)) @masI@("FCL",y1,x1)=$G(@masT@("FCL",i1,i)),@masI@("FCL",y1,x1,1)=$G(@masT@("FCL",i1,i,1))
 S:$D(@masT@("FTR",i1,i)) @masI@("FTR",y1,x1)=$G(@masT@("FTR",i1,i)),@masI@("FTR",y1,x1,1)=$G(@masT@("FTR",i1,i,1))
 S $P(@masI@(y1,x1),"@",2)=x,$P(@masI@(y1,x1),"@",1)=y
 S:i1=strC $P(@masI@(y1,x1),"@",17)=i   ;Номер колонки из насчета
 S y1=y1+1,y=y+$P(@masT@(i1,i),"@",3)
 Q
