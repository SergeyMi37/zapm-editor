ROUTINE %ZAPM.bs.BSzvR1
%BSzvR1 ;Компоненты создания прогры сводки  (А.Тимофеев) ; 13:38   19.04.2000
 
KEY(%1) ;Вытаскивание условий на ключи БД
 S %0="" N f,u
 S f=$S($G(@mas@(nf,na,i,%1,"11,4"))'="":^("11,4"),$G(@mas3@("zp",%1,7))'="":^(7),1:"")
 I f'="" S f=$S(f'=+f:f,1:"$E("_%1_",1,"_$L(f)_")="_f)
 S u=$S($G(@mas@(nf,na,i,%1,"9,4"))'="":^("9,4"),$G(@mas3@("zp",%1,6))'="":^(6),1:"")
 I u'="" S u=$S(u'=+u:u,1:"$E("_%1_",1,"_$L(u)_")="_u)
 S %0=$S($G(f)'="":" S "_f_" ",1:$S($G(@mas@(nf,na,i,%1,"7,4"))="Y":",-1",1:"")_") Q:"_%1_"=""""  ")
 S %0=%0_$S($G(u)'="":"I "_u,1:"")
 S %0=%0_" D "_$S(keyS=$E(%1,2):"C"_i,1:" ")
 I $D(@mas@(nf,na,i,%1)) S %0=%0_$S($G(@mas@(nf,na,i,%1,"3,4"))'="Y":" Q  ",1:"")
 Q %0
 
KEYL(%1) ;Конкректное кол-во ключей
 N k,str
 S %0=""
 S str="(" F k=1:1:keyS S str=str_$S(k=1:"k"_k,1:",k"_k)
 S %0=str_")" Q %0
 
PO(%1,%2,%3,%4,%5) ;Конкректные адреса в БД
 ;%1 (1 - Только по О.Ч., 2 - Смешанное условие или Л.Ч.)
 ;%2 - программная строка
 ;%3 - Полная глобальная ссылка на таблицу БД с О.Ч.
 ;%4 - Полная глобальная ссылка на таблицу БД с Л.Ч.
 ;%5 - Условие мультитаблицы (1)
 I %2["|" S %2=$TR(%2,"|","@")
 S:$G(%3)'="" tabO=%3 S:$G(%4)'="" tabO=%4 S:$G(%5)'="" mtb="Y"
 N y,x,str1,str2,str3,TAB
 S %0=%2 I %1=""!(%2="") Q %0
PO1 I %2'["{" S %0=%2 D:%2["^("  Q %0
 .F y=2:1 S str1=$P(%2,"^(",y) Q:str1=""  D
 ..S str2=$P(str1,")"),$P(str1,")")=str2_")"
 ..S $P(%2,"^(",y)=str1
 .S %2=$$TR^%ZAPM.bs.BSsFUNM(%2,"^(","$G(@masR@(") K y,str1,str2 S %0=%2
 I %1=1 S TAB=tabO D  G PO1   ;Для условия только по О.Ч.
 .S str1=$P(%2,"{"),str2=$P($P(%2,"{",2),"}"),str3=$E(%2,($L(str1)+$L(str2)+3),$L(%2))
 .S y=$P(str2,","),x=$P(str2,",",2)
 .D  S %2=str1_str2_str3
 ..I $P($P($G(@TAB@(y,x)),"@",18),"#")'="" S str2=$P($P($G(@TAB@(y,x)),"@",18),"#") Q
 ..S str2=y_","_x Q
 I %1=2 S TAB=tabO D          ;Для совместного условия по О.Ч.
 .I %2'["P(""{" Q
 .S str1=$P(%2,"P(""{"),str2=$P($P(%2,"P(""{",2),"}"),str3=$E(%2,($L(str1)+$L(str2)+6),$L(%2))
 .S y=$P(str2,","),x=$P(str2,",",2)
 .D  S %2=str1_"P("""_str2_str3
 ..I $P($P($G(@TAB@(y,x)),"@",18),"#")'="" S str2=$P($P($G(@TAB@(y,x)),"@",18),"#") Q
 ..S str2=y_","_x Q
 S TAB=$S(mtb="Y":tabL,1:tabO) D  G PO1    ;Для совместного по Л.Ч. или по О.Ч.
 .I %2'["^(""{" Q
 .S str1=$P(%2,"^(""{"),str2=$P($P(%2,"^(""{",2),"}"),str3=$E(%2,($L(str1)+$L(str2)+6),$L(%2))
 .S y=$P(str2,","),x=$P(str2,",",2)
 .D  S %2=str1_"^("""_str2_str3
 ..I $P($P($G(@TAB@(y,x)),"@",18),"#")'="" S str2=$P($P($G(@TAB@(y,x)),"@",18),"#") Q
 ..S str2=y_","_x Q
 Q
