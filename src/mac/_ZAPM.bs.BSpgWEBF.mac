ROUTINE %ZAPM.bs.BSpgWEBF
TKWEBF	;PG&A,TK-WEB,1.1,FILE UTILITIES;21SEP97  20:22 [ 12/04/97  10:51 AM ];;;04OCT2000  17:11
	;;Copyright (C) 1997 Patterson, Gray and Associates, Inc.
	;
	;
FILES	; FILE STRUCTURE
	; NAME ; TYPE ; SIZE ; DATE MOD ; YYYYMMDD MOD ; DATE CREATED ; SRCE
	;
IMPORT	; READ FILES INTO GLOBAL FORM
 K STK I $G(TK(1)) S STK=TK(1),TK(1)=1 X ^%ZAPM.bs.BSpg("TERM-OFF"),^%ZAPM.bs.BSpg("ECHO-ON")
	R "FILE (d:file.ext) -",AA,! Q:AA=""  R "Store in what Directory? ",TDIR,! S:TDIR="" TDIR="TEST" R "Store under name-",NN,! S:NN="" NN=AA
	I AA'[":" S AA="D:\FCC\"_AA
 S DIRECT=1 D IN1 K DIRECT I $D(STK) X ^%ZAPM.bs.BSpg("TERM-ON") S TK(1)=STK K STK
	Q
IN1	D INIT S TDIR=$TR(TDIR,LC,UP),NN=$TR(NN,LC_" ",UP_"_")
 S G="^%ZAPM.bs.BSpgWEBF(""PAGES"",TDIR,NN)",D=$$GMT^%ZAPM.bs.BSpgWEBH($H,+$G(^%ZAPM.bs.BSpgWEBC("TimeZone"))),SZ=0,OD="",C=DT
	I $D(@G) W "Already defined - Overwrite? (Y/N) - " R Y Q:Y="N"  Q:Y="n"  S OD=$G(@G) K @G W OD,! S C=$P(OD,"`",6)
	S T=$P(OD,"`",2),S=$P(OD,"`",7)
 D:^%ZAPM.bs.BSpg("OS")="MSM" MSM D:^%ZAPM.bs.BSpg("OS")="M3" M3 S S=""
	S T=$TR($E($P(AA,".",2),1,3),UP,LC),T=$S(T="gif":"G",T="htm":"H",T="jpe":"J",T="jpg":"J",1:"")
	I T="" S T=$TR($E(@G@(1),1,3),UP,LC),T=$S(T="<ht":"H",T="gif":"G",T="<!d":"H",1:"") I T="",$D(DIRECT) W $E(^(1),1,3),! R "Type (H-HTML,I-IMAGE(G-GIF,J-JPEG),T-TEXT,O-OTHER) - ",T,!
	S @G=NN_"`"_T_"`"_SZ_"`"_D_"`"_DT_"`"_C_"`"_S
	S DIR=TDIR,FILE=NN D XREF
	Q:'$D(DIRECT)  U 0 W "finished" Q
	;
MSM	O 52:(AA:::::"") F J=1:1 U 52 R B#510 S @G@(J)=B,SZ=SZ+$L(B) Q:$ZC  I $D(DIRECT) U 0 W "."
	C 52 Q
M3	O "FILE2":(AA:"IR") F J=1:1 U "FILE2" R B#510 S @G@(J)=B,SZ=SZ+$L(B) Q:$ZC  I $D(DIRECT) U 0 W "."
	C "FILE2" Q
	;
INX	N (AA,NN,TDIR) D IN1 Q
INXK N (AA,NN,TDIR) D IN1 I "M3,MSM"[^%ZAPM.bs.BSpg("OS") S A=$ZOS(2,AA)
	Q
	;
DIR	; READ IN A ENTIRE DIRECTORY
	R "PATH-",P,! Q:P=""  R "Directory-",TDIR,!
	S A=$ZOS(8,P),DR=$ZOS(14)_":",DIR=$ZOS(11,DR)
	S TO=0 F  D DIR1 Q:TO=""  W !,TX,! R "(Y/N)-",YN#1 I YN="Y" S (AA,NN)=$P(TX,"  ") D:AA]"" IN1
	Q
DIR1	S TX="" S:TO=0 TO="" S:TO="" S=$ZOS(12,"*.*",63) S:TO'="" S=$ZOS(13,TO) S TO=S I S<0 S TO="" Q
	S (TO1,TO2,TO3,TO4)=""
	S TO3=$P(TO,"^"),TO2=$P(TO3,".",2),TO3=$P(TO3,"."),T=$P(TO,"^",2,9) I $ZB($A(T,22),16,1) G DIR1:$P(TO,"^")=".." S TO2="",TO1="<"_$S(TO3]"":"DIR",1:"back")_">  "
	E  S TO1=$J($FN($A(T,30)*256+$A(T,29)*256+$A(T,28)*256+$A(T,27),","),11)
	S %F=$A(T,26)*256+$A(T,25),%D=%F#32,%F=%F\32,%E=%F#16,%F=%F\16,%F=%F+1980
	S TO4=$E("  ",1,2-$L(%E))_%E_"-"_$E(%D+100,2,3)_"-"_%F_" "
	S %F=$A(T,24)*256+$A(T,23),%D=%F#32*2,%F=%F\32,%E=%F#64,%F=%F\64,%A=%F\12,%B=%F#12 S:%B=0 %B=12
	S TO4=TO4_$E("  ",1,2-$L(%B))_%B_":"_$E("00",1,2-$L(%E))_%E_$S(%A:"p",1:"a") S TO3=TO3_$S(TO2]"":"."_TO2,1:"")
	S TX=$E(TO3_$J("",40),1,35)_" "_TO1_" "_TO4 Q
	;
INIT D INIT^%ZAPM.bs.BSpgWEBH Q
	;
XREF S ^%ZAPM.bs.BSpgWEBF(99,DIR,FILE)="" D INIT I T'="H" G XREFO
 S XGLOB="^%ZAPM.bs.BSpgWEBF(""PAGES"",DIR,FILE)"
 I '$D(^%ZAPM.bs.BSpgWEBF("PAGES",DIR)) S (^%ZAPM.bs.BSpgWEBF(98,DIR),^%ZAPM.bs.BSpgWEBF("PAGES",DIR))="\\\"
 S ^%ZAPM.bs.BSpgWEBF(98,DIR,FILE)=$G(@XGLOB)
 S A="" F  S A=$O(^%ZAPM.bs.BSpgWEBF("XREF",DIR,"H",FILE,A)) Q:A=""  K ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"R",A,FILE),^%ZAPM.bs.BSpgWEBF("XREF",DIR,"F",A,FILE),^%ZAPM.bs.BSpgWEBF("XREF",DIR,"L",A,FILE)
 K ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"H",FILE),^%ZAPM.bs.BSpgWEBF("XREF",DIR,"J",FILE)
	S A=0,B="" F  S F=A,A=$O(@XGLOB@(A)) Q:A=""  S C=B,B=$TR(^(A),LC_$C(10),UP) D XREF1
	S C=B,B="" D XREF1
	K @XGLOB@(0,0),^(.1)
	;  <-D-xxx> insert variable contents of xxxxx or %(xxxxx)
 ;  <-F-xxx> insert result of $$xxx^%ZAPM.bs.BSpgWEBL
	;  <-F-xxx^yyy> insert result of $$xxx^yyy
	;  <-W-xxx^yyy> insert result of output of $$xxx^yyy
	;
XREFen	; entry point
	S A=.03,cc=0 F  S A=$O(@XGLOB@(A)) Q:A=""  S C=^(A) I $L($P(C,"<",2,99),"<")+1'=$L($P(C,"<",2,99),">") S B=$O(^(A)) I B,^(B)[">" S ^(A)=C_$P(^(B),">")_">",^(B)=$P(^(B),">",2,99)
	S A=.03 F  S A=$O(@XGLOB@(A)) Q:A=""  S C=^(A) F  Q:C'?.E1"<-D-"1AP.E1">".E  S B=$P($P(C,"<-D-",2),">"),C=$P(C,"<-D-",2,9),@XGLOB@(0,0,A+cc)="M1`<-D-"_B_">`"_B,cc=cc+.01  ; variable
	S A=.03 F  S A=$O(@XGLOB@(A)) Q:A=""  S C=^(A) F  Q:C'?.E1"<-F-"1A.E  S B=$P($P($P(C,"<-F-",2),";"),">"),C=$P(C,"<-F-",2,9),@XGLOB@(0,0,A+cc)="M2`<-F-"_B_"`"_B,cc=cc+.01 ; function
	S A=.03 F  S A=$O(@XGLOB@(A)) Q:A=""  S C=^(A) F  Q:C'?.E1"<-W-"1A.E  S B=$P($P($P(C,"<-W-",2),";"),">"),C=$P(C,"<-W-",2,9),@XGLOB@(0,0,A+cc)="M3`<-W-"_B_"`"_B,cc=cc+.01 ; not yet used
	S A=$D(@XGLOB@(0,0)) S $P(@XGLOB,"`",3)=$TR($P(@XGLOB,"`",3),"M")_$S(A:"M",1:"")
	K @XGLOB@(0,1) S A=0,B="",F="<!--OPTION"
	F  S C=B,A=$O(@XGLOB@(A)) Q:A=""  S B=^(A),E=C_B F J=2:1:$L(E,F)  S D=$TR($P($P(E,F,J),">")," "),@XGLOB@(0,1,+D)=$P(D,",",2,99)
XREFO	Q
XREF1	;
 F D=1:1:$L(C_B,"HREF=") S E=$P(C_B,"HREF=",1,D) I $L(E)+4<$L(C) S E=$P(C_B,"HREF=",D+1,99),E=$$PQ S:E]""&($L(E)<99) ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"L",E,FILE,F)="",^%ZAPM.bs.BSpgWEBF("XREF",DIR,"H",FILE,E,F)=""
 F D=1:1:$L(C_B,"SRC=") S E=$P(C_B,"SRC=",1,D) I $L(E)+3<$L(C) S E=$P(C_B,"SRC=",D+1,99),E=$$PQ S:E]""&($L(E)<99) ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"R",E,FILE,F)="",^%ZAPM.bs.BSpgWEBF("XREF",DIR,"H",FILE,E,F)=""
 F D=1:1:$L(C_B,"FRAME SRC=") S E=$P(C_B,"SRC=",1,D) I $L(E)+9<$L(C) S E=$P(C_B,"FRAME SRC=",D+1,99),E=$$PQ S:E]"" ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"F",E,FILE,F)=""
 F D=1:1:$L(C_B,"JAVA") S E=$P(C_B,"JAVA",1,D) I $L(E)+3<$L(C) S E=$P(C_B,"JAVA",D+1,99),E=$$PQ S:E]"" ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"J",FILE,F)=""
	Q         Q
SCN S DIR="",FILE="" F  S DIR=$O(^%ZAPM.bs.BSpgWEBF("PAGES",DIR)) Q:DIR=""  F   S FILE=$O(@XGLOB) Q:FILE=""  I FILE[".H" S T="H" D XREF
	Q
PQ()	Q:$E(E)="""" $P(E,"""",2) Q $P($P(E,">")," ")
	;
 ;  ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"H",HOBJ,ROBJ,LINE)
 ;  ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"R",ROBJ,HOBJ,LINE))
 ;  ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"L",HOBJ,ROBJ,LINE)
 ;  ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"F",ROBJ,HOBJ,LINE))
 ;  ^%ZAPM.bs.BSpgWEBF("XREF",DIR,"J",HOBJ,LINE))
	;
TOOLX	; TO BE MOVED TO TKWEBT
	;  RENAME
	;  MOVE TO - DIRECTORY - NAME
	;  DELETE
	;  NOTES
	;
 I TOFOLD]"" S:TONAME="" TONAME=TOJB M ^%ZAPM.bs.BSpgWEBF("PAGES",TOFOLD,TONAME)=^%ZAPM.bs.BSpgWEBF("PAGES",TDIR,TOBJ) S DIR=TOFOLD,FILE=TONAME D XREF^%ZAPM.bs.BSpgWEBF
 I RENAME]"" M ^%ZAPM.bs.BSpgWEBF("PAGES",TDIR,RENAME)=^%ZAPM.bs.BSpgWEBF("PAGES",TDIR,TOBJ) S DIR=TDIR,FILE=TONAME D XREF^%ZAPM.bs.BSpgWEBF
	;
	;   NEED DELETE XREF LOGIC
	;
	;
BRAND	; BRANDING LOGIC
	;  VIRGIN LINK = DNS    OR  DNS/DIRECTORY
	;  EMBED rHREFs = /Bxx_xx/PAGEx.HTM
	;  EMBED fHREFs = HTTP://DNS/Bxx_xx/PAGEx.HTM
	;
 ;  DATABASE    ^%ZAPM.bs.BSpgWEBD("B",BRAND,URL,DTTM)=""
	;                     (BRAND,DTTM)=ACTION ITEMS
 ;              ^%ZAPM.bs.BSpgWEBD("U",URL,BRAND,DTTM)=""
 ;              ^%ZAPM.bs.BSpgWEBD("D",DTTM,URL,BRAND)=""
	;
NEW	; NEW DIRECTORY
	R "Enter new Directory Name ",DIR,! Q:DIR=""
 S ^%ZAPM.bs.BSpgWEBF("PAGES",DIR)="",^%ZAPM.bs.BSpgWEBF(98,DIR)="",^%ZAPM.bs.BSpgWEBF(99,DIR)="" G NEW
	;
STRIP	; STRIPS BOLDS AND UNDERLINES FROM DOCS
	R "DIRECTORY-",DIR,! Q:DIR=""
 S (A,B)="" F  S A=$O(^%ZAPM.bs.BSpgWEBF("PAGES",DIR,A)) Q:A=""  F  S B=$O(^%ZAPM.bs.BSpgWEBF("PAGES",DIR,A,B)) Q:B=""  I ^(B)[$C(27) S C=^(B) D STR1 S ^(B)=C
	Q
STR1	F L=2:1:$L(C,$C(27)) S D=$P(C,$C(27),2,99),E=$E(D,1,3),C=$P(C,$C(27))_$S(E["[07":"<B>",E["[27":"</B>",1:"")_$P(D,"m",2,99)
	Q
